*        COPYRIGHT (C) 1986,1994,1997  PRYCROFT SIX PTY LTD
         TITLE 'ENQUEUE MONITOR DISPLAY LINE MAPPING'
ENQLINE  DSECT
ENQENTRY DS    XL(L'LOWX)
QNAME    DS    CL8
EXCLSIVE DS    CL1
RNAME    DS    CL72
ENQUSER  EQU   *-8,8
RLENGTH  DS    CL4
         DS    CL1
ENQSYS   DS    CL8
         DS    CL1
RESUCB   DS    CL4
RESFLAG  DS    CL1
RESVOL   DS    CL6
         DS    CL1
ENQJOB   DS    CL8
SMC      DS    CL3
NQDL     EQU   *-QNAME
NQLL     EQU   *-ENQENTRY
         SPACE 2
         LCLC  &TSUQNAM
         LCLC  &ODDQNAM
         LCLC  &SPFQNAM
         AIF   (&MVSMON).MVSENQ
&TSUQNAM SETC  'SYSKEQUA'
&ODDQNAM SETC  'SYSAIMS0'
&SPFQNAM SETC  'SPFDSN  '
         AGO   .MSPENQ
.MVSENQ  ANOP
&TSUQNAM SETC  'SYSIKJUA'
&ODDQNAM SETC  'SPFUSER '
&SPFQNAM SETC  'SPFEDIT '
.MSPENQ  ANOP
         TITLE 'ENQUEUE MONITOR SUBROUTINE'
&IMPSECT CSECT
         USING ENQUEUES,R11
ENQUEUES DS    0H
         GETMAIN VRC,LV=(1048576,8192),SP=SUBPOOL,LOC=ANY
         LTR   R15,R15           GETMAIN OK?
         BZ    GQGMOKAY          YES.
         LA    R1,NSMSG          NO, NOTIFY USER OF
         LA    R0,NSMSGLEN           GETMAIN FAILURE
         B     PUTSCRNE              AND GET NEXT USER REQUEST.
GQGMOKAY STM   R0,R1,GQREGS      PREPARE PARAMETERS FOR FREEMAIN.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         SRL   R0,10             DIVIDE LENGTH BY 1024 FOR KILOBYTES.
         CVD   R0,WORK
         MVC   GQHLPSIZ,ED5      LOAD EDIT MASK.
         ED    GQHLPSIZ,WORK+5   SHOW SIZE OBTAINED IN HELP PANEL.
         CLI   INCHARS+1,C' '    ANY OPTION PRE-SPECIFIED?
         BNH   REDOENQS          NO.
         MVC   INCHARS(L'INCHARS-1),INCHARS+1
         MVI   INCHARS+L'INCHARS-1,C' ' YES, ADJUST ANY COMMAND INPUT.
         SLR   R8,R8             FLAG QCB SCAN END FOR SCAN RESTART.
         B     CMDSCANE          PROCESS IT.
         SPACE
REDOENQS L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R2,2              ASSUME HEADINGS WILL BE SET UP OKAY.
         ST    R2,WINLINES       INITIALIZE LINE COUNTER.
         L     R2,WINBUFF        GET WINDOW BUFFER START ADDRESS.
         MVC   0(L'LOWX,R2),LOWX SUPPLY 3270 ATTRIBUTES.
         MVI   0(R2),X'FF'       FIXED FLAG - NON-SCROLLABLE LINE.
         LA    R2,LL-79(,R2)     POINT PAST ATTRIBUTES.
         MVI   0(R2),C' '        BLANK A LINE.
         MVC   1(NQDL-1,R2),0(R2)
         MVC   0(79,R2),NQSELINE INDICATE SELECTION CRITERIA.
         TM    WAITFLAG,X'88'    SELECTING ALL ENQUEUES BY PREFIX?
         BZ    ENQPRFLN          YES, SHOW PREFIX MASKS.
         MVC   38(40,R2),BLANKS  NO, BLANK OUT PREFIX TEMPLATES.
         MVC   38(L'ERESVHDG,R2),ERESVHDG
         TM    WAITFLAG,X'08'    SHOWING RESERVE ENQUEUES?
         BO    ENQJMASK          YES, GOOD GUESS FOR CRITERIA HEADING.
         MVC   38(L'EWAITHDG,R2),EWAITHDG
         B     ENQJMASK          NO, MUST BE RESOURCE CONFLICTS.
ENQPRFLN CLI   SELMAJNM,C' '     USING JOB NAME PREFIX SELECTION?
         BNH   ENQQMASK          NO.
         LH    R1,QPRFXLEN       GET PREFIX LENGTH.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,NQQLDMSK       LOAD THE PREFIX INTO THE DISPLAY LINE.
ENQQMASK CLI   SELMINNM,C' '     USING JOB NAME PREFIX SELECTION?
         BNH   ENQRMASK          NO.
         LH    R1,RPRFXLEN       GET PREFIX LENGTH.
         LA    R0,16             GET MAXIMUM SHOWABLE PREFIX LENGTH.
         CR    R1,R0             MASK TOO LONG FOR DISPLAY?
         BNH   ENQRMSKL          NO.
         LR    R1,R0             YES, JUST SHOW FIRST 16 CHARACTERS.
ENQRMSKL BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,NQRLDMSK       LOAD THE PREFIX INTO THE DISPLAY LINE.
ENQRMASK CLI   SELJOBNM,C' '     USING JOB NAME PREFIX SELECTION?
         BNH   ENQJMASK          NO.
         LH    R1,JPRFXLEN       GET PREFIX LENGTH.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,NQJLDMSK       LOAD THE PREFIX INTO THE DISPLAY LINE.
ENQJMASK TR    0(79,R2),XLATETBL HANDLE ANY UNDISPLAYABLE DATA.
         LA    R2,NQDL(,R2)      POINT TO NEXT DISPLAY LINE.
         MVC   0(L'HIGHX,R2),HIGHX   LOAD HEADING ATTRIBUTES.
         MVI   2(R2),X'F4'       USE UNDERSCORE FOR HEADING.    (I3279)
         MVI   5(R2),X'C4'       SUPPLY UNDERLINE ALSO.         (F9526)
         MVI   8(R2),X'F7'       SHOW HEADING IN WHITE.         (I3279)
         MVC   L'HIGHX(NQDL,R2),HEADINGE    LOAD DISPLAYABLE
         TR    L'HIGHX(NQDL,R2),XLATETBL         HEADING.
         LA    R2,NQLL(,R2)      POINT PAST HEADING.
         DROP  R15               WNDWMNGR.
         USING ENQLINE,R2
         MVI   QNAME,C' '        BLANK OUT FIRST DETAIL LINE.
         MVC   QNAME+1(NQDL-1),QNAME
         NI    WAITFLAG,X'88'    TURN OFF DISPLAYED SUPPRESSED QNAMES.
RESUMENQ L     R3,GQREG1         GET WORK AREA ADDRESS.
         L     R4,GQREG0         GET WORK AREA LENGTH.
         TM    WAITFLAG,X'80'    ENQUEUE WAITS ONLY REQUESTED?
         BZ    NOTWAITS          NO.
         GQSCAN AREA=((3),(4)),SCOPE=ALL,WAITCNT=1,TOKEN=GQTOKEN
         B     QSCANEND          QUEUE SCAN DONE.
NOTWAITS TM    WAITFLAG,X'08'    HARDWARE RESERVES ONLY REQUESTED?
         BZ    NOTRESVS          NO.
         GQSCAN AREA=((3),(4)),SCOPE=ALL,RESERVE=YES,TOKEN=GQTOKEN
         B     QSCANEND          QUEUE SCAN DONE.
NOTRESVS CLI   SELMAJNM,C' '     ANY QUEUE NAME FILTERING REQUESTED?
         BNH   NOQNAMES          NO.
         CLI   QPRFXLEN+1,8      ALL EIGHT CHARACTERS SPECIFIED?
         BNE   NOQNAMES          NO.
         LA    R0,8              EIGHT CHARACTERS TO CHECK.
         LA    R1,SELMAJNM       POINT TO THE REQUESTED QNAME.
QGENRCHK CLI   0(R1),C'*'        PLACE HOLDER CHARACTER?
         BE    NOQNAMES          YES, GENERIC SCAN REQUIRED.
         LA    R1,1(,R1)         NO, POINT TO NEXT CHARACTER.
         BCT   R0,QGENRCHK       CONTINUE THE CHECKING.
         GQSCAN AREA=((3),(4)),SCOPE=ALL,RESNAME=SELMAJNM,TOKEN=GQTOKEN
         B     QSCANEND          QUEUE SCAN DONE.
NOQNAMES GQSCAN AREA=((3),(4)),SCOPE=ALL,TOKEN=GQTOKEN
QSCANEND LR    R8,R1             LOAD RIB COUNTER.
         B     GQRTNTBL(R15)     ACT UPON RETURN CODE.
GQRTNTBL B     RIBGALOR   RC=0   RIBS GALORE, SO PROCESS THAT DATA.
         B     PUTALLNQ   RC=4   NOTHING FOUND.
         B     RIBGALOR   RC=8   AREA FILLED, SO PROCESS THAT DATA.
         B     PUTQEMSG   RC=12
         B     PUTQEMSG   RC=16
PUTQEMSG LA    R1,QEMSG   RC=20  NOTIFY USER OF
         LA    R0,QEMSGLEN              GQSCAN FAILURE
         B     PUTSCRNE                 AND GET NEXT USER REQUEST.
RIBGALOR ST    R0,GQSCANR0       STORE RIB BASE AND RIBE SIZES.
         L     R3,GQREG1         POINT TO FIRST RIB OF GQSCAN WORKAREA.
         B     SELNQCHK          PROCESS FIRST RIB.
         USING RIB,R3
NEXTRIB  LR    R3,R4             POINT TO THE PREVIOUS RIBE.
         AH    R3,NQRIBESZ       POINT TO THE NEXT RESOURCE INFO BLOCK.
         BCT   R8,SELNQCHK       DECREMENT RIB COUNTER AND PROCESS RIB.
         ICM   R1,B'1111',GQTOKEN
         BNZ   RESUMENQ          QUEUE SCAN NOT COMPLETE SO RESUME IT.
         B     PUTALLNQ          QUEUE SCAN COMPLETE SO SHOW RESULTS.
SELNQCHK MVC   QNAME,RIBQNAME    LOAD QUEUE NAME.
         TM    WAITFLAG,X'88'    OPTION W OR OPTION R ACTIVE?
         BNZ   LOADRNAM          YES, DON'T FILTER QNAMES.
         CLI   SELMAJNM,C' '     SELECTED MAJOR NAMES ONLY?
         BNH   FILTERQ           NO, FILTER OUT MASSES.
         LH    R10,QPRFXLEN      YES, GET QNAME PREFIX LENGTH.
         LA    R9,SELMAJNM       POINT TO DESIRED PREFIX.
         ST    R9,WORK+4         SAVE FIRST PARAMETER.
         LA    R9,RIBQNAME       POINT TO QUEUE NAME.
         STM   R9,R10,WORK2      SAVE REST OF PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             SEE IF ELIGIBLE.
         BE    GOTQNAME          YES, GO DO IT.
         B     DUMPRIB           NO, FORGET IT.
FILTERQ  CLI   SELJOBNM,C' '     SPECIFIC JOBS?
         BH    GOTQNAME          YES, DON'T SUPPRESS ANY MAJOR Q NAMES.
         CLI   SELMINNM,C' '     SPECIFIC RESOURCES?
         BH    GOTQNAME          YES, DON'T SUPPRESS ANY MAJOR Q NAMES.
         CLC   =CL8'SYSDSN  ',RIBQNAME
         BNE   TRYTSUNQ          CHECK FOR TIME SHARING USER ENQUEUE.
         TM    WAITFLAG,X'40'    SYSDSN ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'40'    NO, BUT IT HAS BEEN NOW.
         B     SUPRESED          SUPPRESS DATA SET ENQUEUE DISPLAY.
TRYTSUNQ CLC   =CL8'&TSUQNAM',RIBQNAME
         BNE   TRYODDNQ          CHECK FOR SPFUSER/SYSAIMS0.
         TM    WAITFLAG,X'20'    SYSIKJUA ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'20'    NO, BUT IT HAS BEEN NOW.
         B     SUPRESED          SUPPRESS TSO USERID ENQUEUE DISPLAY.
TRYODDNQ CLC   =CL8'&ODDQNAM',RIBQNAME
         BNE   TRYSPFED          CHECK FOR SPF EDIT DATA SET ENQUEUE.
         TM    WAITFLAG,X'10'    SPFUSER ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'10'    NO, BUT IT HAS BEEN NOW.
         B     SUPRESED          SUPPRESS SPF USERID ENQUEUE DISPLAY.
TRYSPFED CLC   =CL8'&SPFQNAM',RIBQNAME
         BNE   TRYSVSAM          CHECK FOR SYSVSAM.
         TM    WAITFLAG,X'04'    SPF EDIT DATA SET ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'04'    NO, BUT IT HAS BEEN NOW.
         B     SUPRESED          SUPPRESS SPF DATA SET ENQUEUE DISPLAY.
TRYSVSAM CLC   =CL8'SYSVSAM ',RIBQNAME
         BNE   TRYSPARE          CHECK FOR ???QNAME.
         TM    WAITFLAG,X'02'    SYSVSAM ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'02'    NO, BUT IT HAS BEEN NOW.
         B     SUPRESED          SUPPRESS VSAM OPEN ENQUEUE DISPLAY.
TRYSPARE L     R1,ATHTBADR       POINT TO CUSTOMIZABLE AREA.
         LA    R9,256(,R1)       POINT TO ZAPPABLE SUPPRESSION MASK.
         ST    R9,WORK+4         SAVE FIRST PARAMETER.
         LA    R9,RIBQNAME       POINT TO QUEUE NAME.
         LA    R10,8             GET THE QNAME LENGTH.
         STM   R9,R10,WORK2      SAVE REST OF PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             SEE IF ELIGIBLE.
         BNE   GOTQNAME          NOT ELIGIBLE FOR SUPPRESSION.
         TM    WAITFLAG,X'01'    ???QNAME ENCOUNTERED BEFORE?
         BO    DUMPRIB           YES.
         OI    WAITFLAG,X'01'    NO, BUT IT HAS BEEN NOW.
         L     R1,ATHTBADR       POINT TO CUSTOMIZABLE AREA.
         MVC   QNAME,256(R1)     HANDLE GENERIC FILTERING.
SUPRESED MVC   RNAME+16(27),=C'***SUPPRESSED-BY-DEFAULT***'
         MVC   ENQENTRY,LOWX     LOW INTENSITY.
         MVI   ENQENTRY+8,X'F2'  RED.                           (I3279)
         SLR   R5,R5             ZERO REMAINING RIBE COUNTER AS A FLAG.
         B     SHOWLINE          DISPLAY SUPPRESSION MESSAGE.
DUMPRIB  L     R5,RIBNRIBE       GET RIBE NUMBER FOR THIS RIB.
         LH    R4,RIBVLEN        GET LENGTH OF RIBVAR.
         AH    R4,NQRIBFSZ       GET THE TOTAL RIB LENGTH.
         AR    R4,R3             POINT TO THE FIRST RIBE OF THIS RIB.
         MH    R5,NQRIBESZ       GET NUMBER OF RIBES BY RIBE LENGTH.
         AR    R4,R5             POINT TO THE NEXT RIB.
         SH    R4,NQRIBESZ       POINT TO THE LAST RIBE OF THIS RIB.
         B     NEXTRIB           FORGET THIS RIB AND GET THE NEXT ONE.
GOTQNAME CLI   SELMINNM,C' '     SELECTED MINOR NAMES ONLY?
         BNH   LOADRNAM          NO, SHOW THE LOT.
         LA    R9,SELMINNM       POINT TO DESIRED PREFIX.
         ST    R9,WORK+4         SAVE FIRST POINTER OF PARAMETER LIST.
         LH    R10,RPRFXLEN      YES, GET RNAME PREFIX LENGTH.
         LA    R9,RIBEND         POINT TO RESOURCE NAME (RIBRNAME).
         STM   R9,R10,WORK2      SAVE REST OF PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             SEE IF ELIGIBLE.
         BNE   DUMPRIB           NO, FORGET IT.
LOADRNAM SLR   R6,R6             PREPARE FOR INSERT CHARACTER.
         IC    R6,RIBRNMLN       GET RESOURCE NAME LENGTH.
         CVD   R6,WORK
         MVC   RLENGTH,ED3       SHOW IT.
         ED    RLENGTH,WORK+6
         LA    R0,L'RNAME        GET MAXIMUM DISPLAYABLE LENGTH.
         CR    R6,R0             TOO LONG TO SHOW WHOLE RNAME?
         BNH   *+6               NO.
         LR    R6,R0             YES, REDUCE TO MAXIMUM.
         BCTR  R6,0              LESS 1 FOR EXECUTE.
         MVC   RNAME,BLANKS      BLANK OUT PREVIOUS RUBBISH.
         EX    R6,RNAMLOAD       LOAD RESOURCE NAME.
         BAS   R14,RNAMECHK      CALL RNAME FIELD EDIT ROUTINE.
         MVC   SMC,BLANKS        CLEAR OUT SET-MUST-COMPLETE FLAG.
         L     R5,RIBNRIBE       GET RIBE NUMBER FOR THIS RIB.
         LH    R4,RIBVLEN        GET LENGTH OF RIBVAR.
         AH    R4,NQRIBFSZ       GET OFFSET TO FIRST RIBE.
         AR    R4,R3             POINT TO FIRST RIBE.
         SH    R4,NQRIBESZ       POINT TO BEFORE FIRST RIBE.
         USING RIBE,R4
NEXTRIBE AH    R4,NQRIBESZ       POINT TO NEXT RIBE.
         MVC   ENQJOB,RIBEJBNM   MOVE IN JOBNAME.
         L     R1,CVTPTR(,0)     POINT TO THE CVT.
         USING CVT,R1
         CLC   RIBESYSN,CVTSNAME IS ENQUEUE FROM THIS SYSTEM?
         BNE   XTRNLENQ          NO, IT CAME FROM SOMEWHERE ELSE.
         DROP  R1                CVT.
         SLR   R1,R1
         ICM   R1,B'0011',RIBEASID
         BZ    CHKNQJOB          NO ASID SO LEAVE BLANK.
         SLL   R1,2              GET INDEX INTO ASVT (MULTIPLY BY 4).
         A     R1,ASVTADDR
         AIF   (&MVSMON).EASVT1A
         AIF   (&XATEST).EASVT1A
         AGO   .EASVT1E
.EASVT1A ANOP
         LA    R1,524(,R1)       GET ASVT ENTRY FOR THIS ASID.
.EASVT1E ANOP
*        MVC   ENQJOB,ENDED      LEAVE RIBEJBNM SHOWING.
         ICM   R15,B'1111',0(R1) IS THIS ADDRESS SPACE ACTIVE?
         BM    CHKNQJOB          NO, THE JOB HAS ENDED.
*        MVC   ENQJOB,STARTING   LEAVE RIBEJBNM SHOWING.
         ICM   R1,15,JBNI(R15)   LOAD PTR TO JOBNAME.
         BNZ   LODUNAME
         ICM   R1,15,JBNS(R15)   LOAD PTR TO JOBNAME.
         BZ    CHKNQJOB          JOB HAS NO NAME.
LODUNAME MVC   ENQJOB,0(R1)      OTHERWISE, MOVE IN JOBNAME.
         CLI   ENQJOB,X'49'      IS IT UNPRINTABLE?
         BH    CHKNQJOB          NO, JOB NAME HAS BEEN ASCERTAINED.
         MVC   ENQJOB,UNKNOWN    YES, JOB NAME NOT KNOWN.
         B     CHKNQJOB          CHECK JOB NAME SELECTION.
XTRNLENQ MVC   ENQSYS,RIBESYSN   LOAD SYSTEM NAME OF EXTERNAL ENQUEUE.
CHKNQJOB TM    WAITFLAG,X'88'    OPTION W OR OPTION R ACTIVE?
         BNZ   GOTNQJOB          YES, DON'T FILTER JOB NAMES.
         CLI   SELJOBNM,C' '     SELECTED JOB NAMES ONLY?
         BNH   GOTNQJOB          NO, SHOW THE LOT.
         LH    R10,JPRFXLEN      YES, GET LENGTH OF JOB NAME PREFIX.
         LA    R9,SELJOBNM       POINT TO DESIRED PREFIX.
         ST    R9,WORK+4         SAVE FIRST PARAMETER.
         LA    R9,ENQJOB         POINT TO JOB NAME.
         STM   R9,R10,WORK2      SAVE REST OF PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             COMPARE JOB NAME WITH PREFIX.
         BE    GOTNQJOB          KEEP IT IF IT MATCHES.
         BCT   R5,NEXTRIBE       NO MATCH, PROCESS NEXT RIBE.
         B     NEXTRIB           NO MORE RIB EXTENTS, PROCESS NEXT RIB.
GOTNQJOB DS    0H
         AIF   (&MSPMON).ENOX1
         BAS   R14,RNMEXTRA      SUPPLY SYSTEM ID OR USER ASID MAYBE.
.ENOX1   ANOP
         TM    RIBSCOPE,RIBGLBL  SCOPE=GLOBAL?
         BZ    NQLNCOLR          NO, NO NEED TO APPEND SYSTEM NAME.
         CLC   BLANKS(4),RIBESYSN+4 SYSTEM NAME LESS THAN FIVE BYTES?
         BNE   NQLNCOLR             NO, GO DECIDE DISPLAY COLOUR.
         CLC   BLANKS(6),ENQJOB+3   ROOM FOR SYSTEM NAME IN JOB FIELD?
         BNE   NQLNCOLR             NO, GO DECIDE DISPLAY COLOUR.
         MVI   ENQJOB+3,C'-'        YES.
         MVC   ENQJOB+4(4),RIBESYSN SHOW SYSTEM NAME OF ENQ'S ASID.
NQLNCOLR MVC   ENQENTRY,LOWX        LOW INTENSITY.
         MVI   ENQENTRY+8,X'F4'     GREEN.                      (I3279)
         TM    RIBERFLG,RIBETYPE    SHARED ENQUEUE?
         BO    SHARENQ              YES.
         MVI   EXCLSIVE,C'*'        NO, INDICATE EXCLUSIVE.
SHARENQ  TM    RIBESFLG,RIBESTAT    IS THE RESOURCE OWNED?
         BO    HAVEDISP             YES.
         MVI   ENQENTRY+4,X'F8'     HIGH INTENSITY FOR WAIT ENQUEUE.
         MVI   ENQENTRY+5,X'50'     REVERSE VIDEO FOR WAIT.     (F9526)
         OI    ENQENTRY+8,X'01'     WAITING SO ADD BLUE.        (I3279)
HAVEDISP DS    0H
         TM    RIBERFLG,RIBERESV    RESERVE?
         BZ    SOFTENQ              NO.
         CLI   ENQENTRY+4,X'F8'     ALREADY HIGH INTENSITY FOR WAIT?
         BE    ALREDYHI             YES, DON'T OVERWRITE FOR RESERVE.
         MVI   ENQENTRY+4,X'F4'     MEDIUM INTENSITY FOR DASD RESERVE.
ALREDYHI OI    ENQENTRY+8,X'02'     ADD RED FOR DASD RESERVE.   (I3279)
         MVC   RESUCB+1(10),=C'NOT RESRVD'
         TM    RIBERFLG,RIBERESC    RESERVE CONVERTED TO GLOBAL ENQ?
         BZ    GRSCNVRT             NO.
         MVI   RESFLAG,C'¢'         YES, FLAG CONVERTED RESERVE.
GRSCNVRT ICM   R6,B'1111',RIBEUCB   GET UCB ADDRESS.
         BZ    SOFTENQ              NO ADDRESS, RESERVE NOT PERFORMED.
         L     R1,CVTPTR(,0)        POINT TO THE CVT.
         USING CVT,R1
         CLC   RIBESYSN,CVTSNAME    IS ENQUEUE FROM THIS SYSTEM?
         BNE   SOFTENQ              NO, DON'T CHECK GARBAGE ADDRESS.
         DROP  R1                   CVT.
         CLI   2(R6),X'FF'          STANDARD UCB?
         BNE   SOFTENQ              NO, DON'T LOAD GARBAGE.
         UNPK  WORK(5),4(3,R6)
         TR    WORK(4),HEX-C'0'
         MVC   RESUCB,WORK          UCB NAME.
         MVC   RESVOL,28(R6)        VOLUME SERIAL NUMBER.
         TM    RIBERFLG,RIBERESC    RESERVE CONVERTED TO GLOBAL ENQ?
         BO    SOFTENQ              YES.
         CLI   36(R6),0             NO, ANY RESERVES ON THIS DASD?
         BNE   SOFTENQ              YES, GOOD.
         MVI   RESFLAG,C'¬'         NO, FLAG IT.
SOFTENQ  DS    0H
         CLI   SELJOBNM,C' '     JOB SELECTION ACTIVE?
         BNH   SMCTEST           NO, DON'T RELOAD QNAME AND RNAME.
         TM    WAITFLAG,X'88'    OPTION W OR OPTION R ACTIVE?
         BNZ   SMCTEST           YES, DON'T RELOAD QNAME AND RNAME.
         MVC   QNAME,RIBQNAME    LOAD QUEUE NAME.
         SLR   R6,R6             PREPARE FOR INSERT CHARACTER.
         IC    R6,RIBRNMLN       GET RESOURCE NAME LENGTH.
         BCTR  R6,0              LESS 1 FOR EXECUTE.
         CLI   RNAME+58,C')'     IS THIS A HARDWARE RESERVE?
         BE    RSVRNAME          YES, DON'T OVERWRITE RESERVE DETAILS.
         MVC   RNAME,BLANKS      BLANK OUT PREVIOUS RUBBISH.
RSVRNAME MVC   RNAME(47),BLANKS  BLANK OUT PREVIOUS RUBBISH.
         EX    R6,RNAMLOAD       LOAD RESOURCE NAME.
         BAS   R14,RNAMECHK      CALL RNAME FIELD EDIT ROUTINE.
         AIF   (&MSPMON).ENOX2
         BAS   R14,RNMEXTRA      SUPPLY SYSTEM ID OR USER ASID MAYBE.
.ENOX2   ANOP
SMCTEST  TM    RIBERFLG,RIBEMC   MUST COMPLETE?
         BZ    NOTMC             NO.
         MVC   SMC+1(2),=C'MC'   YES, FLAG AS MUST-COMPLETE.
         B     SHOWLINE
NOTMC    MVI   SMC+1,C'<'        FLAG RIB AS LOCAL.
         TM    RIBSCOPE,RIBGLBL  SCOPE=GLOBAL?
         BZ    SCOPETST          NO.
         MVI   SMC+1,C'>'        YES, FLAG AS GLOBAL.
SCOPETST MVI   SMC+2,C'J'        INDICATE JOB-STEP-WIDE ENQUEUE.
         TM    RIBSCOPE,RIBSTEP  SCOPE=STEP?
         BO    SHOWLINE          YES.
         MVI   SMC+2,C'O'        SHOW AS OPERATING-SYSTEM-WIDE ENQUEUE.
         TM    RIBSCOPE,RIBSYS   SCOPE=SYSTEM?
         BO    SHOWLINE          YES.
         MVI   SMC+2,C'G'        SHOW AS COMPLEX-WIDE (GLOBAL) ENQUEUE.
         TM    RIBSCOPE,RIBSYSS  SCOPE=SYSTEMS?
         BO    SHOWLINE          YES.
         MVI   SMC+2,C'?'        NO, UNKNOWN TYPE.
SHOWLINE TR    LL-79(NQDL,R2),XLATETBL  HANDLE WACKY RESOURCE NAMES.
         LA    R2,NQLL(,R2)      ADJUST BUFFER POINTER.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R0,1
         A     R0,WINLINES       INCREMENT LINE COUNTER.
         ST    R0,WINLINES
         MVC   0(2,R2),WINFULL   PREPARE FOR FULL WINDOW.
         C     R2,WINEND         SEE IF THE BUFFER IS FULL YET.
         BNL   PUTSCRNE          IF YES, THEN GO PUT THIS SCREEN.
         DROP  R15               WNDWMNGR.
         MVI   QNAME,C' '        BLANK OUT DETAIL LINE.
         MVC   QNAME+1(NQDL-1),QNAME
         LTR   R5,R5             NO, IS THIS QNAME SUPPRESSED?
         BZ    DUMPRIB           YES, FORGET ABOUT THIS RIB.
         BCT   R5,NEXTRIBE       NO, PROCESS NEXT RIB EXTENT.
         B     NEXTRIB           NO MORE RIB EXTENTS, PROCESS NEXT RIB.
PUTALLNQ BAS   R1,SYSUMMRY       PRODUCE & SHOW SYSTEM SUMMARY LINE.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R1,1
         A     R1,WINLINES       INCREMENT LINE COUNTER.
         ST    R1,WINLINES
         MVC   0(2,R2),FFFF+2    SET FINAL LINE FLAG FOR WINDOW DATA.
         DROP  R15               WNDWMNGR.
PUTSCRNE LA    R0,LL             MAKE REGISTER 0 POSITIVE FOR OUTPUT.
GETSCRNE L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         BASR  R14,R15           CALL WINDOW MANAGER.
         CLI   DOWNCNTR,C' '     ARE WE IN AUTO REFRESH MODE?
         BH    ENQENTER          YES, PROCESS STACKED ENTERS.
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BZ    GETCMDNQ          NO, PROCESS SUBCOMMAND.
         BAS   R14,QUITSCAN      YES, TERMINATE THIS SCAN FOR EXIT.
         LM    R0,R1,GQREGS      RESTORE REGISTERS FOR FREEMAIN.
         FREEMAIN RU,LV=(0),A=(1),SP=SUBPOOL  FREE GQSCAN WORK AREA.
         BR    R9                RETURN TO MAINLINE VIA SCREENIO.
GETCMDNQ CLI   INCHARS,C' '      CONTINUE REQUESTED?
         BNE   CMDSCANE          NO, CHECK FOR ENQMON SUBCOMMAND.
ENQENTER C     R8,GQTOKEN        YES, DID QUEUE SCAN COMPLETE?
         BE    REDOENQS          YES, START OVER.
         B     RESUMENQ          NO, RESUME FROM WHERE SCAN STOPPED.
CMDSCANE CLI   INCHARS,C'W'      DASD RESERVE ONLY SELECTION?
         BNE   TRYMAJOR          IF NOT, TRY FOR MAJOR NAME PREFIX.
         MVI   WAITFLAG,X'80'    YES, SET ENQUEUE CONFLICT SWITCH.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         B     REDOENQS          RESTART QUEUE CONTROL BLOCK SCAN.
TRYMAJOR CLI   INCHARS,C'Q'      CHECK FOR MAJOR QUEUE NAME PREFIX.
         BNE   TRYMINOR          IF NOT, TRY FOR MINOR NAME PREFIX.
         XC    SELMAJNM,SELMAJNM ERASE PREVIOUS JOBNAME.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOMAJPFX          YES, DO IT.
         CLI   INCHARS+1,C' '    QNAME PREFIX SUPPLIED?
         BNH   EGETQNAM          NO.
         MVC   SELMAJNM,INCHARS+1   LOAD PRE-PROMPT SUPPLIED MASK.
         LA    R15,SELMAJNM+1    POINT TO FIRST BYTE TO CHECK.
         LA    R0,7              MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
EQNMLOOP CLI   0(R15),C' '       END OF MAJOR NAME PREFIX FOUND?
         BNH   NOMAJPFX          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,EQNMLOOP
         B     NOMAJPFX          EIGHT CHARACTER MASK SUPPLIED.
EGETQNAM NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
         LA    R1,QPRFXMSG
         LA    R0,MAJSELEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    EGETQNAM          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVC   SELMAJNM,INCHARS  EXTRACT SPECIFIED PREFIX.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
NOMAJPFX STH   R1,QPRFXLEN       STORE PREFIX LENGTH.
         B     REDOENQS
TRYMINOR CLI   INCHARS,C'R'      CHECK FOR MINOR QUEUE NAME PREFIX.
         BNE   TRYNQJOB          IF NOT, TRY FOR JOB PREFIX REQUEST.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOMINPFX          YES, DO IT.
         CLI   INCHARS+1,C' '    JOB PREFIX SUPPLIED?
         BNH   EGETRNAM          NO.
         MVC   INCHARS(L'INCHARS-1),INCHARS+1
         MVI   INCHARS+L'INCHARS-1,C' ' YES, ADJUST ANY COMMAND INPUT.
         LA    R15,INCHARS+1     POINT TO FIRST BYTE TO CHECK.
         LA    R0,39             MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
ERNMLOOP CLI   0(R15),C' '       END OF MINOR NAME PREFIX FOUND?
         BNH   NOMINPFX          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,ERNMLOOP
         B     NOMINPFX          FORTY CHARACTER MASK SUPPLIED.
EGETRNAM NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
         LA    R1,RPRFXMSG
         LA    R0,MINSELEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    EGETRNAM          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
NOMINPFX STH   R1,RPRFXLEN       STORE PREFIX LENGTH.
         XC    SELMINNM,SELMINNM ERASE PREVIOUS RESOURCE NAME.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         LTR   R1,R1             ANY PREFIX SUPPLIED?
         BM    REDOENQS          NO.
         EX    R1,LODMINNM       EXTRACT SPECIFIED PREFIX.
         B     REDOENQS
LODMINNM MVC   SELMINNM(0),INCHARS              <<< EXECUTED >>>
TRYNQJOB CLI   INCHARS,C'J'      CHECK FOR SPECIFIC JOB NAME PREFIX.
         BNE   RSVCHECK          IF NOT, TRY FOR RESERVE SELECTION.
         XC    SELJOBNM,SELJOBNM ERASE PREVIOUS JOBNAME.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOJOBPNQ          YES, DO IT.
         CLI   INCHARS+1,C' '    JOB PREFIX SUPPLIED?
         BNH   EGETJNAM          NO.
         MVC   SELJOBNM,INCHARS+1   LOAD PRE-PROMPT SUPPLIED MASK.
         LA    R15,SELJOBNM+1    POINT TO FIRST BYTE TO CHECK.
         LA    R0,7              MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
EJOBLOOP CLI   0(R15),C' '       END OF JOB NAME PREFIX FOUND?
         BNH   NOJOBPNQ          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,EJOBLOOP
         B     NOJOBPNQ          EIGHT CHARACTER MASK SUPPLIED.
EGETJNAM NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
         LA    R1,JPRFXMSG
         LA    R0,JOBSELEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    EGETJNAM          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVC   SELJOBNM,INCHARS  EXTRACT SPECIFIED PREFIX.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
NOJOBPNQ STH   R1,JPRFXLEN       STORE PREFIX LENGTH.
         B     REDOENQS          RESTART QUEUE CONTROL BLOCK SCAN.
RSVCHECK CLI   INCHARS,C'D'      DASD RESERVE ONLY SELECTION?
         BNE   NORMLCHK          NO, TRY FOR NORMAL MODE.
         MVI   WAITFLAG,X'08'    YES, SET RESERVE SELECTION FLAG.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         B     REDOENQS          RESTART QUEUE CONTROL BLOCK SCAN.
NORMLCHK CLI   INCHARS,C'P'      SELECTION SUBJECT ONLY TO PREFIXES?
         BNE   GIVEHLPE          NO, DROP A HINT.
         MVI   WAITFLAG,X'00'    YES, RESET WAIT AND RESERVE FLAGS.
         BAS   R14,QUITSCAN      TERMINATE THIS SCAN FOR RESTART.
         B     REDOENQS          RESTART QUEUE CONTROL BLOCK SCAN.
GIVEHLPE MVC   INBUFF(8),SBLST   REQUEST SUBCOMMAND LIST FROM TUTORIAL.
         MVI   TGETLEN+1,8       SUPPLY LENGTH OF INPUT TO PARSE.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRNE          GO DISPLAY HELP PANEL.
         TITLE 'ENQUEUE MONITOR - TERMINATE CURRENT QUEUE SCAN'
QUITSCAN ICM   R1,B'1111',GQTOKEN
         BZR   R14               CURRENT SCAN ALREADY ENDED SO RETURN.
         GQSCAN QUIT=YES,TOKEN=GQTOKEN
         BR    R14               RETURN TO CALLER.
         TITLE 'ENQUEUE MONITOR - EDIT RESOURCE NAME'
RNAMECHK DS    0H
         AGO   .ENQHEX
         CLC   RIBQNAME,=CL8'SYSIKJBC'  BRODCAST?
         BE    RNAMEHEX                YES, SHOW RESOURCE NAME IN HEX.
         CLC   RIBQNAME,=CL8'SYSZTIOT'  TIOT?
         BE    RNAMEHEX                YES, SHOW RESOURCE NAME IN HEX.
         CLC   RIBQNAME,=CL8'SYSCTLG '  OS/CVOL?
         BNE   TSOECHK                  NO.
         MVC   RNAME+8(3),=C'+X'''      INDICATE HEX DIGITS.
         UNPK  RNAME+11(9),RIBEND+8(5)  SHOW UCB ADDR HEX IN EBCDIC.
         MVC   RNAME+19(12),=C''' (SYSCTLG.V'
         L     R1,WORK                  GET UCB ADDRESS.
         MVC   RNAME+31(6),28(1)        MOVE IN VOLUME SERIAL NUMBER.
         MVI   RNAME+37,C')'
         TR    RNAME+11(8),HEX-C'0'
         BR    R14
TSOECHK  CLC   RIBQNAME,=CL8'SYSZTSOE'  TSO/E SESSION MANAGER?
         BNE   QCMPSCHK                 NO.
         CLI   RIBRNMLN,12              YES, EXPECTED FORMAT?
         BNE   RGENLCHK                 NO.
         MVC   RNAME(3),=C' C'''        YES, INDICATE CHARACTERS.
         MVC   RNAME+3(4),RIBEND        LOAD CHARACTER PART.
         MVC   RNAME+7(4),=C'''+X'''    INDICATE HEX DIGITS.
         UNPK  RNAME+11(9),RIBEND+4(5)  SHOW HEX IN EBCDIC.
         TR    RNAME+11(8),HEX-C'0'
         MVI   RNAME+19,C''''
         BR    R14
QCMPSCHK CLC   RIBQNAME,=CL8'SRCOMPRS'  QUICK COMPRESS?
         BE    RNAMEHEX                 YES, SHOW RESOURCE NAME IN HEX.
         CLC   RIBQNAME,=CL8'SYSZIGGI'  TSO TSB?
         BNE   RGENLCHK                 NO.
         MVC   RNAME(7),=C'ASID=X'''    INDICATE HEX DIGITS.
         UNPK  RNAME+7(5),RIBEND(3)     SHOW HEX IN EBCDIC
         MVI   RNAME+11,C''''           HEXADECIMAL DIGITS.
         TR    RNAME+7(4),HEX-C'0'
         BR    R14
.ENQHEX  ANOP
RGENLCHK CLI   RIBRNMLN,29              NICE LENGTH TO SHOW IN HEX?
         BHR   R14                      NO.
         LR    R7,R2                    SAVE TRT REGISTER.
         EX    R6,ENQXTEST              ANY UNDISPLAYABLES?
         LR    R2,R7                    RESTORE TRT REGISTER.
         BZR   R14                      NO, RETURN WITHOUT ACTION.
RNAMEHEX LA    R6,1(,R6)                GET LENGTH OF RSOURCE NAME.
         MVC   RNAME(3),=C' X'''        INDICATE HEX DIGITS.
         LA    R7,RNAME+3               POINT TO FIRST DISPLAY BYTE.
         LA    R9,RIBEND                POINT TO RIBRNAME.
ERNHEXLP UNPK  0(3,R7),0(2,9)           UNPACK A BYTE.
         TR    0(2,R7),HEX-C'0'         CONVERT TO PRINTABLE HEX CHARS.
         MVI   2(R7),C''''              IN CASE LAST BYTE.
         LA    R7,2(,R7)                POINT TO NEXT DISPLAY POSITION.
         LA    R9,1(,R9)                POINT TO NEXT SOURCE BYTE.
         BCT   R6,ERNHEXLP              PROCESS NEXT BYTE IF ANY.
         BR    R14
ENQXTEST TRT   RIBEND(0),HEXENQTB       <<< EXECUTED >>>
         SPACE 2
         AIF   (&MSPMON).ENOX3
         AIF   (&MVSMON).ERX1
RNMEXTRA L     R1,CVTPTR(,0)
         USING CVT,R1
         CLC   RIBESYSN,CVTSNAME    ENQUEUE FROM THIS SYSTEM?
         DROP  R1                   CVT.
         BNE   ESHOWSYS             NO, POSSIBLY SHOW THE SYSTEM ID.
         TM    RIBERFLG,RIBESIDV    YES, ENQ ON BEHALF OF USER ASID?
.ERX1    ANOP
RNMEXTRA TM    RIBERFLG,RIBESIDV    YES, ENQ ON BEHALF OF USER ASID?
         BZR   R14                  NO, RETURN WITHOUT ACTION.
         CLC   RNAME+35(12),BLANKS NO, ROOM TO INDICATE ADDRESS SPACE?
         BNER  R14                  NO, RETURN WITHOUT ACTION.
         SLR   R1,R1
         ICM   R1,B'0011',RIBESAID
         BZR   R14                  SKIP NAME SEARCH IF ASID IS ZERO.
         MVC   ENQUSER-1(5),=X'4D81A28984' =C'(ASID'
         UNPK  ENQUSER+4(5),RIBESAID(3)
         TR    ENQUSER+4(4),HEX-C'0' SHOW HEX ASID FOR BAD JOB NAME.
         MVI   ENQUSER+8,C')'
         SLL   R1,2                 MULTIPLY ASID BY 4 FOR ASVT INDEX.
         A     R1,ASVTADDR
         LA    R1,524(,R1)          GET ASVT ENTRY FOR THIS ASID.
         ICM   R15,B'1111',0(R1)    POINT TO ASCB.
         BMR   R14                  ASID IS NOT ASSIGNED.
         ICM   R1,B'1111',JBNI(R15) LOAD POINTER TO JOB NAME.
         BNZ   ESHOWUSR             HAVE JOB NAME POINTER.
         ICM   R1,B'1111',JBNS(R15) LOAD POINTER TO JOB NAME.
         BZR   R14                  NO JOB NAME SO LEAVE ASID ON SHOW.
ESHOWUSR MVC   ENQUSER,0(R1)        DISPLAY USER ADDRESS SPACE NAME.
         BR    R14                  RETURN TO CALLER.
         AGO   .ENOX3
ESHOWSYS CLC   RNAME+31(16),BLANKS  NO, ROOM TO INDICATE SYSNAME?
         BNER  R14                  NO, RETURN WITHOUT ACTION.
         MVC   RNAME+37(8),RIBESYSN YES, LOAD THE SYSTEM NAME.
         MVC   RNAME+32(5),=C'(SYS='
         LA    R1,RNAME+44          POINT TO LAST SYSNAME BYTE.
RNAMBRAK CLI   0(R1),C' '           TRAILING BLANK?
         BNE   ENDRNAME             NO.
         BCTR  R1,0                 YES, TRY PREVIOUS BYTE.
         B     RNAMBRAK
ENDRNAME MVI   1(R1),C')'           CLOSE PARENTHESES.
         BR    R14                  RETURN TO CALLER.
.ENOX3   ANOP
         SPACE 2
NQQLDMSK MVC   40(0,R2),SELMAJNM                <<< EXECUTED >>>
NQRLDMSK MVC   51(0,R2),SELMINNM                <<< EXECUTED >>>
NQJLDMSK MVC   70(0,R2),SELJOBNM                <<< EXECUTED >>>
LOADJPFX MVC   HEADINGE+23(0),SELJOBNM          <<< EXECUTED >>>
QPRFXLEN DC    H'0'                  LENGTH FOR FOLLOWING CLC AND MVC.
MAJNMCMP CLC   SELMAJNM(0),RIBQNAME             <<< EXECUTED >>>
LOADQPFX MVC   HEADINGE+34(0),SELMAJNM          <<< EXECUTED >>>
RPRFXLEN DC    H'0'                  LENGTH FOR RESOURCE NAME PREFIX.
RNAMLOAD MVC   RNAME(0),RIBEND (= RIBRNAME)     <<< EXECUTED >>>
LOADRPFX MVC   HEADINGE+45(0),SELMINNM          <<< EXECUTED >>>
         DROP  R2,R3,R4,R11          ENQLINE, RIB, RIBE, ENQUEUES.
         TITLE 'ENQUEUE MONITOR - VARIABLES AND CONSTANTS'
GQTOKEN  DC    F'0'               GQSCAN TOKEN.
GQREGS   EQU   *,8                GETMAIN REGISTERS SAVE AREA.
GQREG0   DS    F'0'
GQREG1   DC    F'0'
GQSCANR0 DS    0F                 REGISTER 0 AFTER GQSCAN.
NQRIBFSZ DC    H'40'              SIZE OF RIB BASE (FROM GQSCAN).
NQRIBESZ DC    H'36'              SIZE OF RIBE (FROM GQSCAN).
WAITFLAG DC    X'00'
*        EQU   X'80'              ONLY SHOW ENQUEUE WAITS.
*        EQU   X'40'              SUPPRESSED MSG SHOWN FOR SYSDSN.
*        EQU   X'20'              SUPPRESSED MSG SHOWN FOR SYSIKJUA.
*        EQU   X'10'              SUPPRESSED MSG SHOWN FOR SPFUSER.
*        EQU   X'08'              ONLY SHOW HARDWARE RESERVES.
*        EQU   X'04'              SUPPRESSED MSG SHOWN FOR SPFEDIT.
*        EQU   X'02'              SUPPRESSED MSG SHOWN FOR SYSVSAM.
*        EQU   X'01'              SUPPRESSED MSG SHOWN FOR USER-ZAP.
GQHLPSIZ DC    CL6' '             DEBUG DIAGNOSTIC.
         DC    C'K BYTES OBTAINED FOR GQSCAN WORK AREA'
QPRFXMSG DC    X'C11140403C40400011C5C2&LOW'
         DC    C'ENTER QUEUE NAME PREFIX OR COMPLETE MAJOR QUEUE NAME'
         DC    X'&MED',C'===>',X'1BC94E132841F4'
SELMAJNM DC    XL8'00'
         DC    X'2841001BF8C2114BD2&LOW'
         DC    C'(USE',X'&HIGH',C'Q/',X'&LOW',C'FROM DETAIL SCREEN '
         DC    C'TO CANCEL MAJOR QUEUE NAME PREFIX SELECTION)'
         DC    X'11D1E3'
         DC    C'ENTER ALL EIGHT (8) BYTES INCLUDING TRAILING BLANKS WH+
               ERE APPROPRIATE'
         DC    X'11D2F3'
         DC    C'FOR IMPROVED PERFORMANCE.'
MAJSELEN EQU   *-QPRFXMSG
RPRFXMSG DC    X'C11140403C404000'
         DCS   SBA,(5,3)
         DC    X'&LOW'
         DC    C'ENTER RESOURCE NAME PREFIX'
         DCS   SBA,(6,3)
         DC    X'&MED',C'===>',X'1BC94E132841F4'
SELMINNM DC    XL40'00'
         DC    X'2841001BF8C2114BD2&LOW'
         DC    C'(USE',X'&HIGH',C'R/',X'&LOW',C'FROM DETAIL SCREEN '
         DC    C'TO CANCEL RESOURCE NAME PREFIX SELECTION)'
         DC    X'11D1E3'
         DC    C'(USE',X'&HIGH',C'*',X'&LOW'
         DC    C'FOR NON-SPECIFIC PLACE-HOLDER CHARACTERS)'
MINSELEN EQU   *-RPRFXMSG
NQSELINE DC    CL79' ENQUEUE DISPLAY SELECTION CRITERIA:  Q=******** R=+
               **************** J=******** '
HEADINGE DC    CL49' Q-NAME E RESOURCE-NAME '
         DC    CL32'RESOURCE-NAME '
         DC    C'RLEN OTHERSYS RESV-DEVICE   USER     '
EWAITHDG DC    C'ALL RESOURCE CONFLICTS (ENQUEUE WAITS)'
ERESVHDG DC    C'ALL DASD HARDWARE RESERVES'
NSMSG    DC    X'C5115DD81DF82842F22841F2'   "NO STORAGE" MESSAGE.
         DC    CL39'     INSUFFICIENT VIRTUAL STORAGE      '
         DC    X'1140402842001D402841F4130000'
NSMSGLEN EQU   *-NSMSG
QEMSG    DC    X'C5115DD81DF82842F22841F2'  "QUEUE SCAN ERROR" MESSAGE.
         DC    CL39'        QUEUE SCAN ABNORMALITY         '
         DC    X'1140402842001D402841F4130000'
QEMSGLEN EQU   *-QEMSG
         SPACE
HEXENQTB DS    0H
         DC    64X'FF',191X'00',X'FF'
         LTORG
         SPACE
         DS    0H
