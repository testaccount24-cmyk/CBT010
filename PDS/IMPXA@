*        COPYRIGHT (C) 2001       PRYCROFT SIX PTY LTD
         TITLE 'UNIX USER DATA BASE DISPLAY LINE MAPPING AND DSECTS'
UULINE   DSECT
UULNAT   DS    XL(L'LOWX)
UUUID    DS    CL12
         DS    CL1
UUUSER   DS    CL8
UUGID    DS    CL12
         DS    CL1
UUGROUP  DS    CL8
         DS    CL1
UUDIR    DS    CL36
UUPGM    EQU   *-16,16
         SPACE
UGLINE   DSECT
UGLNAT   DS    XL(L'LOWX)
UGGID    DS    CL12
         DS    CL1
UGGROUP  DS    CL8
         DS    CL58
         SPACE
         BPXYGIDN
         BPXYGIDS
         TITLE 'UNIX USER DATA BASE DISPLAY SUBROUTINE'
IMPXA    CSECT
         USING UNIXUSER,R11
UNIXUSER DS    0H
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         MVC   INCHARS(L'INCHARS-1),INCHARS+1
         MVI   INCHARS+L'INCHARS-1,C' ' ADJUST ANY COMMAND INPUT.
         B     CMDSCAN@          PROCESS IT.
         SPACE
REDOUUID L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         L     R2,WINBUFF        GET WINDOW BUFFER START ADDRESS.
         SLR   R0,R0             GET ZERO.
         ST    R0,WINLINES       INITIALIZE LINE COUNTER.
         MVC   WINTOP,@TOPLINE   SET TOP-OF-SCREEN LINE NUMBER.
         DROP  R15               WNDWMNGR.
         SPACE
         MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   8(R2),X'F3'       SHOW HEADING IN PINK.          (I3279)
         CLI   @FLAG,C'G'        SHOWING GROUPS?
         BE    REDOUGID          YES.
         MVC   LL-79(79,R2),HEADING@
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   2(R2),X'F4'       USE UNDERSCORE FOR HEADING.    (I3279)
         MVI   5(R2),X'C4'       SUPPLY UNDERLINE ALSO.         (F9526)
         MVI   8(R2),X'F7'       SHOW HEADING IN WHITE.         (I3279)
         MVC   LL-79(79,R2),UUIDCOLS
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         SPACE
         USING UULINE,R2
NEXTUUID DS    0H
*        CALL  BPX1GPE,          SEQUENTIALLY ACCESS USER DATA BASE.
         L     R1,CVTPTR(,0)
         L     R1,CVTCSRT-CVT(,R1) CSRTABLE.
         L     R1,24(,R1)        CSR SLOT.
         L     R15,#BPX1GPE(,R1) ADDRESS OF THE SERVICE.
         CALL  (15),             SEQUENTIALLY ACCESS USER DATA BASE.   +
               (@RETVAL,@RETCD,@RSNCD),                                +
               VL,MF=(E,WORK)
         ICM   R3,15,@RETVAL     WAS AN ENTRY RETURNED?
         BNZ   SHOWUUID          YES, ADDRESS RETURNED.
         ICM   R3,15,@RETCD      END-OF-FILE?
         BZ    ENDOFUID          YES.
         MVC   UULNAT,HIGHX      NO, HIGH INTENSITY FOR ERROR CODES.
         MVC   LL-63(23,R2),=CL23'BPX1GPE ERROR - HEX RC='
         UNPK  LL-40(9,R2),@RETCD(5)
         TR    LL-40(8,R2),HEX-C'0'
         MVC   LL-32(5,R2),=CL5' RSN='
         UNPK  LL-27(9,R2),@RSNCD(5)
         TR    LL-27(8,R2),HEX-C'0'
         MVI   LL-19(R2),C' '
         B     DONEUUID          LINE NOW COMPLETE.
         USING GIDN,R3
SHOWUUID LA    R14,UUUSER        POINT TO TARGET.
         LA    R15,L'UUUSER      LENGTH OF TARGET.
         LA    R0,GIDN_U_NAME    POINT TO SOURCE.
         ICM   R1,15,GIDN_U_LEN  LENGTH OF SOURCE.
         ALR   R3,R1             ADJUST ADDRESSING FOR USER NAME.
         ICM   R1,8,BLANKS       LOAD PAD BYTE.
         MVCL  R14,R0            LOAD USER ID NAME.
         ICM   R0,15,GIDN_USERID LOAD USER ID
         CVD   R0,WORK
         MVC   UUUID,ED11
         ED    UUUID,WORK+2
         ICM   R0,15,GIDN_GROUPID AND GROUP ID.
         CVD   R0,WORK
         STCM  R0,15,UUGROUP     SAVE FOR LATER GROUP LOOK-UP.
         MVC   UUGID,ED11
         ED    UUGID,WORK+2
         LA    R14,UUDIR         POINT TO TARGET.
         LA    R15,L'UUDIR       LENGTH OF TARGET.
         LA    R0,GIDN_D_NAME    POINT TO SOURCE.
         ICM   R1,15,GIDN_D_LEN  LENGTH OF SOURCE.
         ALR   R3,R1             ADJUST ADDRESSING FOR PATH NAME.
         ICM   R1,8,BLANKS       LOAD PAD BYTE.
         MVCL  R14,R0            LOAD WORKING DIRECTORY NAME.
         LA    R14,UUPGM         POINT TO TARGET.
         LA    R15,L'UUPGM       LENGTH OF TARGET.
         LA    R0,GIDN_P_NAME    POINT TO SOURCE.
         ICM   R1,15,GIDN_P_LEN  LENGTH OF SOURCE.
         BZ    DONEUUID          DO NOT OVERLAY DIRECTORY IF NONE.
         ICM   R1,8,BLANKS       LOAD PAD BYTE.
         MVCL  R14,R0            LOAD INITIAL PROGRAM NAME.
         DROP  R3                GIDN.
DONEUUID BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         ICM   R3,15,@RETVAL     STILL PROCESSING ENTRIES?
         BNZ   NEXTUUID          YES, CREATE NEXT ENTRY LINE.
         DROP  R2                UULINE.
         USING WNDWMNGR,R15
ENDOFUID L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         L     R5,WINLINES       GET THE LINE COUNT.
         AHI   R5,-2             DO NOT COUNT HEADING LINES.
         BNP   SHOWSUM@          NO DETAIL LINES TO PROCESS.
         L     R4,WINBUFF        POINT TO DATA ACCUMULATED SO FAR.
         LA    R4,LL+LL(,R4)     POINT TO FIRST DETAIL LINE.
         USING UULINE,R4
UIDGIDLP CLI   UUUID-1,X'F5'     TURQUOISE LINE?
         BNE   SHOWSUM@          NO, END OF DETAIL LINES.
         ICM   R0,15,UUGROUP     YES, GET SAVED GID.
         C     R0,@UIDGID        SAME AS LAST CALL?
         BE    SHOWUGRP          YES, LOAD GROUP NAME.
         ST    R0,@UIDGID        NO, SAVE IT FOR CALL.
         MVC   @UIDGRP,BLANKS    CLEAR RESIDUAL DATA.
*        CALL  BPX1GGI,          ACCESS GROUP DATA BASE BY ID.
         L     R1,CVTPTR(,0)
         L     R1,CVTCSRT-CVT(,R1) CSRTABLE.
         L     R1,24(,R1)        CSR SLOT.
         L     R15,#BPX1GGI(,R1) ADDRESS OF THE SERVICE.
         CALL  (15),             ACCESS GROUP DATA BASE BY ID.         +
               (@UIDGID,@RETVAL,@RETCD,@RSNCD),                        +
               VL,MF=(E,WORK)
         ICM   R3,15,@RETVAL     WAS AN ENTRY RETURNED?
         BZ    SHOWUGRP          NO, NO DATA TO COPY.
         USING GIDS,R3
         LA    R14,@UIDGRP       POINT TO TARGET.
         LA    R15,L'@UIDGRP     LENGTH OF TARGET.
         LA    R0,GIDS_G_NAME    POINT TO SOURCE.
         ICM   R1,15,GIDS_G_LEN  LENGTH OF SOURCE.
         ICM   R1,8,BLANKS       LOAD PAD BYTE.
         MVCL  R14,R0            LOAD USER ID NAME.
         DROP  R3                GIDS.
SHOWUGRP MVC   UUGROUP,@UIDGRP   SHOW GROUP NAME.
         TR    LL-79(79,R4),XLATETBL    CLEAR UNPRINTABLES.
         LA    R4,LL(,R4)        ADJUST BUFFER POINTER.
         BCT   R5,UIDGIDLP       PROCESS NEXT LINE.
         B     SHOWSUM@          JUST IN CASE.
         DROP  R4                UULINE.
         SPACE
REDOUGID MVC   LL-79(79,R2),HEADNG2@
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   2(R2),X'F4'       USE UNDERSCORE FOR HEADING.    (I3279)
         MVI   5(R2),X'C4'       SUPPLY UNDERLINE ALSO.         (F9526)
         MVI   8(R2),X'F7'       SHOW HEADING IN WHITE.         (I3279)
         MVC   LL-70(9,R2),UGIDCOLS
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         SPACE
         USING UGLINE,R2
NEXTUGID DS    0H
*        CALL  BPX1GGE,          SEQUENTIALLY ACCESS GROUP DATA BASE.
         L     R1,CVTPTR(,0)
         L     R1,CVTCSRT-CVT(,R1) CSRTABLE.
         L     R1,24(,R1)        CSR SLOT.
         L     R15,#BPX1GGE(,R1) ADDRESS OF THE SERVICE.
         CALL  (15),             SEQUENTIALLY ACCESS GROUP DATA BASE.  +
               (@RETVAL,@RETCD,@RSNCD),                                +
               VL,MF=(E,WORK)
         ICM   R3,15,@RETVAL     WAS AN ENTRY RETURNED?
         BNZ   SHOWUGID          YES, ADDRESS RETURNED.
         ICM   R3,15,@RETCD      END-OF-FILE?
         BZ    SHOWSUM@          YES.
         MVC   UGLNAT,HIGHX      NO, HIGH INTENSITY FOR ERROR CODES.
         MVC   LL-63(23,R2),=CL23'BPX1GPE ERROR - HEX RC='
         MVI   LL-58(R2),C'G'
         UNPK  LL-40(9,R2),@RETCD(5)
         TR    LL-40(8,R2),HEX-C'0'
         MVC   LL-32(5,R2),=CL5' RSN='
         UNPK  LL-27(9,R2),@RSNCD(5)
         TR    LL-27(8,R2),HEX-C'0'
         MVI   LL-19(R2),C' '
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         B     SHOWSUM@
         SPACE
         USING GIDS,R3
SHOWUGID LA    R14,UGGROUP       POINT TO TARGET.
         LA    R15,66            LENGTH OF TARGET.
         LA    R0,GIDS_G_NAME    POINT TO SOURCE.
         ICM   R1,15,GIDS_G_LEN  LENGTH OF SOURCE.
         AR    R3,R1
         ICM   R1,8,BLANKS       LOAD PAD BYTE.
         MVCL  R14,R0            LOAD USER ID NAME.
         ICM   R0,15,GIDS_GROUPID
         CVD   R0,WORK
         MVC   UGGID,ED11
         ED    UGGID,WORK+2
         BAS   R14,@ADDLINE      LINE NOW COMPLETE.
         B     NEXTUGID          PROCESS NEXT GROUP.
         DROP  R3                GIDS.
         SPACE
SHOWSUM@ BAS   R1,SYSUMMRY       PRODUCE & SHOW SYSTEM SUMMARY LINE.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         LA    R1,1
         A     R1,WINLINES       INCREMENT LINE COUNTER.
         ST    R1,WINLINES
         MVC   0(2,R2),FFFF+2    SET FINAL LINE FLAG FOR WINDOW DATA.
         DROP  R15               WNDWMNGR.
PUTSCRN@ LA    R0,LL             MAKE REGISTER 0 POSITIVE FOR OUTPUT.
GETSCRN@ L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         BASR  R14,R15           CALL WINDOW MANAGER.
         L     R15,WINMANAD      YES, POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         MVC   @TOPLINE,WINTOP   SAVE TOP-OF-SCREEN LINE NUMBER.
         DROP  R15               WNDWMNGR.
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BOR   R9                YES, RETURN TO MAINLINE VIA SCREENIO.
         CLI   DOWNCNTR,C' '     ARE WE IN AUTO REFRESH MODE?
         BH    REDOUUID          YES.
CMDSCAN@ CLI   INCHARS,C'U'      DISPLAY UNIX USERS REQUESTED?
         BE    @SHOWSEL          YES.
         CLI   INCHARS,C'G'      DISPLAY UNIX GROUPS REQUESTED?
         BE    @SHOWSEL          YES.
         CLI   INCHARS,C' '      DISPLAY UPDATE REQUESTED?
         BNE   GIVEHLP@          NO, SHOW HELP PANEL.
         B     REDOUUID          REGENERATE DISPLAY.
@SHOWSEL CLC   @FLAG,INCHARS     CHANGE OF FUNCTION?
         BE    REDOUUID          NO, REGENERATE DISPLAY.
         MVC   @FLAG,INCHARS     YES, UPDATE FUNCTION FLAG.
         LA    R0,1
         ST    R0,@TOPLINE       SHOW NEW DISPLAY FROM THE TOP.
         B     REDOUUID          REGENERATE DISPLAY.
GIVEHLP@ MVC   INBUFF(8),SBLST   REQUEST SUBCOMMAND LIST FROM TUTORIAL.
         MVI   TGETLEN+1,8       SUPPLY LENGTH OF INPUT TO PARSE.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRN@          GO DISPLAY HELP PANEL.
         SPACE
@ADDLINE LA    R2,LL(,R2)        ADJUST BUFFER POINTER.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R0,1
         A     R0,WINLINES       INCREMENT LINE COUNTER.
         ST    R0,WINLINES
         MVC   0(2,R2),WINFULL   PREPARE FOR FULL WINDOW.
         C     R2,WINEND         SEE IF THE BUFFER IS FULL YET.
         BNL   PUTSCRN@          IF YES, THEN FORGET SUMMARY LINE.
         MVC   0(LL-79,R2),LOWX  SUPPLY ATTRIBUTE BYTES.
         MVI   LL-79(R2),C' '    BLANK A LINE.
         MVC   LL-78(78,R2),LL-79(R2)
         BR    R14
         SPACE
         DROP  R11               UNIXUSER.
         TITLE 'UNIX USER DATA BASE DISPLAY - VARIABLES AND CONSTANTS'
@TOPLINE DC    F'1'              TOP-OF-SCREEN LINE NUMBER.
@RETVAL  DC    F'0'              UNIX CALL RETURN VALUE.
@RETCD   DC    F'0'              UNIX CALL RETURN CODE.
@RSNCD   DC    F'0'              UNIX CALL REASON CODE.
@UIDGID  DC    F'-1'             USER'S GID.
@UIDGRP  DC    CL8' '            GROUP NAME.
@FLAG    DC    C'U'              SUBROUTINE FUNCTION FLAG.
HEADING@ DC    CL79'                          UNIX USER DATA BASE DISPL+
               AY                          '
HEADNG2@ DC    CL79'                         UNIX GROUP DATA BASE DISPL+
               AY                          '
UUIDCOLS DC    CL79'         UID USERNAME         GID GROUP    WORKING-+
               DIRECTORY   INITIAL-PROGRAM '
UGIDCOLS EQU   UUIDCOLS+30,9
         SPACE
         LTORG
