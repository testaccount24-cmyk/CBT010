*        COPYRIGHT (C) 1986  PRYCROFT SIX PTY LTD
         TITLE 'MAINLINE - OPTION/SUBROUTINE/MONITOR SELECTION'
         USING EXAMINEC,R12
         USING GOTAREA,R13
EXAMINEC SLR   R0,R0
         STH   R0,ENDDEPTH       "END" OUT OF WHOLE MONITOR SUBROUTINE.
         STH   R0,GLOSSPG#       CLEAR GLOSSARY PAGE NUMBER.
         ST    R0,CRNTGLOS       CLEAR GLOSSARY VECTOR ORIGIN.
         L     R1,INITADDR       GET ADDRESSABILITY TO SCRNHDR.
         USING INITIAL,R1        RESTORE EXPECTED HEADING FORMAT.
         MVC   HOME(HDRLEN),SCRNHDR
         DROP  R1                INITIAL.
         CLI   INCHARS,C'Z'      CHECK IF TERMINATION REQUESTED.
         BE    EODAD             BYE BYE BABY....
         CLI   INCHARS,C'X'      CHECK IF TERMINATION REQUESTED.
         BE    EODAD             BYE BYE BABY....
         MVC   INBUFF+1(2),ONE   FORCE AS IF PRIMARY INPUT.
FINDOPTN L     R3,OPLKUPAD       POINT TO FIRST SUBROUTINE ENTRY.
SCAN4MON CLC   0(1,R3),INCHARS   COMPARE ENTRY TO SELECTION.
         BE    FOUNDMON          GREAT, FOUND IT!
         BH    SHOWMENU          NOT FOUND BECAUSE IT WASN'T THERE.
         LA    R3,OPTBLEN(,R3)   POINT TO NEXT ENTRY.
         B     SCAN4MON          TRY NEXT ENTRY.
SHOWMENU L     R3,OPLKUPAD       USE FIRST ENTRY WHICH IS MAIN MENU.
FOUNDMON ST    R3,CRNTOPTN       REMEMBER ENTRY ADDRESS.
         MVC   OPTNATR,COPYATR1  GET PSCBATR1 IN WORK AREA.
         OC    OPTNATR,1(R3)     PROCESS AGAINST OPTION FLAG BYTE.
         TM    OPTNATR,X'FA'     SUFFICIENT AUTHORITY FOR THIS OPTION?
         BNO   SHOWMENU          NO, SHOW MAIN MENU.
         L     R15,WINMANAD      YES, POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R1,1
         ST    R1,WINTOP         RESET TOP-OF-SCREEN.
         MVI   WINSUFLN,X'FF'    NEGATE DATA STREAM SUFFIX LENGTH CODE.
         MVC   WINHDGCT(8),8(R3) COPY OPTION'S WINDOW VARIABLES.
         MVC   WINRIGHT,2(R3)
         NI    WINRIGHT,X'7F'
         DROP  R15               WNDWMNGR.
         L     R11,4(,R3)        LOAD OPTION'S START ADDRESS.
         BR    R11               BRANCH TO OPTION'S SUBROUTINE.
EXAMINEK SLR   R0,R0
         STH   R0,ENDDEPTH       "END" OUT OF WHOLE MONITOR SUBROUTINE.
         STH   R0,GLOSSPG#       CLEAR GLOSSARY PAGE NUMBER.
         ST    R0,CRNTGLOS       CLEAR GLOSSARY VECTOR ORIGIN.
         L     R1,INITADDR       GET ADDRESSABILITY TO SCRNHDR.
         USING INITIAL,R1        RESTORE EXPECTED HEADING FORMAT.
         MVC   HOME(HDRLEN),SCRNHDR
         DROP  R1                INITIAL.
         L     R3,OPLKUPAD
         CLI   INBUFF,3          PF3/15?
         BE    ENDPFKEY          YES.
         LA    R3,ADDRMON-OPLOOKUP(,R3)
         CLI   INBUFF,6          PF6/18?
         BE    FOUNDMON          YES, CALL ADDRESS SPACE MONITOR.
         LA    R3,DEVMON-ADDRMON(,R3)
         CLI   INBUFF,9          PF9/21?
         BE    FOUNDMON          YES, CALL DEVICE MONITOR.
*                                SHOULD NEVER GET HERE - ALL PFKS USED.
         LA    R3,LISTLSTR-DEVMON(,R3)
         STFSMODE ON,NOEDIT=YES  RESTORE VTAM FULL SCREEN MODE FOR PA1.
         B     FOUNDMON          CALL LIBRARY LISTER (BUG FREE SCREEN).
         SPACE
*         THE ABOVE CODE MEANS THAT A PA1/ATTN SHOULD BE ABLE TO BREAK
*         A "SCREEN ERASURE CAUSED BY ERROR RECOVERY PROCEDURE" LOOP
*         (ASSUMING OPTION "L" CAN COME UP WITH AN ERROR FREE SCREEN).
         SPACE
ENDPFKEY C     R3,CRNTOPTN       CURRENTLY SHOWING THE MAIN MENU?
         BNE   FOUNDMON          NO, SO "END" BACK TO IT.
         TM    MODESW3,TSU       TIME SHARING USER?
         BZ    FOUNDMON          NO, PF3 CAN'T STOP STARTED TASK.
         B     EODAD             YES, SO TERMINATE AND EXIT.
         TITLE 'ATTENTION INTERRUPT EXIT'
         USING ATTNEXIT,R15      ESTABLISH ADDRESSABILITY.
ATTNEXIT OI    MODESW2,ATTN      SET INTERRUPT REQUEST FLAG.
         TM    MODESW2,AUTH      APF AUTHORIZED?
         BZR   R14               NO, RETURN.
         STM   R14,R12,12(R13)   SAVE REGISTERS FOR EXTRA PROCESSING.
         AIF   (&MSPMON).ATSACOK
         SAC   X'000'            ENSURE PRIMARY SPACE MODE.
.ATSACOK ANOP
         L     R1,PSATOLD        GET POINTER TO CURRENT TCB.
         TM    30(R1),X'08'      IS TCBFJMC ON?
         BZ    ATTNTERM          NO, RETURN.
         DROP  R15               ATTNEXIT.
         SPACE 2
*        THIS CODE WILL CAUSE AN ATTENTION TO RESTART OTHER TASKS
*        IN THIS ADDRESS SPACE.  OTHERWISE THE TSO USER WOULD HAVE
*        TO BE CANCELLED AND FORCED TO GET OUT OF THE NON-SWAPPABLE
*        WAIT STATE CAUSED BY CROSS MEMORY FUNNIES IN SOME OPTIONS.
         SPACE
         LR    R11,R15           NEW BASE REGISTER FOR THIS EXIT.
         USING ATTNEXIT,R11      ESTABLISH ADDRESSABILITY.
         L     R1,PSAAOLD        POINT TO THE CURRENT ASCB.
         LH    R1,ASID(,R1)      GET CURRENT ASID.
         SSAR  R1                GET OUT OF CROSS MEMORY MODE.
         L     R1,AXVALUE        GET SAVED AX NUMBER.
         AXSET AX=(1)            SET AX = 0.
         MODESET MF=(E,MDSET0)   KEY ZERO REQUIRED FOR STATUS MACRO.
         STATUS RESET,MC,STEP    TURN OFF TCBJMC -MUST COMPLETE STATUS.
         AIF   (&MSPMON).ATOKSWP
         L     R1,RMCTADDR       POINT TO THE RMCT.
         USING RMCT,R1
         L     R1,RMCTMCT        POINT TO THE MCT.
         DROP  R1                RMCT.
         USING MCT,R1
         TM    MCTSFLGS,MCTSQA1+MCTSQA2 SQA SHORTAGE?
         BNZ   ATTNNSWP                 YES, STAY NON-SWAPPABLE.
         TM    MCTOFLGS,MCTASM1+MCTASM2 AUXILIARY STORAGE SHORTAGE?
         BNZ   ATTNNSWP                 YES, STAY NON-SWAPPABLE.
         TM    MCTCFLGS,MCTRLSHT+MCTB16SH REAL/PAGEABLE STG SHORTAGE?
         BNZ   ATTNNSWP                 YES, STAY NON-SWAPPABLE.
         DROP  R1                       MCT.
         SYSEVENT OKSWAP                NO, BECOME SWAPPABLE.
ATTNNSWP DS    0H
         AGO   .ATSWPOK
.ATOKSWP ANOP
         LA    R0,X'2A'          LOAD OKSWAP SYSEVENT CODE.
         SVC   95                ISSUE SYSEVENT SVC.
.ATSWPOK ANOP
         MODESET MF=(E,MDSETN0)  RETURN TO TASK'S KEY.
ATTNTERM LM    R14,R12,12(R13)   RESTORE REGISTERS.
         BR    R14               RETURN.
         DROP  R11               ATTNEXIT.
         SPACE
AXVALUE  DC    F'0'              SAVE AREA FOR ACCESS INDEX.
         TITLE 'MACRO LIST FORMS'
STAXLIST STAX  ATTNEXIT,MF=L
         SPACE 2
MDSET0   MODESET KEY=ZERO,MF=L
         SPACE 2
MDSETN0  MODESET KEY=NZERO,MF=L
         SPACE 2
MDSTSUP0 MODESET MODE=SUP,KEY=ZERO,MF=L
         SPACE 2
MDSTPROB MODESET MODE=PROB,KEY=NZERO,MF=L
         SPACE 2
MDSTSUP  MODESET MODE=SUP,MF=L
         SPACE 2
MDSTPRB  MODESET MODE=PROB,MF=L
         SPACE 2
         AIF   (&MSPMON).NOBEER
ALESERVL ALESERV MF=L
.NOBEER  ANOP
         TITLE 'VTOC READING SUBROUTINE INTERFACE'
*              THIS INTERFACE ROUTINE IS USED BECAUSE
*              XVTCREAD IS A 24-BIT PROGRAM.
*              THIS INTERFACE SHOULD BE INVOKED WITH A BASSM.
*              HENCE, THIS INTERFACE SHOULD RUN IN 24-BIT AMODE.
*              RETURN TO CALLING OPTION IS WITH A BSM.
*              HENCE, THE AMODE OF THE CALLING OPTION IS RESTORED.
         SPACE 2
VTOCCOMM ST    R14,SAVRETAD      SAVE CALLING OPTION RETURN DETAILS.
         L     R15,VTCRDADR      GET XVTCREAD ENTRY POINT ADDRESS.
         BASR  R14,R15           INVOKE XVTCREAD.
         L     R14,SAVRETAD      RESTORE RETURN ADDRESS AND MODE.
         BSM   0,R14             RETURN TO CALLING OPTION.
         TITLE 'SCREEN INPUT/OUTPUT MANAGER'
SCREENIO STM   R14,R12,SAVE+12   SAVE ALL GENERAL REGISTER VALUES.
         L     R11,STGCHKAD      GET STORAGE SHORTAGE CHECK ROUTINE EP.
         BASR  R14,R11           CALL IT.
         LM    R14,R12,SAVE+12   RESTORE ALL GENERAL PURPOSE REGISTERS.
         L     R8,SCRNIOAD
         BR    R8
         TITLE 'STACKED ENTER MANAGER'
STACKER  L     R1,CRNTOPTN
         TM    1(R1),X'04'       AUTO-UPDATE SUPPORTED?
         BZ    DORESHOW          NO, SUPPRESS AUTO-UPDATE REQUEST.
         ICM   R1,B'1111',DELAY  IS DELAY ZERO?
         BNZ   NOHOLD            NO, HOLD TPUTS NOT REQUIRED.
         MVI   TPUTFLG,X'0B'     YES, USE HOLD TPUTS (AVOID S0C9).
NOHOLD   NI    INCHARS,X'0F'     GET NUMERIC PART.
         SLR   R1,R1             CLEAR REGISTER.
         IC    R1,INCHARS        GET FIRST NUMBER.
         CLI   INCHARS+1,C'0'    SECOND DIGIT GIVEN?
         BL    CHECKAID          NO, SINGLE DIGIT REQUEST.
         MH    R1,TEN+2          YES, HAVE TENS DIGIT.
         NI    INCHARS+1,X'0F'   GET NUMERIC PART OF UNITS.
         SLR   R0,R0             CLEAR REGISTER.
         IC    R0,INCHARS+1      GET SECOND NUMBER.
         AR    R1,R0             GET COUNT OF STACKED ENTERS REQUESTED.
CHECKAID CLI   INBUFF,13         ENTER?
         BNL   NEXTONE           YES, UPDATE REFRESH COUNTER.
         ST    R1,SCRLAMT        NO, THIS MAY BE A SCROLL AMOUNT.
         BR    R9                RETURN TO CALLER.
         SPACE 2
WAITER   LH    R1,REFRESH#       GET STACK DEPTH.
         BCT   R1,NEXTONE        DECREMENT.
NEXTZERO STH   R1,REFRESH#       ZERO, SAVE COUNTER.
         MVC   DOWNCNTR,FFFF     ERASE COUNTER REPEATER.
         MVI   TPUTFLG,X'03'     USE NOHOLD TPUTS FOR MANUAL UPDATE.
         B     DELAYIT           EXECUTE REQUESTED DELAY.
DORESHOW L     R8,SCRNIOAD
         USING SCRNIO,R8
         B     DOORSHOW
         DROP  R8                SCRNIO.
         SPACE
NEXTONE  STH   R1,REFRESH#       SAVE DECREMENTED COUNTER VALUE.
         CVD   R1,WORK           REPORT REMAINING STACK DEPTH.
         OI    WORK+7,X'0F'
         UNPK  DOWNCNTR,WORK+6(2)
         SPACE
DELAYIT  ICM   R1,B'1111',DELAY  IS DELAY LENGTH ZERO?
         BZ    DELAIDIT          YES, AVOID SVC OVERHEAD.
         SPACE
         STIMER WAIT,BINTVL=DELAY
         SPACE
DELAIDIT TM    MODESW2,ATTN      ANY ATTENTION INTERRUPTION?
         BZR   R9                NO, RETURN TO CALLER.
         XC    REFRESH#,REFRESH#     ZERO COUNTER.
         MVC   DOWNCNTR,FFFF     ERASE COUNTER REPEATER.
         MVI   TPUTFLG,X'03'     USE NOHOLD TPUTS FOR MANUAL UPDATE.
         NI    MODESW2,255-ATTN  ZERO ATTENTION FLAG.
         STFSMODE ON,NOEDIT=YES  RESTORE VTAM FULL SCREEN MODE.
         BR    R9                DELAY DONE SO RETURN TO CALLER.
