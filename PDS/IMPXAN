*        COPYRIGHT (C) 1986,1994  PRYCROFT SIX PTY LTD
         TITLE 'NUCLEUS CSECT MAP DISPLAY SUBROUTINE'
         USING NUCLKMAP,R11
NUCLKMAP DS    0H
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         MVC   WINTOP,NTOPLINE   SET TOP-OF-SCREEN LINE NUMBER.
         DROP  R15               WNDWMNGR.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         MVC   INCHARS(L'INCHARS-1),INCHARS+1
         MVI   INCHARS+L'INCHARS-1,C' ' ADJUST ANY COMMAND INPUT.
         B     CMDSCANN          PROCESS IT.
         SPACE
REDONUCM L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         L     R2,WINBUFF        GET WINDOW BUFFER START ADDRESS.
         MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   8(R2),X'F3'       SHOW HEADING IN PINK.          (I3279)
         MVC   LL-79(79,R2),HEADINGN     LOAD DISPLAYABLE
         TR    LL-79(79,R2),XLATETBL          HEADING.
         LA    R2,LL(,R2)        POINT PAST HEADING.
         MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   2(R2),X'F4'       USE UNDERSCORE FOR HEADING.    (I3279)
         MVI   5(R2),X'C4'       SUPPLY UNDERLINE ALSO.         (F9526)
         MVI   8(R2),X'F7'       SHOW HEADING IN WHITE.         (I3279)
         MVC   LL-79(79,R2),NUCMCOLS     LOAD DISPLAYABLE
         TR    LL-79(39,R2),XLATETBL          HEADING.
         LA    R2,LL(,R2)        POINT PAST HEADING.
         LA    R1,2
         ST    R1,WINLINES       INITIALIZE LINE COUNTER.
         MVC   WINTOP,NTOPLINE   SET TOP-OF-SCREEN LINE NUMBER.
         DROP  R15               WNDWMNGR.
         BAS   R14,NUCMPDTL      GET NUCLEUS MAP ADDR AND ENTRY COUNT.
NEXTNUCE MVC   0(LL-79,R2),LOWX
         MVI   LL-79(R2),C' '    BLANK DISPLAY LINE.
         MVC   LL-78(78,R2),LL-79(R2)
         CLC   NUCMNAME,0(R3)    WAS THIS NAME REQUESTED?
         BNE   NUCATROK          NO.
         MVC   0(LL-79,R2),HIGHX
NUCATROK TM    12(R3),X'10'      JUST AN ENTRY POINT?
         BZ    NUCENTPT          YES, PROCESS IT.
         MVC   LL-73(8,R2),0(R3) NO, SHOW CSECT NAME.
         UNPK  LL-62(9,R2),8(5,R3)
         TR    LL-62(8,R2),HEX-C'0'
*        MVI   LL-54(R2),C' '    SHOW ENTRY POINT ADDRESS.
         UNPK  LL-51(7,R2),13(4,R3)
         TR    LL-51(6,R2),HEX-C'0'
         MVI   LL-45(R2),C' '    SHOW CSECT SIZE.
         TM    12(R3),X'23'      EXPLICIT AMODE?
         BZ    GOTNUCAM          NO, AMODE 24 BY DEFAULT.
         MVC   LL-42(8,R2),=C'AMODE 64'
         TM    12(R3),X'20'      AMODE 64?
         BO    GOTNUCAM          YES.
         MVI   LL-36(R2),C'2'    NO.
         TM    12(R3),X'02'      CORRECT?
         BZ    GOTNUCAM          YES.
         MVC   LL-36(2,R2),=C'31'
         TM    12(R3),X'01'      CORRECT?
         BZ    GOTNUCAM          YES.
         MVC   LL-36(3,R2),=C'ANY'
GOTNUCAM TM    12(R3),X'08'      RSECT?
         BZ    DONENCST          NO.
         MVC   LL-8(3,R2),=C'R-O'
DONENCST TR    LL-79(79,R2),XLATETBL    HANDLE BAD ENTRY POINT NAME.
         MVI   LL-63(R2),X'24'   MAKE ENTRY ADDRESS TAB SELECTABLE.
         MVI   LL-54(R2),X'25'
         B     DONENUCE          CSECT PROCESSING FINISHED.
NUCENTPT MVC   LL-30(8,R2),0(R3) SHOW ENTRY POINT NAME.
         UNPK  LL-19(9,R2),8(5,R3)
         TR    LL-19(8,R2),HEX-C'0'
*        MVI   LL-11(R2),C' '    SHOW ENTRY POINT ADDRESS.
         TR    LL-79(79,R2),XLATETBL    HANDLE BAD ENTRY POINT NAME.
         MVI   LL-20(R2),X'24'   MAKE ENTRY ADDRESS TAB SELECTABLE.
         MVI   LL-11(R2),X'25'
DONENUCE LA    R2,LL(,R2)        ADJUST BUFFER POINTER.
         LA    R3,16(,R3)        POINT TO NEXT NUCLEUS MAP ENTRY.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R0,1
         A     R0,WINLINES       INCREMENT LINE COUNTER.
         ST    R0,WINLINES
         MVC   0(2,R2),WINFULL   PREPARE FOR FULL WINDOW.
         C     R2,WINEND         SEE IF THE BUFFER IS FULL YET.
         BNL   PUTSCRNN          IF YES, THEN FORGET SUMMARY LINE.
         BCT   R4,NEXTNUCE       NO, CREATE NEXT ENTRY LINE.
         BAS   R1,SYSUMMRY       PRODUCE & SHOW SYSTEM SUMMARY LINE.
         LA    R1,1
         A     R1,WINLINES       INCREMENT LINE COUNTER.
         ST    R1,WINLINES
         MVC   0(2,R2),FFFF+2    SET FINAL LINE FLAG FOR WINDOW DATA.
         DROP  R15               WNDWMNGR.
PUTSCRNN LA    R0,LL             MAKE REGISTER 0 POSITIVE FOR OUTPUT.
GETSCRNN L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         BASR  R14,R15           CALL WINDOW MANAGER.
         L     R15,WINMANAD      YES, POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         MVC   NTOPLINE,WINTOP   SAVE TOP-OF-SCREEN LINE NUMBER.
         DROP  R15               WNDWMNGR.
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BOR   R9                YES, RETURN TO MAINLINE VIA SCREENIO.
         CLI   DOWNCNTR,C' '     ARE WE IN AUTO REFRESH MODE?
         BH    NUCAUTO           YES.
CMDSCANN CLI   INCHARS,C'A'      REQUEST TO LOCATE A NUCLEUS ADDRESS?
         BE    NUCADDR           YES.
         CLI   INCHARS,C'N'      REQUEST TO LOCATE AN ENTRY NAME?
         BE    NUCNAME           YES.
         CLI   INCHARS,C' '      DISPLAY UPDATE REQUESTED?
         BNE   GIVEHLPN          NO, SHOW HELP PANEL.
*        CLI   INBUFF+1,3        CURSOR ON HEADING LINE?
*        BL    REDONUCM          YES, RESHOW THE SCREEN.
*        CLI   INBUFF+2,0        CURSOR IN LEFT-MOST COLUMN?
*        BE    REDONUCM          YES, RESHOW THE SCREEN.
         SLR   R0,R0
         IC    R0,INBUFF+1       GET THE CURSOR ROW NUMBER.
         AHI   R0,-3             DO NOT COUNT HEADING LINES.
         BM    REDONUCM          CURSOR ON HEADING LINE.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         A     R0,WINTOP         GET DETAIL LINE NUMBER.
         AH    R0,WINHDGCT       COUNT THE HEADING LINES.
         C     R0,WINLINES       BEFORE SUMMARY LINE?
         BNL   REDONUCM          NO, CURSOR PAST END OF MAP.
         SH    R0,WINHDGCT       RESTORE MAP ENTRY RELATIVE INDEX.
         SLL   R0,4              MULTIPLY BY SIXTEEN FOR INDEX.
         L     R3,CVTPTR(,0)     POINT TO THE CVT.
         L     R3,1200(,R3)      POINT TO THE NUCLEUS MAP.
         ALR   R3,R0             POINT TO REQUESTED NUCLEUS MAP ENTRY.
         L     R1,=A(BRWSPNTR)
         MVC   0(4,R1),8(R3)     SUPPLY NEW BROWSE ADDRESS.
         NI    0(R1),X'7F'       RESET THE SIGN BIT.
         MVC   INBUFF(9),NZMSTR  SUPPLY THE ZOOM COMMAND STRING.
         MVI   TGETLEN+1,9       SUPPLY "INPUT" DATA LENGTH.
         SR    R0,R0
         STH   R0,JBTARGAS       ENSURE PRIMARY ASID BROWSE.
         ST    R0,JBTARGI#
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRNN          ZOOM TO OPTION "VB".
         DROP  R15               WNDWMNGR.
NUCAUTO  MVI   INBUFF,X'F8'      SIMULATE A SCROLL DOWN REQUEST.
         MVI   TGETLEN+1,3       SUPPLY LENGTH OF INPUT TO PARSE.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRNN          GO DISPLAY HELP PANEL.
NUCNAME  MVI   NUCNAMEP,X'C1'    RESET WCC.
         LA    R0,NNAMELEN
NUCNAMEX LA    R1,NUCNAMEP       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BOR   R9                YES, RETURN TO MAINLINE VIA SCREENIO.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    NUCNAME           YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVC   NUCMNAME,INCHARS  EXTRACT SPECIFIED ENTRY NAME.
         CLC   NUCMNAME,BLANKS   NULL NAME?
         BE    REDONUCM          YES, RESTART NUCLEUS MAP.
         BAS   R14,NUCMPDTL      GET NUCLEUS MAP ADDR AND ENTRY COUNT.
         LA    R1,1              GET FIRST LINE NUMBER.
NUCNSRCH CLC   NUCMNAME,0(R3)    FOUND IT?
         BE    NUCFOUND          YES.
         LA    R3,16(,R3)        NO, POINT TO NEXT ENTRY.
         LA    R1,1(,R1)         INCREMENT LINE NUMBER.
         BCT   R4,NUCNSRCH       TRY IT.
         LA    R0,NNAMELNX       NOT FOUND, REISSUE PROMPT.
         MVI   NUCNAMEP,X'C5'    SET BELL BIT ON IN WCC.
         B     NUCNAMEX
NUCADERR MVI   NUCADDRP,X'C5'    SET BELL BIT ON IN WCC.
         LA    R0,NADDRLNX       REISSUE PROMPT WITH ERROR MESSAGE.
         B     NUCADDRX
NUCADDR  MVI   NUCADDRP,X'C1'    RESET WCC.
         LA    R0,NADDRLEN
NUCADDRX LA    R1,NUCADDRP       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BOR   R9                YES, RETURN TO MAINLINE VIA SCREENIO.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    NUCADDR           YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVC   NUCMADDR,INCHARS  EXTRACT SPECIFIED ENTRY NAME.
         LA    R0,8              MAXIMUM NUMBER OF DIGITS IN ADDRESS.
         LA    R3,INCHARS        POINT TO FIRST ALLEGED HEX CHARACTER.
         SLR   R1,R1             ZERO WORK REGISTER.
         SLR   R15,R15           ZERO ACCUMULATOR.
NHEXLOOP CLI   0(R3),C' '        BLANK?
         BE    NEXTNNUM          YES, IGNORE IT.
         CLI   0(R3),C'A'        INVALID?
         BL    NUCADERR          YES.
         CLI   0(R3),C'9'        INVALID?
         BH    NUCADERR          YES.  (HOW WAS THAT DONE?)
         CLI   0(R3),C'G'        VALID?
         BL    NHEXALFA          YES, ALPHABETIC HEX CHARACTER.
         CLI   0(R3),C'0'        INVALID?
         BL    NUCADERR          YES.
         NI    0(R3),X'0F'       NO, GET NUMERIC PART.
         B     NHEXNMBR          NOW HAVE HEX DIGIT'S VALUE.
NHEXALFA TR    0(1,R3),NALFAHEX-C'A'
NHEXNMBR SLL   R15,4             PROMOTE ANY PREVIOUS DIGITS.
         IC    R1,0(,R3)         LOAD THIS DIGIT.
         OR    R15,R1            GATE INTO ACCUMULATOR.
NEXTNNUM LA    R3,1(,R3)         POINT TO NEXT POSSIBLE DIGIT.
         BCT   R0,NHEXLOOP       PROCESS NEXT POSSIBLE DIGIT POSSIBLY.
GOTNADDR LA    R15,0(,R15)       TURN OFF THE AMODE BIT.
         BAS   R14,NUCMPDTL      GET NUCLEUS MAP ADDR AND ENTRY COUNT.
         LA    R1,1              GET FIRST LINE NUMBER.
NUCLKUP@ C     R15,16+8(,R3)     GOT THE RIGHT SPOT?
         BL    NUCFOUND          YES.
         LA    R3,16(,R3)        NO, POINT TO THE NEXT ENTRY.
         LA    R1,1(,R1)         INCREMENT LINE NUMBER.
         BCT   R4,NUCLKUP@
NUCFOUND TM    12(R3),X'10'      CSECT?
         BO    NUCMPLOC          YES, SHOW THIS AS THE TOP DETAIL LINE.
         BCTR  R1,0              NO, BACK UP ONE ENTRY/LINE.
         AHI   R3,-16
         B     NUCFOUND
NUCMPLOC ST    R1,NTOPLINE       SPECIFY NEW TOP-OF-SCREEN.
         B     REDONUCM
GIVEHLPN MVC   INBUFF(8),SBLST   REQUEST SUBCOMMAND LIST FROM TUTORIAL.
         MVI   TGETLEN+1,8       SUPPLY LENGTH OF INPUT TO PARSE.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRNN          GO DISPLAY HELP PANEL.
         SPACE
NUCMPDTL L     R3,CVTPTR(,0)     POINT TO THE CVT.
         L     R3,1200(,R3)      POINT TO THE NUCLEUS MAP.
         L     R4,12(,R3)        GET LENGTH OF NUCLEUS MAP.
         SRL   R4,4              DIVIDE BY 16 TO GET ENTRY POINT COUNT.
         LA    R3,16(,R3)        POINT TO THE FIRST ENTRY.
         BCTR  R4,0              DECREMENT FOR HEADER.
         BR    R14               RETURN TO CALLER.
         SPACE
         DROP  R11               NUCLKMAP.
         TITLE 'NUCLEUS MAP DISPLAY - VARIABLES AND CONSTANTS'
NTOPLINE DC    F'1'              TOP-OF-SCREEN LINE NUMBER.
NUCMAPST DC    F'0'      \___    NUCLEUS MAP ADDRESS.
NENTRYCT DC    F'0'      /       NUCLEUS MAP ENTRY COUNT.
NALFAHEX DC    X'0A0B0C0D0E0F'   ALPHABETIC HEX VALUES.
NZMSTR   DC    X'7D40C11140C1'   READ HEADER FOR NUCLEUS ZOOM.
         DC    C'=VB'            CHARACTER STRING FOR NUCLEUS ZOOM.
HEADINGN DC    CL79'                       NUCLEUS CSECT AND ENTRY POIN+
               T MAP                       '
NUCMCOLS DC    CL79'      CSECT-NM   VIRTADDR   LENGTH               EN+
               TRY-PT   VIRTADDR           '
NUCADDRP DC    X'C11140403C40400011C5C2&LOW'
         DC    C'ENTER HEXADECIMAL VIRTUAL STORAGE ADDRESS IN NUCLEUS'
         DC    X'&MED',C'===>',X'1BC94E132841F4'
NUCMADDR DC    X'0000000000000000'
         DC    X'2841001BF0C2'
NADDRLEN EQU   *-NUCADDRP
         DC    X'11D1E2&HIGH'
         DC    C'REENTER - INVALID ADDRESS SPECIFIED'
NADDRLNX EQU   *-NUCADDRP
NUCNAMEP DC    X'C11140403C40400011C5C2&LOW'
         DC    C'ENTER NUCLEUS ENTRY NAME TO BE LOCATED'
         DC    X'&MED',C'===>',X'1BC94E132841F4'
NUCMNAME DC    X'0000000000000000'
         DC    X'2841001BF0C2'
NNAMELEN EQU   *-NUCNAMEP
         DC    X'11D1E2&HIGH'
         DC    C'REENTER - THE PREVIOUSLY SPECIFIED NAME WAS NOT FOUND'
NNAMELNX EQU   *-NUCNAMEP
         SPACE
         LTORG
