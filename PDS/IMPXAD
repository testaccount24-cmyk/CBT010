*        COPYRIGHT (C) 1986-2016  PRYCROFT SIX PTY LTD
         TITLE 'CREATE I/O DEVICE PERFORMANCE MONITOR TABLE'
         USING CREIOTBL,R4
CREIOTBL DS    0H                CREATE DEVICE STATISTICS TABLE.
         LA    R11,DEVICES       GET ADDRESSABILITY TO OPTION "D".
         LA    R6,2048(,R11)     SET UP SECOND
         LA    R6,2048(,R6)                    BASE REGISTER.
         USING DEVICES,R11,R6
         ST    R14,CREIOR14      SAVE THE RETURN ADDRESS.
         L     R15,CNFGTKAD      POINT TO IOCTOKEN AREA.
         XC    0(48,R15),0(R15)  CLEAR THE TOKEN FOR THIS UCB SCAN.
         ICM   R5,15,DEVTBL      DEVICE TABLE ALREADY THERE?
         BZ    NODEVTBL          NO.
         LM    R0,R1,0(R5)       YES, SO FREE IT FIRST.
         FREEMAIN RU,LV=(0),A=(1),SP=SUBPOOL
         ST    R15,DEVTBL        RESET DEVICE TABLE ADDRESS.
NODEVTBL L     R7,CVTPTR(,0)     POINT TO THE CVT.
         USING CVT,R7
         L     R3,CVTUCBA        POINT TO FIRST UCB ON CHAIN.
         LA    R5,1+1            INITIALIZE UCB COUNTER.
         TM    CVTOSLVL,CVTUCBSV UCB SERVICES AVAILABLE?
         BNO   FRSTUCB1          NO, CHECK FIRST UCB.
         L     R8,CNFGTKAD       YES, POINT TO IOCTOKEN AREA.
         UCBINFO DEVCOUNT,       GET THE NUMBER OF UCBS.               +
               COUNT=UCBCOUNT,                                         +
               GROUP=DEVICECLASS,DEVCLASS=ALL,                         +
               IOCTOKEN=(R8),                                          +
               MF=(E,DUCBINFO,COMPLETE)
         L     R5,UCBCOUNT       GET THE TOTAL UCB COUNT.
         AHI   R5,1+1            FINALIZE TABLE SIZE.
         B     DGETMAIN
         USING UCBOB,R3
NXTUCB1  ICM   R3,15,UCBNXUCB    GET UCB ADDRESS.
         BZ    DGETMAIN          END OF UCB CHAIN.
FRSTUCB1 LA    R5,1(,R5)         YES, INCREMENT DEVICE COUNTER.
         B     NXTUCB1           GET NEXT UCB.
DGETMAIN MHI   R5,IOENTLEN       GET NUMBER OF DEVICES TIMES IOENTLEN.
         GETMAIN RU,LV=(R5),SP=SUBPOOL,LOC=(31,64)
         ST    R5,0(,R1)         SAVE STORAGE SIZE.
         ST    R1,4(,R1)         SAVE STORAGE ADDRESS.
         XC    DSCNWORK,DSCNWORK CLEAR WORK AREA FOR THIS UCB SCAN.
         LA    R3,CVTUCBA-8      POINT TO "BEFORE" FIRST UCB ON CHAIN.
         LR    R5,R1             POINT TO THE DEVICE TABLE.
         ST    R5,DEVTBL         SAVE THE DEVICE TABLE ADDRESS.
         USING IOENTRY,R5
         L     R9,CMCTADDR       POINT TO CHANNEL MEASUREMENT CNTL TBL.
         USING CMCT,R9
         TM    CVTOSLV3,CVTH7707 Z/OS 1.4 OR LATER?
         BNO   DGETCMBV          NO, CANNOT HAVE ECMBS.
         TM    CMCTECMBFLAGS,CMCTECMBMODE
         BNO   DGETCMBV          DO NOT HAVE ECMBS.
         OI    MODESW4,ECMB      FLAG ECMBS BEING USED BY SYSTEM.
DGETCMBV L     R9,CMCTCMBV       POINT TO CHANNEL MEASUREMENT BLOCK.
         DROP  R9,R7             CMCT, CVT.
         TIME  BIN               GET THE CURRENT TIME.
         ST    R0,CREIOTIM       SAVE IT FOR LATER.
NXTUCB2A LA    R5,IOENTLEN(,R5)  POINT TO NEXT DEVICE TABLE ENTRY.
         XC    IOENTRY(IOENTLEN),IOENTRY
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BNO   NXTUCB2B          NO, FOLLOW STATIC UCB CHAIN.
         L     R8,CNFGTKAD       YES, POINT TO IOCTOKEN AREA.
         TM    MODESW2,AUTH      APF AUTHORIZED?
         BZ    DCOPYUCB          NO, HAVE TO COPY UCB.
         TM    OSLEVEL+1,CVTH5520 SP5.2 OR LATER FOR UCBPXPTR?
         BNO   DCOPYUCB          NO, HAVE TO COPY UCB.
         MODESET MF=(E,MDSTSUP)  YES, GET INTO SUPERVISOR STATE.
         UCBSCAN ADDRESS,        GET UCB ADDRESSES.                    +
               WORKAREA=DSCNWORK,                                      +
               UCBPTR=IOUCBADR,                                        +
               NOPIN,                                                  +
               UCBPXPTR=IOPFXADR,                                      +
               LOC=ANY,                                                +
               DYNAMIC=YES,                                            +
               RANGE=ALL,                                              +
               NONBASE=YES,                                            +
               DEVCLASS=ALL,                                           +
               IOCTOKEN=(R8),                                          +
               LINKAGE=SYSTEM,                                         +
               MF=(E,DSCN,COMPLETE)
         LR    R8,R15            SAVE UCBSCAN RETURN CODE.
         MODESET MF=(E,MDSTPRB)  REVERT TO PROBLEM STATE.
         LTR   R8,R8             WAS A UCB RETURNED BY UCBSCAN?
         BNZ   CREIORTN          NO, NO MORE UCB ENTRIES.
         L     R3,IOUCBADR       POINT TO THE UCB BASE.
         LA    R1,L'DSCNUCB-1    GET UCB BASE SEGMENT LENGTH CODE.
         CLI   UCBTBYT3,UCB3TAPE TAPE DEVICE CLASS?
         BE    *+8               YES, SHOULD NOT GET S0C4.
         LA    R1,L'DSCNUCB-9    NO, REDUCE LENGTH.
         EX    R1,DBASESEG       COPY THE UCB BASE SEGMENT.
         L     R1,IOPFXADR       POINT TO THE UCB PREFIX EXTENSION.
         MVC   DSCNPRFX(40),0(R1) COPY THE UCB PREFIX ENTENSION.
         B     DSCANUCB
DBASESEG MVC   DSCNUCB(0),0(R3)                 <<< EXECUTED >>>
DCOPYUCB UCBSCAN COPY,           SCAN UCBS, EVEN IF NOT APF'D.         +
               WORKAREA=DSCNWORK,                                      +
               UCBAREA=DSCNUCB,                                        +
               CMXTAREA=DSCNCMXT,                                      +
               DEVNCHAR=DSCNDEVN,                                      +
               UCBPAREA=DSCNPRFX,                                      +
               DCEAREA=DSCNDCE,                                        +
               DCELEN=DSCNDCEL,                                        +
               DYNAMIC=YES,                                            +
               RANGE=ALL,                                              +
               NONBASE=YES,                                            +
               DEVCLASS=ALL,                                           +
               IOCTOKEN=(R8),                                          +
               LINKAGE=SYSTEM,                                         +
               MF=(E,DSCN,COMPLETE)
         LTR   R15,R15           WAS A UCB RETURNED BY UCBSCAN?
         BNZ   CREIORTN          NO, NO MORE UCB ENTRIES.
DSCANUCB LA    R3,DSCNUCB        YES, POINT TO THE RETURNED UCB COPY.
         B     NXTUCB2C          JUMP OVER STATIC UCB CHAIN STUFF.
NXTUCB2B ICM   R3,15,UCBNXUCB    GET NEXT UCB ADDRESS.
         BZ    CREIORTN          HANDLE END OF STATIC UCB CHAIN.
         LR    R1,R3             POINT TO UCB.
         AHI   R1,-48            POINT INTO UCB PREFIX.
         ST    R3,IOUCBADR       SAVE ADDRESS OF UCB BASE.
         ST    R1,IOPFXADR       SAVE ADDRESS OF UCB PREFIX EXTENSION.
NXTUCB2C MVC   IOTODBIN,CREIOTIM GET TIME-OF-DAY.
         MVC   UCBDEVNO,UCBCHAN  GET DEVICE NUMBER.
         LR    R1,R3             POINT TO UCB.
         AHI   R1,-48            POINT INTO UCB PREFIX.
*        TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
*        BNO   *+8               NO, CONTINUE.
*        LA    R1,DSCNPRFX       YES, POINT TO UCB PREFIX COPY.
         USING UCBPDATA,R1
         XC    UCBUSYCT(6),UCBUSYCT       ZERO SAMPLING COUNTERS.
         SLR   R15,R15           CLEAR FOR INSERT.
         SLR   R8,R8             CLEAR FOR INSERT.
         ICM   R15,3,UCBMBI      GET CMB BLOCK INDEX FOR THIS DEVICE.
         TM    UCBPMCW1+1,UCBMCMB     MEASUREMENTS BEING STORED IN CMB?
         BO    CMBSTATS          YES, USE THEM.
         DROP  R1                UCBPDATA.
         TM    UCBTBYT3,UCB3TAPE NO, MAGNETIC TAPE DEVICE?
         BNO   NXTUCB2A          NO, GET NEXT UCB.
         ICM   R1,7,UCBXTNB      POINT TO MAGNETIC TAPE UCB EXTENSION.
         BZ    NXTUCB2A          HANDLE UCB ABOVE THE LINE.
         DROP  R3                UCBOB.
         USING UCBMT,R1
         ICM   R8,3,UCBSIO       GET SIO COUNT.
         ST    R8,SIOSOFAR       SAVE SIO COUNT.
         B     NXTUCB2A          GET NEXT UCB.
         DROP  R1                UCBMT.
CMBSTATS SLL   R15,5             GET OFFSET OF ENTRY INTO CMB.
         ALR   R15,R9            POINT TO ENTRY FOR THIS DEVICE.
         TM    MODESW4,ECMB      ECMB RATHER THAN CMB FOR DEVICE?
         BNO   CREIOCMB          NO, DIRECTLY ACCESS THE CMB.
         TM    MODESW2,AUTH      APF AUTHORIZED?
         BNO   CRECMBOK          NO, CANNOT ACCESS ECMBS.
         L     R15,IOSECMB@      YES, POINT TO IOSCMB ROUTINE.
         L     R1,IOUCBADR       POINT TO THE UCB COMMON SEGMENT.
         BASR  R14,R15           INVOKE IT.
         LTR   R15,R15           SUCCESS?
         BNZ   NXTUCB2A          NO.
CRECMBOK LA    R15,SECWORKA+48   YES, POINT TO RETURNED CMB.
CREIOCMB ICM   R8,3,0(R15)       GET SSCH COUNT.
         ST    R8,SIOSOFAR       SAVE SSCH COUNT.
         ICM   R8,3,2(R15)       GET SSCH DATA COLLECTION COUNT.
         ST    R8,SIOSAMPL       SAVE SSCH DATA COLLECTION COUNT.
         MVC   SIOCONTM(12),4(R15) SAVE DEVICE MEASUREMENT TIMES.
         MVI   UCBUSYCT,X'FF'    FLAG CMB USED FOR THIS DEVICE.
         B     NXTUCB2A          GET NEXT UCB.
         DROP  R5                IOENTRY.
         SPACE
CREIORTN L     R14,CREIOR14      RESTORE RETURN ADDRESS.
         BR    R14               RETURN TO CALLER.
         DROP  R4,R11,R6         CREIOTBL, DEVICES.
         SPACE
UCBCOUNT DC    F'0'              UCB COUNT FROM UCBINFO.
CREIOR14 DC    F'0'              SAVE AREA FOR RETURN ADDRESS.
CREIOTIM DC    F'0'              SAVE AREA FOR CURRENT TIME.
         UCBINFO PLISTVER=MAX,MF=(L,DUCBINFO)
         LTORG
         TITLE 'DEVICE MONITOR DISPLAY LINE MAPPING'
DEVLINE  DSECT
DEVENTRY DS    XL(L'LOWX)
VSAMFLAG DS    CL1
PAGEFLAG DS    CL1
SHRDFLAG DS    CL1
VOLUME   DS    CL6
         DS    CL1
DEVNAME  DS    CL4
         DS    CL1
STATUS1  DS    CL3               ALC/ON /OFF/SYS/UNL/VRY/BOX
         DS    CL1
STATUS2  DS    CL3               PVT/PUB/STG/SMS/INT-REQ
         DS    CL1
STATUS3  DS    CL3               RSD/RSV/REM/MNT-PND/REW
DEVPERM  DS    CL1
DEVPATH  DS    CL3
USECOUNT DS    CL4
OPNCOUNT DS    CL4
QUECOUNT DS    CL4
RSVCOUNT DS    CL4
DEVGRFMK DS    CL1
DEVGRFTM DS    CL32
VTOC     DS    CL4
DEVTYPNM DS    CL8
DEVSIORT DS    CL7
         DS    CL1
UNITUSER DS    CL8
CYLINDER DS    CL5
         DS    CL1
HEAD     DS    CL2
RECORD   DS    CL4
         DS    CL1
KEY      DS    CL1
         DS    CL1
IODRIVER DS    CL4
DEVSRVTM DS    CL6
DEVRSPTM DS    CL6
DEVBSYPC DS    CL4
UNITPCNT DS    CL1
         DS    CL1
DEVCU    DS    CL3
DEVCACHE DS    CL5
         DS    CL1
DEVDFW   DS    CL3
         DS    CL1
DEVDUPLX DS    CL3
         DS    CL1
DEVSTOGP DS    CL8
         DS    CL1
DEVSMSST DS    CL6
DEVTOTAL DS    CL9
DEVFREE  DS    CL9
DEVLARGE DS    CL9
DVDL     EQU   *-VSAMFLAG
DVLL     EQU   *-DEVENTRY
         TITLE 'DEVICE MONITOR SUBROUTINE'
IMPXA    CSECT
         USING DEVICES,R11,R6
DEVICES  DS    0H
         LA    R6,2048(,R11)     SET UP SECOND
         LA    R6,2048(,R6)                    BASE REGISTER.
         MVI   TGETFLG,TGETFLGN  PERFORM ASIS NOWAIT TGETS FOR DEVMON.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         CLI   INBUFF,9          ENTRY VIA PF9/21?
         BE    DASDCHK           YES, ANY COMMAND ALREADY IN PLACE.
DEVINITD MVC   INCHARS(L'INCHARS-1),INCHARS+1
         MVI   INCHARS+L'INCHARS-1,C' '  NO, ADJUST ANY COMMAND INPUT.
         B     DASDCHK           PROCESS IT.
         SPACE
REDODEVS L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R2,2              ASSUME HEADINGS WILL BE SET UP OKAY.
         ST    R2,WINLINES       INITIALIZE LINE COUNTER.
         L     R2,WINBUFF        GET WINDOW BUFFER START ADDRESS.
         DROP  R15               WNDWMNGR.
         LA    R15,WHATDEVS      POINT TO ROUTINE ENTRY POINT.
         BR    R15               GO SHOW CURRENT SELECTION CRITERIA.
         SPACE
         USING DEVLINE,R2
         USING UCBOB,R3
MASKTEST TM    UCBTBYT3,X'00'                   <<< EXECUTED >>>
DEVHDGOK L     R4,CVTPTR(,0)     POINT TO THE CVT.
         L     R3,CVTUCBA-CVT(,R4) POINT TO FIRST UCB.
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BNO   FIRSTUCB          NO, FIRST TIME THROUGH THE LOOP.
         XC    DSCNWORK,DSCNWORK CLEAR WORK AREA FOR THIS UCB SCAN.
NEXTUCB  TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BNO   XANXTUCB          NO, FOLLOW STATIC UCB CHAIN.
         L     R15,CNFGTKAD      YES, POINT TO IOCTOKEN AREA.
         CLI   CLASMASK,X'00'    CAN ANY DEVICES BE SELECTED?
         BE    SHOWDEVS          NO, SKIP UCBSCAN OVERHEAD.
         CLI   CLASMASK,X'20'    JUST INTERESTED IN DASD?
         BE    DUSCNCLS          YES, REDUCE UCBSCAN OVERHEAD.
         CLI   CLASMASK,X'80'    JUST INTERESTED IN TAPE?
         BE    DUSCNCLS          YES, REDUCE UCBSCAN OVERHEAD.
         CLI   CLASMASK,X'08'    JUST INTERESTED IN UREC?
         BE    DUSCNCLS          YES, REDUCE UCBSCAN OVERHEAD.
         CLI   CLASMASK,X'10'    JUST INTERESTED IN DISP?
         BE    DUSCNCLS          YES, REDUCE UCBSCAN OVERHEAD.
         UCBSCAN COPY,           SCAN UCBS, EVEN IF NOT APF'D.         +
               WORKAREA=DSCNWORK,                                      +
               UCBAREA=DSCNUCB,                                        +
               CMXTAREA=DSCNCMXT,                                      +
               DEVNCHAR=DSCNDEVN,                                      +
               UCBPAREA=DSCNPRFX,                                      +
               DCEAREA=DSCNDCE,                                        +
               DCELEN=DSCNDCEL,                                        +
               DYNAMIC=YES,                                            +
               RANGE=ALL,                                              +
               NONBASE=YES,                                            +
               DEVCLASS=ALL,                                           +
               IOCTOKEN=(R15),                                         +
               LINKAGE=SYSTEM,                                         +
               MF=(E,DSCN,COMPLETE)
         B     DUSCNCHK          CHECK UCBSCAN RESULTS.
DUSCNCLS UCBSCAN COPY,           SCAN UCBS OF A SPECIFIC DEVICE CLASS. +
               WORKAREA=DSCNWORK,                                      +
               UCBAREA=DSCNUCB,                                        +
               CMXTAREA=DSCNCMXT,                                      +
               DEVNCHAR=DSCNDEVN,                                      +
               UCBPAREA=DSCNPRFX,                                      +
               DCEAREA=DSCNDCE,                                        +
               DCELEN=DSCNDCEL,                                        +
               DYNAMIC=YES,                                            +
               RANGE=ALL,                                              +
               NONBASE=YES,                                            +
               DEVCID=CLASMASK,                                        +
               IOCTOKEN=(R15),                                         +
               LINKAGE=SYSTEM,                                         +
               MF=(E,DSCN,COMPLETE)
DUSCNCHK LTR   R15,R15           WAS A UCB RETURNED BY UCBSCAN?
         BNZ   DUSCNOT0          NO, HANDLE NON-ZERO RETURN CODE.
         LA    R3,DSCNUCB        YES, POINT TO THE RETURNED UCB COPY.
         B     FIRSTUCB          PROCESS IT.
XANXTUCB ICM   R3,15,UCBNXUCB    GET NEXT UCB ADDRESS.
         BZ    SHOWDEVS          NO MORE UCB ENTRIES.
FIRSTUCB IC    R1,CLASMASK       LOAD DEVICE CLASS SELECTION MASK.
         EX    R1,MASKTEST       CORRECT DEVICE TYPE?
         BZ    NEXTUCB           NO, TRY NEXT UCB.
         CLI   SELVOLID,C' '     VOLUME SERIAL PREFIX SELECTION?
         BNH   UCBVFY            NO, VERIFY UCB PREFIX CHECKING.
         LH    R9,VPRFXLEN       YES, GET VOLUME SERIAL PREFIX LENGTH.
         LA    R7,SELVOLID       POINT TO DESIRED PREFIX.
         LA    R8,UCBVOLI        POINT TO VOLUME SERIAL NUMBER.
         STM   R7,R9,WORK+4      SAVE PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             COMPARE VOLUME SERIAL WITH PREFIX.
         BNE   NEXTUCB           DIDN'T MATCH, SO GET THE NEXT ONE.
UCBVFY   CLI   SELUCBID,C' '     UNIT CONTROL BLOCK PREFIX SELECTION?
         BNH   LOADATTR          NO, LOAD ATTRIBUTE BYTES.
         LH    R9,UPRFXLEN       YES, GET UCB LABEL PREFIX LENGTH.
         LA    R7,SELUCBID       POINT TO DESIRED PREFIX.
         LA    R8,UCBNAME        POINT TO UCB NAME.
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BNO   *+8               NO, CONTINUE.
         LA    R8,DSCNDEVN       YES, POINT TO RETURNED DEVICE NUMBER.
         STM   R7,R9,WORK+4      SAVE PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             COMPARE UCB LABEL WITH PREFIX.
         BNE   NEXTUCB           DIDN'T MATCH, SO GET THE NEXT ONE.
LOADATTR MVC   DEVENTRY,LOWX     LOAD ATTRIBUTE BYTES.
         MVI   DEVENTRY+4,X'F4'  MEDIUM INTENSITY.
         MVI   DEVENTRY+8,X'F1'  BLUE.                          (I3279)
         MVI   VSAMFLAG,C' '     BLANK OUT DETAIL LINE
         MVC   PAGEFLAG(DVDL-1),VSAMFLAG               BEFORE USE.
         MVC   DEVNAME+1(3),UCBNAME   SHOW UCB NAME.
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BNO   DUCBNMOK          NO, PROCEED.
         MVC   DEVNAME,DSCNDEVN  YES, SHOW RETURNED DEVICE NUMBER.
DUCBNMOK MVC   DEVTYPN,UCBTYP    GET UCB TYPE INDICATORS.
         CLI   DEVTYPN+2,X'20'   DASD?
         BNE   NOTDATYP          NO.
         NI    DEVTYPN+1,X'DF'   YES, ENSURE SHARED BIT IS OFF.
NOTDATYP TM    OSLEVEL,CVTUCBSV  IS EDTINFO AVAILABLE?
         BNO   DEDTSCAN          NO, DO MANUAL SCAN.
         CLC   DEVTYPO,DEVTYPN   SAME DEVICE TYPE AS PREVIOUSLY?
         BNE   DNEWTYPE          NO, DETERMINE ITS UNIT NAME.
         LA    R1,DEVTYPC-4      YES, POINT TO THE UNIT NAME.
         B     GOTDVTYP          GO LOAD IT INTO THE DISPLAY LINE.
DNEWTYPE MVC   DEVTYPO,DEVTYPN   SAVE THIS DEVICE TYPE.
         EDTINFO RTNUNIT,        REQUEST GENERIC UNIT NAME.            +
               EXTENDED=YES,     PERFORM EXTRA WORK FOR COMMS CLASS.   +
               DEVTYPE=DEVTYPO,  INPUT DEVICE UCB TYPE BYTES.          +
               OUTUNIT=DEVTYPC   OUTPUT CHARACTER GENERIC UNIT NAME.
         LA    R1,DEVTYPC-4      POINT TO THE RESULTANT UNIT NAME.
         LTR   R15,R15           UNIT NAME SUCCESSFULLY DETERMINED?
         BZ    GOTDVTYP          YES, GO LOAD IT INTO THE DISPLAY LINE.
         MVC   DEVTYPC,BLANKS    NO, ERASE ANY RESIDUAL GARBAGE.
         B     DONDVTYP          PROCEED WITH FURTHER PROCESSING.
DEDTSCAN ICM   R1,15,EDTADDR     POINT TO ELIGIBLE DEVICES TABLE.
         BZ    DONEVIOT          SKIP SEARCH IF ADDRESS UNKNOWN.
         L     R1,28(,R1)        POINT TO LOOK UP VALUE SECTION.
         L     R15,8(,R1)        GET NUMBER OF UNIT NAMES.
         ST    R15,WORK2+4       SAVE IT FOR "HALFTEST".
         LA    R1,12(,R1)        POINT TO FIRST ENTRY MINUS FOUR.
         ST    R1,WORK2          SAVE IT FOR "HALFTEST".
FULLTEST CLC   DEVTYPN,12(R1)    IS THIS THE UNIT TYPE?
         BE    GOTDVTYP          YES, GO AND LOAD THE UNIT NAME.
         LA    R1,32(,R1)        NO, POINT TO NEXT ENTRY.
         BCT   R15,FULLTEST      TRY THE NEXT UNIT TYPE.
         L     R1,WORK2          POINT TO FIRST DEVICE NAME ENTRY.
         L     R15,WORK2+4       GET NUMBER OF UNIT NAMES.
HALFTEST CLC   DEVTYPN+2(2),14(R1) IS THIS THE UNIT TYPE?
         BE    GOTDVTYP          YES, GO AND LOAD THE UNIT NAME.
         LA    R1,32(,R1)        NO, POINT TO NEXT ENTRY.
         BCT   R15,HALFTEST      TRY THE NEXT UNIT TYPE.
*        MVC   DEVTYPNM+1(6),=C'(UNKN)'
         B     DONDVTYP          COULDN'T FIND IT.
GOTDVTYP MVC   DEVTYPNM+1(7),4(1)
DONDVTYP TM    UCBJBNR,UCBVRDEV  IS THE UCB FOR A VIO DEVICE?
         BZ    DONEVIOT          NO.
         MVI   DEVTYPNM,C'V'     YES, INDICATE SAME.
DONEVIOT LR    R5,R3             POINT TO UCB.
         AHI   R5,-48            POINT INTO UCB PREFIX.
*        TM    OSLEVEL,X'88'     UCB SERVICES AVAILABLE?
*        BNO   *+8               NO, CONTINUE.
*        LA    R5,DSCNPRFX       YES, POINT TO UCB PREFIX COPY.
         LA    R1,24(,R5)        POINT TO UNIT CHANNEL PATH ARRAY.
         ICM   R5,8,22(R5)       LOAD LAST PATH USED MASK.
         BM    GTLSTPTH          LAST PATH WAS FIRST CHPID IN ARRAY.
         LA    R9,7              SEVEN OTHER BITS IN THIS BYTE.
LSTPTHLP LA    R1,1(,R1)         INCREMENT CHANNEL PATH ARRAY POINTER.
         SLA   R5,1              TEST NEXT BIT.
         BO    GTLSTPTH          THIS IS IT.
         BCT   R9,LSTPTHLP       TRY NEXT ONE.
         B     LSTPTHOK          COULD NOT FIND IT.
GTLSTPTH UNPK  DEVPATH(3),0(2,R1) UNPACK LAST PATH CHPID BYTE.
         TR    DEVPATH(2),HEX-C'0' CONVERT TO HEX CHARACTERS.
         MVI   DEVPATH+2,C' '    ERASE GARBAGE.
LSTPTHOK TM    UCBSTAT,UCBONLI   IS THE DEVICE ONLINE?
         BO    ONLINE            YES.
         CLI   SELJOBNM,C' '     LOOKING FOR CERTAIN JOB NAMES?
         BH    OFFLINE           YES.
         TM    DEVFLAGS,DOFFLN   SELECTING OFFLINE DEVICES?
         BZ    NEXTUCB           NO.
OFFLINE  MVC   STATUS1,=C'OFF'   YES.
         TM    UCBFLA,UCBBOX     HAS THE DEVICE BEEN FORCED OFFLINE?
         BO    DEVBOXED          YES, DEVICE IS BOXED OFFLINE.
         TM    UCBSTAT,UCBALOC   IS THE DEVICE ALLOCATED?
         BO    ONLINE            YES, COULD BE BOXED OFFLINE.
         TM    UCBFL5,UCBNALOC   BEING PROCESSED IN OFFLINE MODE?
         BNO   OFFUNUSD          NO.
         MVC   STATUS1+3(7),=C'-IN-USE'
         MVI   DEVENTRY+4,X'F0'  SHOW IN LOW INTENSITY.
         MVI   DEVENTRY+8,X'F4'  SHOW IN GREEN.                 (I3279)
         B     OFFUNUSD          CATER FOR I/O DETAILS.
DEVBOXED MVC   STATUS1,=C'BOX'   INDICATE BOXED OFFLINE.
         MVI   DEVENTRY+4,X'F8'  SHOW IN HIGH INTENSITY.
         MVI   DEVENTRY+8,X'F2'  SHOW IN RED.                   (I3279)
         B     OFFUNUSD          GET ANY OTHER GOOD DATA GOING.
ONLINE   MVC   STATUS1,=C'ON '   DEVICE IS ONLINE.
         MVI   DEVENTRY+4,X'F4'  MEDIUM INTENSITY.
         MVI   DEVENTRY+8,X'F5'  SHOW IN TURQUOISE.             (I3279)
         TM    UCBSTAT,UCBALOC   IS THE DEVICE ALLOCATED?
         BZ    GOTALLOC          NO.
         MVC   STATUS1,=C'ALC'   YES.
         MVI   DEVENTRY+4,X'F0'  LOW INTENSITY.
         MVI   DEVENTRY+8,X'F6'  SHOW IN YELLOW.                (I3279)
GOTALLOC TM    UCBSTAT,UCBUNLD   IS THE DEVICE TO BE UNLOADED?
         BZ    NOUNLOAD          NO.
         MVC   STATUS1,=C'UNL'   YES.
NOUNLOAD TM    UCBSTAT,UCBCHGS   IS THE DEVICE TO BE VARIED OFFLINE?
         BZ    NOVARY            NO.
         MVC   STATUS1,=C'VRY'   YES.
NOVARY   TM    UCBSTAT,UCBSYSR   IS THIS THE SYSTEM RESIDENCE VOLUME?
         BZ    GOTSYSRS          NO, NOT SYSRES NOR SYSTEM CONSOLE.
         MVC   STATUS1,=C'SYS'   YES.
         MVI   DEVENTRY+4,X'F0'  LOW INTENSITY.
         MVI   DEVENTRY+8,X'F7'  SHOW IN WHITE.                 (I3279)
GOTSYSRS TM    UCBFLA,UCBNRY     IS THE DEVICE NOT READY?
         BZ    DEVREADY          NO.
         MVI   VSAMFLAG,C'¬'     YES.
DEVREADY TM    UCBFLA,UCBPERM    SUBCHANNEL UNUSEABLE?
         BZ    SCHOKAY           NO.
         MVI   DEVPERM,C'?'      YES.
SCHOKAY  TM    UCBTBYT3,UCB3TAPE+UCB3DACC DASD OR TAPE DEVICE?
         BZ    NOTRSV            NO, SKIP VOLUME ATTRIBUTE TESTS.
         TM    UCBSTAB,UCBBPRV   IS THIS A PRIVATE VOLUME?
         BZ    NOTPVT            NO.
         MVC   STATUS2,=C'PVT'   YES.
         B     NOTSTG            SKIP PUBLIC & STORAGE TESTS.
NOTPVT   TM    UCBSTAB,UCBBPUB   IS THIS A PUBLIC VOLUME?
         BZ    NOTPUB            NO.
         MVC   STATUS2,=C'PUB'   YES.
         B     NOTSTG            SKIP STORAGE TEST.
NOTPUB   TM    UCBSTAB,UCBBSTR   IS THIS A STORAGE VOLUME?
         BZ    NOTSTG            NO.
         MVC   STATUS2,=C'STG'   YES.
NOTSTG   TM    UCBFL5,UCBSMS     IS THIS AN SMS-MANAGED VOLUME?
         BZ    NOTSMS            NO.
         MVC   STATUS2,=C'SMS'   YES.
         LA    R1,UCBVOLI        POINT TO VOLUME SERIAL NUMBER.
         L     R15,GETVLDAD      POINT TO SMS VLD EXTRACTION ROUTINE.
         BASR  R14,R15           INVOKE SMS VLD EXTRACTION ROUTINE.
         LTR   R15,R15           SUCCESSFUL SSI CALL?
         BNZ   NOTSMS            NO, CAN'T TELL ANY STORAGE GROUP.
         L     R1,8(,R13)        FOLLOW SAVE AREA FORWARD CHAIN.
         LA    R1,80(,R1)        POINT TO SSOB.
         USING SSOB,R1
         L     R0,SSOBRETN       GET SUBSYSTEM RETURN CODE.
         DROP  R1                SSOB.
         LA    R1,SSOBHSIZ(,R1)  POINT TO SSSA.
         USING IEFSSSA,R1
         LTR   R0,R0             SUCCESSFUL SUBSYSTEM CALL?
         BZ    DSHOWVLD          YES, SHOW VLD CONTENTS.
         CVD   R0,WORK
         MVC   DEVSMSST+2(4),ED3
         ED    DEVSMSST+2(4),WORK+6
         MVC   DEVSMSST(3),=C'RC='
         L     R0,SSSARSN        SHOW REASON CODE.
         CVD   R0,WORK
         MVC   DEVFREE+3(6),ED5
         ED    DEVFREE+3(6),WORK+5
         MVC   DEVFREE(4),=C'RSN='
         B     NOTSMS            CAN'T SHOW SMS PARTICULARS.
DSHOWVLD ICM   R1,15,SSSA1PTR    POINT TO THE VLD.
         DROP  R1                IEFSSSA.
         BZ    NOTSMS            ONLY USE VLD IF IT EXISTS.
         USING VLD,R1
         MVC   DEVSTOGP,VLDSTGRP DISPLAY SMS STORAGE GROUP OF VOLUME.
         CLI   VLDMVSS,VLDBOXED  IS THE DEVICE BOXED?
         BNE   SMSBOXOK          NO.
         MVC   STATUS1,=C'BOX'   YES, INDICATE THIS.
SMSBOXOK IC    R5,VLDSMSS        LOAD VOLUME SMS STATUS.
         SLL   R5,29             SHIFT OUT TOP 29 BITS.
         SRL   R5,29             ENSURE VALUE IN 0-TO-7 RANGE.
         MHI   R5,6              GET INDEX INTO STATUS LABEL TABLE.
         LA    R5,SMSVSTAT(R5)   POINT TO STATUS LABEL.
         MVC   DEVSMSST,0(R5)    SHOW VOLUME SMS STATUS.
         L     R0,VLDNTCPY       LOAD TOTAL CAPACITY MEGABYTES.
         LA    R15,DEVTOTAL      POINT TO DISPLAY AREA.
         BAS   R14,DEVMEGS       FORMAT AND LOAD INTO DISPLAY AREA.
         L     R0,VLDNFREE       LOAD FREE CAPACITY MEGABYTES.
         LA    R15,DEVFREE       POINT TO DISPLAY AREA.
         BAS   R14,DEVMEGS       FORMAT AND LOAD INTO DISPLAY AREA.
         L     R0,VLDNLEXT       LOAD LARGEST FREE EXTENT MEGABYTES.
         LA    R15,DEVLARGE      POINT TO DISPLAY AREA.
         BAS   R14,DEVMEGS       FORMAT AND LOAD INTO DISPLAY AREA.
         DROP  R1                VLD.
NOTSMS   TM    UCBSTAB,UCBBSVL   IS THIS A REMOVABLE/DEMOUNTABLE DISK?
         BZ    NOTREM            NO.
         MVC   STATUS3,=C'REM'   YES.
         B     NOTRSV            SKIP RESIDENT & RESERVED TESTS.
NOTREM   TM    UCBSTAT,UCBPRES   IS THIS A PERMANENTLY RESIDENT VOLUME?
         BZ    NOTRSD            NO.
         MVC   STATUS3,=C'RSD'   YES.
NOTRSD   TM    UCBSTAT,UCBRESV   IS THIS A RESERVED VOLUME?
         BZ    NOTRSV            NO.
         MVC   STATUS3,=C'RSV'   YES.
NOTRSV   DS    0H                HAVE ALL MOUNT ATTRIBUTES.
*        TM    UCBFLA,X'8E'      IS UCBDEFER/STRT/HALT/CLEAR ON?
*        BZ    DONEDEVQ          NO, SO THERE CAN'T BE ANY QUEUING.
         LR    R5,R3             POINT TO UCB.
         AHI   R5,-48            POINT TO BEFORE UCBIOQF AND UCBIOQL.
*        TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
*        BNO   *+8               NO, CONTINUE.
*        LA    R5,DSCNPRFX       YES, POINT TO UCB PREFIX COPY.
         SLR   R1,R1             RESET QUEUE LENGTH COUNTER.
         LM    R8,R9,4(R5)       POINT TO FIRST AND LAST IOQS.
         LTR   R5,R8             IS THE FIRST POINTER ZERO?
         BZ    DONEDEVQ          YES, NO QUEUING.
         USING IOQ,R5
IOQLOOP  TM    IOQFLA,IOQACTV    IS THIS IOQ ACTIVE?
         BO    NEXTIOQ           YES, SO THIS IS NOT QUEUED.
         LA    R1,1(,R1)         NO, WAITING SO INCREMENT POINTER.
NEXTIOQ  CR    R5,R9             LAST IOQ ON QUEUE?
         BE    GOTIOQLN          YES, END OF CHAIN.
         ICM   R5,15,IOQCHAIN    POINT TO NEXT IOQ ON UCB CHAIN.
         BNZ   IOQLOOP           PROCESS IT.
         DROP  R5                IOQ.
GOTIOQLN CVD   R1,WORK           DISPLAY NUMBER OF I/O REQUESTS QUEUED
         MVC   QUECOUNT,ED3Z             FOR THIS UNIT.
         ED    QUECOUNT,WORK+6
OFFUNUSD DS    0H                CATER FOR I/O TO OFFLINE DISKS.
DONEDEVQ TIME  BIN               GET TIME-OF-DAY.
         USING IOENTRY,R7
         L     R7,DEVTBL         POINT TO FIRST DEVICE ENTRY.
UCBNMCHK LA    R7,IOENTLEN(,R7)  GET NEXT ONE.
         CLC   UCBDEVNO,UCBCHAN  ENTRY FOR THIS DEVICE?
         BNE   UCBNMCHK          NO.
         TM    UCBTBYT3,X'A0'    DASD OR TAPE DEVICE?
         BNZ   GETVSN            YES, GET VOLUME SERIAL NUMBER.
         CLI   UCBTBYT3,X'08'    UNIT RECORD DEVICE?
         BNE   GETSTATS          NO.
         CLI   UCBTBYT4,X'0E'    3800 PRINTING SUBSYSTEM?
         BE    DTLS3800          YES.
         TM    UCBTBYT2,X'80'    UNIVERSAL CHARACTER SET PRESENT?
         BZ    GETSTATS          NO.
         L     R5,UCBXTADR       YES, POINT TO UCS UCB EXTENSION.
         MVC   VTOC,8(R5)        LOAD FORMS CONTROL BUFFER NAME.
         MVC   USECOUNT+1,0(R5)  LOAD UNIVERSAL CHARACTER SET NAME.
         B     GETSTATS
DTLS3800 L     R5,UCBXTADR       YES, POINT TO 3800 UCB EXTENSION.
         MVC   VTOC,24(R5)       LOAD FORMS CONTROL BUFFER NAME.
         MVC   USECOUNT+1,28(R5) LOAD UNIVERSAL CHARACTER SET NAME.
         B     GETSTATS
GETVSN   MVC   VOLUME,UCBVOLI    DISPLAY VOLUME SERIAL NUMBER.
         OC    VOLUME,VOLUME     IS VOLUME SERIAL NUMBER NULLS?
         BZ    GETOPNCT          YES, GET THE DEVICE OPEN COUNT.
         CLI   VOLUME,C'9'       NO, IS IT PRINTABLE?
         BH    SCRATCH           NO, MUST BE A SCRATCH VOLUME REQUEST.
         CLI   VOLUME,C'¢'       IS IT PRINTABLE?
         BNL   GETOPNCT          YES, REALLY IS VOLUME SERIAL NUMBER.
SCRATCH  MVC   VOLUME,=C'SCRTCH' NO, ASSUME SCRATCH, AVOID SCREEN ERRS.
GETOPNCT SLR   R1,R1             CLEAR A WORK REGISTER.
         IC    R1,UCBDMCT        LOAD UCB OPEN COUNT.
         CHI   R1,128            HIGH-ORDER BIT ON?
         BL    SHOWOPEN          NO, SHOW COUNT.
         AHI   R1,-128           TURN OFF HIGH-ORDER BIT.
         MVC   STATUS3(7),=C'MNT-PND'    SHOW THAT A MOUNT IS PENDING.
SHOWOPEN CVD   R1,WORK           DISPLAY NUMBER OF OPEN DCBS
         MVC   OPNCOUNT,ED3Z            FOR THIS VOLUME.
         ED    OPNCOUNT,WORK+6
         CLI   UCBTBYT3,X'20'    DIRECT ACCESS STORAGE DEVICE?
         BE    GETUSAGE          YES, PROCEED WITH DASD PARTICULARS.
         CLI   UCBTBYT3,X'80'    MAGNETIC TAPE UNIT?
         BNE   GETSTATS          NO, SKIP TAPE PARTICULARS.
         LH    R1,UCBFSCT        GET FILE (DATA SET) SEQUENCE COUNT.
         CVD   R1,WORK
         MVC   CYLINDER+1(4),ED3Z
         ED    CYLINDER+1(4),WORK+6 SHOW IT IN THE CYLINDER COLUMN.
         ICM   R1,3,UCBFSEQ      GET FILE (DATA SET) SEQUENCE NUMBER.
         BZ    LABELSOK          ZERO, SO TAPE NOT THERE YET.
         CVD   R1,WORK
         MVC   RECORD,ED3Z
         ED    RECORD,WORK+6     SHOW IT IN THE RECORD COLUMN.
         MVC   HEAD,=C'BL'       PREPARE FOR BYPASS LABEL PROCESSING.
         TM    UCBTFL1,UCBBLP    LABEL=BLP?
         BO    LABELSOK          YES.
         MVI   HEAD,C'S'         NO, ASSUME STANDARD LABELS.
         TM    UCBTFL1,UCBNLTP+UCBNSLTP   CHECK LABEL FLAGS.
         BZ    LABELSOK          LABEL=SL VERIFIED.
         MVI   HEAD,C'N'         ASSUME UNLABELLED VOLUME (OR BLP).
         TM    UCBTFL1,UCBNSLTP  NON-STANDARD LABELS?
         BZ    LABELSOK          NO, MUST BE UNLABELLED,
         MVI   HEAD,C'U'         YES, CALL IT "USER LABELS".
LABELSOK DS    0H
         TM    UCBWGT,UCBREW     REWIND ISSUED TO THIS TAPE?
         BNO   GETDNSTY          NO, GET SIO COUNT.
         MVC   STATUS3,=C'REW'   YES, INDICATE SAME.
GETDNSTY TM    UCBDMCT,UCBMOUNT  MOUNT PENDING?
         BO    GETSTATS          YES, DENSITY NOT APPROPRIATE.
         TM    UCBTFL2,UCBTVCMP  DOES VOLUME CONTAIN COMPACTED DATA?
         BZ    MTCOMPOK          NO.
         MVC   VTOC,=C'COMP'     YES.
MTCOMPOK SLR   R1,R1
         ICM   R1,7,UCBXTNB      POINT TO MAGNETIC TAPE UCB EXTENSION.
         BZ    CALCMTIO          HANDLE UCB ABOVE THE LINE.
         TM    UCBTBYT4,X'80'    3480?
         BO    CARTDENS          YES, CARTRIDGE DEVICE.
         TM    UCBTBYT1,X'01'    3400-9?  (3480 IN 3420 COMPAT MODE?)
         BO    CARTDENS          YES, CARTRIDGE DEVICE.
         CLI   11(R1),X'D3'      2250 BPI MODESET-2 OP-CODE?
         BNE   TRY1600           NO.
         MVC   VTOC,=C'6250'     YES.
         B     CALCMTIO
TRY1600  CLI   11(R1),X'C3'      1600 BPI MODESET-2 OP-CODE?
         BNE   TRY800            NO.
         MVC   VTOC,=C'1600'     YES.
         B     CALCMTIO
TRY800   CLI   11(R1),X'CB'      800 BPI MODESET-2 OP-CODE?
         BNE   CALCMTIO          NO, 9-TRACK DENSITY MODE NOT SET.
         MVC   VTOC,=C' 800'     YES.
         B     CALCMTIO
CARTDENS CLI   10(R1),C' '       ANY VOLUME TO DISMOUNT?
         BNH   CALCMTIO          NO.
         MVC   UNITUSER+2(6),10(R1)   YES, DISPLAY IT.
         ICM   R1,8,4(R1)        LOAD DISMOUNT REQUEST CODE.
         SRL   R1,5              DEMOTE THREE TOP BITS.
         STCM  R1,8,UNITUSER     DISPLAY CODE. (SHOULD BE 1, 2 OR 4.)
         TR    UNITUSER(1),KEEPCART   CONVERT TO PRINTABLE CHARACTER.
*        CLI   VTOC+3,C' '       NULL IMBEDDED IN VOLUME SERIAL?
*        BH    CALCMTIO          NO, CONTINUE.
*        LA    R1,DSCNDCE        POINT TO UCB DEVICE CLASS EXTENSION.
*        TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
*        BO    VOLREPAR          YES, CONTINUE.
*        SLR   R1,R1
*        ICM   R1,B'0111',21(R3) POINT TO UCB COMMON EXTENSION.
*        ICM   R1,B'1111',24(R1) POINT TO THE UCB CLASS EXTENSION.
*        BZ    CALCMTIO          NO CLASS EXTENSION EXISTS.
*OLREPAR MVC   VTOC+3(1),57(R1)  COMPLETE THE VOLUME SERIAL.
CALCMTIO LA    R15,48            CLEAR WORK REGISTER.
         LR    R1,R3             POINT TO UCB.
         SLR   R1,R15            GET INTO UCB PREFIX.
*        TM    OSLEVEL,X'88'     UCB SERVICES AVAILABLE?
*        BNO   *+8               NO, CONTINUE.
*        LA    R1,DSCNPRFX       YES, POINT TO UCB PREFIX COPY.
         TM    17(R1),X'10'      MEASUREMENTS BEING COLLECTED?
         BO    GETSTATS          YES, DON'T USE TAPE UCB EXTN STATS.
         ICM   R1,B'0111',45(R3) POINT TO MAGNETIC TAPE UCB EXTENSION.
         BZ    GETSTATS          HANDLE UCB ABOVE THE LINE.
         ICM   R15,B'0011',6(R1) GET SIO COUNT FOR THIS DATA SET.
         L     R1,SIOSOFAR       GET PREVIOUS SIO COUNT.
         ST    R15,SIOSOFAR      STORE NEW SIO COUNT.
         SR    R15,R1            GET SIO CHANGE.
         BZ    GETSTATS          NO I/O DONE, SO SKIP FORMATING.
         MVC   DEVSIORT+4(6),=C'DS-CHG'
         BM    GETSTATS          NEGATIVE, PROBABLY A DIFFERENT FILE.
         LR    R1,R0             CURRENT TIME.
         S     R1,IOTODBIN       GET CHANGE IN TIME.
         ST    R0,IOTODBIN       SAVE PRESENT TIME.
         M     R14,TENGRAND      FOR CENTISECONDS AND 2 DECIMAL PLACES.
         DR    R14,R1            GET SIO/SECOND.
         CVD   R15,WORK
         MVC   DEVSIORT,=X'402021204B2020'
         ED    DEVSIORT,WORK+5
         B     GETSTATS          CONTINUE WITH GENERAL TESTS.
GETUSAGE DS    0H                DASD ONLY PROCESSING.
         SLR   R1,R1             CLEAR FOR INSERT.
         IC    R1,UCBSQC         LOAD RESERVE MACRO INSTRUCTION COUNT.
         CVD   R1,WORK           DISPLAY NUMBER OF RESERVES ISSUED
         MVC   RSVCOUNT,ED3Z             FOR THIS VOLUME.
         ED    RSVCOUNT,WORK+6
         TM    UCBSTAT,UCBONLI   IS THE DEVICE ONLINE?
         BZ    GETUSECT          NO, OFFLINE.
         CLC   UCBVTOC(2),FFFF   EXAMINE VTOC RELATIVE TT.
         BNE   RELTRACK          NOT TRACK ZERO SO SHOW TRACK ADDRESS.
         IC    R1,UCBVTOC+2      GET RECORD NUMBER OF TRACK ZERO.
         LA    R1,HEX(R1)        GET HEX CHARACTER.
         MVC   VTOC(3),=C'REC'   SHOW IT IS RECORD OF TRACK 0.
         MVC   VTOC+3(1),0(R1)   SHOW RECORD NUMBER.  ('REC5' FOR VM.)
         B     GETUSECT
RELTRACK UNPK  VTOC(5),UCBVTOC(3) GET VTOC STARTING TTR.
         TR    VTOC,HEX-C'0'     TRANSLATE TO HEXADECIMAL CHARACTERS.
         MVI   VTOC+4,C' '       R OF TTR ALWAYS 1, SO DON'T SHOW.
GETUSECT ICM   R1,3,UCBUSER      LOAD UCB USE COUNT.
         CVD   R1,WORK           DISPLAY NUMBER OF ALLOCATIONS.
         ICM   R15,3,DEVPATH     SAVE CHANNEL ID.
         MVC   USECOUNT-2(6),ED5Z
         ED    USECOUNT-2(6),WORK+5
         STCM  R15,3,DEVPATH     RESTORE CHANNEL ID.
         LA    R1,DSCNDCE        POINT TO UCB DEVICE CLASS EXTENSION.
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BO    DDCECHEK          YES, CONTINUE.
         SLR   R1,R1
         ICM   R1,7,UCBEXTP      POINT TO UCB COMMON EXTENSION.
         USING UCBCMEXT,R1
         ICM   R1,15,UCBCLEXT    POINT TO THE UCB CLASS EXTENSION.
         BZ    GETSTATS          NO CLASS EXTENSION EXISTS.
         DROP  R1                UCBCMEXT.
         USING DASDDCE,R1
DDCECHEK TM    DCEFLAG1,DCECUVAL CONTROL UNIT VALID?
         BNO   DEVCUDON          NO.
         UNPK  DEVCU,DCECUID(2)  YES, SHOW CONTROL UNIT ID.
         TR    DEVCU(2),HEX-C'0'
         MVI   DEVCU+2,C' '
DEVCUDON TM    DCEFLAG2,DCESSDPX DUPLEX VOLUME?
         BNO   DEVDUPOK          NO.
         MVC   DEVDUPLX,=C'YES'  YES.
         TM    DCEFLAG2,DCESSPRI PRIMARY DUPLEX VOLUME?
         BNO   DEVDUPL2          NO.
         MVC   DEVDUPLX,=C'PRI'  YES.
DEVDUPL2 TM    DCEFLAG2,DCESSSEC SECONDARY DUPLEX VOLUME?
         BNO   DEVDUPOK          NO.
         MVC   DEVDUPLX,=C'SEC'  YES.
DEVDUPOK TM    DCEACTFE,DCEACACH CACHING THIS VOLUME?
         BNO   DEVCACOK          NO.
         MVC   DEVCACHE+1(3),=C'YES'
DEVCACOK TM    DCEACTFE,DCEAFWRT DASDFASTWRITE ENABLED?
         BNO   DEVDFWOK          NO.
         MVC   DEVDFW,=C'YES'
DEVDFWOK TM    DCEFLAG1,DCEVALFS DEVICE FIELDS VALID?
         BNO   GETSTATS          NO.
         CLI   UCBTBYT4,X'0E'    3380 OR 3390?
         BL    GETSTATS          NO.
         CLI   UCBTBYT4,X'0F'    3380 OR 3390?
         BH    GETSTATS          NO.
         MVC   WORK(1),DCEOBRDT  COPY OBR DEVICE TYPE CODE.
         CLI   WORK,X'1E'        IN EXPECTED RANGE?
         BL    GETSTATS          NO.
         CLI   WORK,X'32'        IN EXPECTED RANGE?
         BH    GETSTATS          NO.
         TR    WORK,DASDMODL-X'1E'
         CLI   WORK,C' '         GOT A MODEL NUMBER?
         BNH   GETSTATS          NO.
         MVI   DEVTYPNM+5,C'('   YES, SHOW IT.
         MVC   DEVTYPNM+6(1),WORK
         MVI   DEVTYPNM+7,C')'
         DROP  R1                DASDDCE.
GETSTATS LR    R5,R3             POINT TO COMMON UCB SECTION.
         AHI   R5,-48            POINT TO UCB PREFIX.
*        TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
*        BNO   *+8               NO, CONTINUE.
*        LA    R5,DSCNPRFX       YES, POINT TO UCB PREFIX COPY.
         USING UCBPDATA,R5
         TM    UCBPMCW1+1,UCBMCMB    MEASUREMENTS BEING COLLECTED?
         BZ    EDITBUSY          NO, NOT FOR THIS DEVICE.
         SLR   R15,R15
         MVI   DEVGRFMK,X'3B'    INDICATE START OF GRAPH.
         MVI   DEVGRFTM+4,C'¦'   CALLIBRATE GRAPH AND SHOW CMB EXISTS.
         MVC   DEVGRFTM+5(25),DEVGRFTM
         ICM   R15,3,18(R5)      GET MEASUREMENT BLOCK INDEX.
         DROP  R5                UCBPDATA.
         L     R1,CMCTADDR       POINT TO CHANNEL MEASUREMENT CNTL TBL.
         USING CMCT,R1
         L     R5,CMCTCMBV       POINT TO CHANNEL MEASUREMENT BLOCK.
         DROP  R1                CMCT.
         SLL   R15,5             GET ENTRY OFFSET INTO CMB.
         ALR   R5,R15            POINT TO THIS DEVICE'S ENTRY.
         TM    MODESW4,ECMB      ECMB RATHER THAN CMB FOR DEVICE?
         BNO   DEVIOCMB          NO, DIRECTLY ACCESS THE CMB.
         TM    MODESW2,AUTH      APF AUTHORIZED?
         BNO   EDITBUSY          NO, CANNOT ACCESS ECMBS.
         L     R15,IOSECMB@      YES, POINT TO IOSCMB ROUTINE.
         LR    R8,R0             SAVE TIME OF DAY.
         LR    R1,R3             POINT TO THE UCB.
         BASR  R14,R15           INVOKE IT.
         LR    R0,R8             RESTORE TIME OF DAY.
         LTR   R15,R15           SUCCESS?
         BNZ   EDITBUSY          NO.
         LA    R5,SECWORKA+48    YES, POINT TO RETURNED CMB.
DEVIOCMB L     R15,0(,R5)        GET SSCH COUNTS FOR THIS DEVICE.
         ST    R15,WORK2         SAVE FOR LATER.
         SRL   R15,16            GET TOTAL SSCH COUNT FOR THIS DEVICE.
         L     R1,SIOSOFAR       GET PREVIOUS SSCH COUNT.
         ST    R15,SIOSOFAR      STORE NEW SSCH COUNT.
         SR    R15,R1            GET SSCH CHANGE.
         BNM   DEVSIOOK          NON-NEGATIVE SSCH COUNT IS OK.
         A     R15,FFFF          WRAP-AROUND OCCURRED,
         LA    R15,1(,R15)       SO MAKE CORRECTION.
DEVSIOOK LR    R1,R0             CURRENT TIME.
         S     R1,IOTODBIN       GET CHANGE IN TIME.
         ST    R0,IOTODBIN       SAVE PRESENT TIME.
         ST    R15,WORK2+4       SAVE SSCH COUNT CHANGE FOR LATER.
         LTR   R15,R15           ANY I/O SINCE LAST TIME?
         BZ    QUSAMPLE          NO, NO I/O DONE SO SKIP FORMATING.
         M     R14,TENGRAND      FOR CENTISECONDS AND 2 DECIMAL PLACES.
         DR    R14,R1            GET SIO/SECOND TO TWO DECIMAL PLACES.
         CVD   R15,WORK
         MVC   DEVSIORT,=X'402021204B2020'
         ED    DEVSIORT,WORK+5
         SLR   R15,R15           CLEAR WORK REGISTER.
         STH   R15,WORK2         ERASE TOTAL SSCH COUNT.
         L     R15,WORK2         GET TOTAL SSCH SAMPLE COUNT.
         S     R15,SIOSAMPL      GET CHANGE IN SSCH SAMPLE COUNT.
         BNM   DVSAMPOK          NON-NEGATIVE SSCH SAMPLE COUNT IS OK.
         A     R15,FFFF          WRAP-AROUND OCCURRED,
         LA    R15,1(,R15)       SO MAKE CORRECTION.
DVSAMPOK MVC   SIOSAMPL,WORK2    SAVE CURRENT SSCH SAMPLE COUNT.
         ST    R15,WORK2         SSCH SAMPLE COUNT CHANGE FOR LATER.
         LM    R8,R10,4(R5)      GET CMB CONTENTS.
         MVC   SECWORKA(12),SIOCONTM  SAVE PREVIOUS CMB CONTENTS.
         L     R15,SIOCONTM      LOAD PREVIOUS CONNECT TIME TOTAL.
         L     R14,SIODSCTM      LOAD PREVIOUS DISCONNECT TIME TOTAL.
         STM   R8,R10,SIOCONTM   SAVE CURRENT CMB VALUES.
         SLR   R8,R15            GET CONNECT TIME TOTAL DIFFERENCE.
         SLR   R10,R14           GET DISCONNECT TIME TOTAL DIFFERENCE.
         LR    R15,R10           GET DISCONNECT TIME TOTAL DIFFERENCE.
         ALR   R15,R8            GET TOTAL CHANGE IN BUSY 10**-4 SECS.
         BZ    RESAMPLE          INDICATE EXACTLY ZERO WITH BLANKS.
         LR    R9,R15            SAVE FOR LATER.
         OC    WORK2(4),WORK2    ANY SAMPLE SSCH'S?
         BZ    NOSAMPLS          NO, SHOULD NEVER HAPPEN.
         M     R14,WORK2+4       MULTIPLY BY NUMBER OF SSCH'S.
         SLDL  R14,7             CONVERT TIME SCALE TO MICROSECONDS.
         LTR   R14,R14           OVERFLOW OCCURRED?
         BM    QUSAMPLE          YES, AVOID S0C9 ABEND.
         D     R14,WORK2         DIVIDE BY NUMBER OF SAMPLE SSCH'S.
NOSAMPLS DR    R14,R1            GET PERCENTAGE TO TWO DECIMAL PLACES.
         SLR   R14,R14           ZERO FOR DIVIDE.
         CHI   R15,994           HIGHER THAN 9.94%?
         BH    INTEGERB          YES, CAN'T FIT IN FRACTIONAL %.
         AHI   R15,5             ROUND FRACTIONAL DIGIT.
         D     R14,TEN           GET N.N%.
         CVD   R15,WORK          CONVERT IT TO DECIMAL.
         MVC   DEVBSYPC,ED2PNT1+1        MOVE IN EDIT MASK.
         ED    DEVBSYPC-1(5),WORK+6      EDIT THE OUTPUT FIELD.
         MVI   UNITPCNT,C'%'     INDICATE PERCENTAGE.
         B     GETSRVTM          GOT PERCENTAGE BUSY, GET SERVICE TIME.
INTEGERB LA    R15,50(,R15)      ROUND TO NEAREST PERCENT.
         D     R14,AHUNDRED      GET PERCENTAGE.
         CVD   R15,WORK          CONVERT IT TO DECIMAL.
         MVC   DEVBSYPC,ED3      SHOW NEAREST WHOLE PERCENT.
         ED    DEVBSYPC,WORK+6
         MVI   UNITPCNT,C'%'     INDICATE PERCENTAGE.
GETSRVTM SLR   R8,R8             CLEAR FOR DIVIDE.
         SLDL  R8,7              CONVERT TIME SCALE TO MICROSECONDS.
         D     R8,WORK2          DIVIDE BUSY TIME BY SAMPLE SSCH COUNT.
         SLR   R8,R8             CLEAR FOR DIVIDE.
         LR    R15,R9            COPY MICROSECONDS VALUE.
         LA    R9,500(,R9)       PERFORM ROUNDING.
         D     R8,ONEGRAND       CONVERT TO MILLISECONDS.
         CVD   R9,WORK           GET AVERAGE BUSY MILLISECONDS PER I/O.
         ST    R9,SECWORKA+12    SAVE FOR LATER.
         MVC   DEVSRVTM,ED5
         ED    DEVSRVTM,WORK+5   EDIT SERVICE TIME MILLISECONDS.
         SLR   R14,R14           CLEAR FOR DIVIDE.
         LA    R15,50(,R15)      PERFORM ROUNDING.
         D     R14,AHUNDRED      CONVERT TO TENTHS OF MILLISECONDS.
         CL    R15,AHUNDRED      TEN MILLISECONDS OR MORE?
         BNL   DSRVTMOK          YES, NO NEED FOR FINER DETAIL.
         CVD   R15,WORK          NO, SHOW FRACTIONAL MILLISECONDS.
         MVC   DEVSRVTM+1(5),ED2PNT1
         ED    DEVSRVTM+1(5),WORK+6
DSRVTMOK L     R9,SIOCONTM
         SL    R9,SECWORKA       GET THE CHANGE IN CONNECT TIME.
         AL    R9,SIOPNDTM
         SL    R9,SECWORKA+4     ADD THE CHANGE IN PENDING TIME.
         AL    R9,SIODSCTM
         SL    R9,SECWORKA+8     ADD THE CHANGE IN DISCONNECT TIME.
         LTR   R9,R9             ANY DATA TO REPORT?
         BNP   QUSAMPLE          NO, CMB DATA HAS NOW BEEN PROCESSED.
         SLR   R8,R8             YES, CLEAR FOR DIVIDE.
         SLDL  R8,7              CONVERT TIME SCALE TO MICROSECONDS.
         D     R8,WORK2          DIVIDE TIME BY SAMPLE SSCH COUNT.
         SLR   R8,R8             CLEAR FOR DIVIDE.
         LR    R15,R9            COPY MICROSECONDS VALUE.
         LA    R9,500(,R9)       PERFORM ROUNDING.
         D     R8,ONEGRAND       CONVERT TO MILLISECONDS.
         CVD   R9,WORK           GET AVERAGE MILLISECONDS PER I/O.
         MVC   DEVRSPTM,ED5
         ED    DEVRSPTM,WORK+5   EDIT SERVICE TIME MILLISECONDS.
         SLR   R14,R14           CLEAR FOR DIVIDE.
         LA    R15,50(,R15)      PERFORM ROUNDING.
         D     R14,AHUNDRED      CONVERT TO TENTHS OF MILLISECONDS.
         CL    R15,AHUNDRED      TEN MILLISECONDS OR MORE?
         BNL   DRSPTMOK          YES, NO NEED FOR FINER DETAIL.
         CVD   R15,WORK          NO, SHOW FRACTIONAL MILLISECONDS.
         MVC   DEVRSPTM+1(5),ED2PNT1
         ED    DEVRSPTM+1(5),WORK+6
DRSPTMOK LA    R15,X'02'         USE RED FOR REQUEST PENDING TIME.
         BAS   R14,DEVGRAPH      PLOT TOTAL RESPONSE TIME PER I/O.
         L     R9,SECWORKA+12    GET DEVICE BUSY ON I/O TIME.
         LA    R15,X'06'         USE YELLOW FOR DISCONNECT TIME.
         BAS   R14,DEVGRAPH      PLOT TOTAL RESPONSE TIME PER I/O.
         L     R9,SIOCONTM
         SL    R9,SECWORKA       GET THE CHANGE IN CONNECT TIME.
         SLR   R8,R8             CLEAR FOR DIVIDE.
         SLDL  R8,7              CONVERT TIME SCALE TO MICROSECONDS.
         D     R8,WORK2          DIVIDE TIME BY SAMPLE SSCH COUNT.
         SLR   R8,R8             CLEAR FOR DIVIDE.
         LA    R9,500(,R9)       PERFORM ROUNDING.
         D     R8,ONEGRAND       CONVERT TO MILLISECONDS.
         LA    R15,X'05'         USE TURQUOISE FOR CONNECT TIME.
         BAS   R14,DEVGRAPH      PLOT TOTAL RESPONSE TIME PER I/O.
         B     QUSAMPLE          CMB DATA HAS NOW BEEN PROCESSED.
DEVGRAPH LTR   R9,R9             ANY NON-ZERO DATA TO PLOT?
         BNPR  R14               NO, RETURN.
         STC   R15,DEVGRFTM      YES, SHOW ONE AT LEAST.
         BCTR  R9,0              GET REMAINING MAGNITUDE TO PLOT.
         BCTR  R9,0              GET LENGTH CODE OF SAME.
         LTR   R9,R9             WAS IT MORE THAN ONE?
         BMR   R14               NO, PLOT NOW COMPLETE.
         LA    R0,30             GET MAXIMUM THAT WILL FIT.
         CR    R9,R0             DATA HIGHER THAN MAXIMUM?
         BNH   DEVGRFOK          NO, PROCEED.
         LR    R9,R0             YES, REDUCE TO MAXIMUM.
DEVGRFOK EX    R9,DEVGRFLD       PLOT REST OF DATA.
         BR    R14               PLOT COMPLETE SO RETURN TO CALLER.
DEVGRFLD MVC   DEVGRFTM+1(0),DEVGRFTM           <<< EXECUTED >>>
EDITBUSY SLR   R14,R14
         LH    R1,SCANCNTR       GET CURRENT UNIT SAMPLE SCAN COUNT.
         C     R1,TEN            DONE TEN OR MORE SCANS?
         BL    FINDUSER          NO, DON'T REPORT SAMPLE PERCENTAGES.
         LH    R15,UCBUSYCT      GET BUSY COUNT FOR THIS UNIT.
         LTR   R15,R15           ANY I/O DETECTED?
         BNP   QUSAMPLE          NO, INDICATE THIS BY SUPPRESSING 0%.
         M     R14,AHUNDRED      PREPARE FOR PERCENTAGE.
         DR    R14,R1            GET BUSY PERCENTAGE.
         CVD   R15,WORK          TRUNCATE TO INTEGRAL PERCENT.
         MVC   DEVBSYPC,ED3      0% MEANS SOME (BUT NOT MUCH) I/O
         ED    DEVBSYPC,WORK+6      HAS BEEN DETECTED BY SAMPLING.
         MVI   UNITPCNT,C'%'     INDICATE PERCENTAGE.
QUSAMPLE LH    R1,SCANCNTR       GET CURRENT UNIT BUSY SCAN COUNT.
         C     R1,TEN            DONE TEN OR MORE SCANS?
         BL    FINDUSER          NO, DON'T REPORT PERCENTAGES.
         CLI   DEVQUEUD+1,0      ZERO COUNTER?
         BE    RESAMPLE          YES, DON'T SHOW IT.
         CLI   QUECOUNT+3,C' '   DATA ALREADY THERE?
         BNE   RESAMPLE          YES, DON'T OVERWRITE IT.
         LH    R15,DEVQUEUD      GET QUEUED COUNT FOR THIS UNIT.
         M     R14,AHUNDRED      PREPARE FOR PERCENTAGE.
         DR    R14,R1            GET BUSY PERCENTAGE.
         CVD   R15,WORK          TRUNCATE TO INTEGRAL PERCENT.
         MVC   QUECOUNT,ED3
         ED    QUECOUNT,WORK+6
         MVC   QUECOUNT(3),QUECOUNT+1
         MVI   QUECOUNT+3,C'%'   INDICATE PERCENTAGE.
RESAMPLE CLI   DEVRSRVD+1,0      ZERO COUNTER?
         BE    FINDUSER          YES, DON'T SHOW IT.
         CLI   RSVCOUNT+3,C' '   DATA ALREADY THERE?
         BNE   FINDUSER          YES, DON'T OVERWRITE IT.
         LH    R15,DEVRSRVD      GET QUEUED COUNT FOR THIS UNIT.
         M     R14,AHUNDRED      PREPARE FOR PERCENTAGE.
         DR    R14,R1            GET BUSY PERCENTAGE.
         CVD   R15,WORK          TRUNCATE TO INTEGRAL PERCENT.
         MVC   RSVCOUNT,ED3
         ED    RSVCOUNT,WORK+6
         MVC   RSVCOUNT(3),RSVCOUNT+1
         MVI   RSVCOUNT+3,C'%'   INDICATE PERCENTAGE.
         DROP  R7                IOENTRY.
FINDUSER SLR   R1,R1             CLEAR WORK REGISTER.
         SLR   R5,R5             CLEAR POINTER REGISTER.
         ICM   R5,B'0111',21(R3) POINT TO COMMON UCB EXTENSION.
         ICM   R1,B'0011',14(R5) GET ASID OF LAST ALLOCATING JOB.
         BZ    BUSYTEST          NO ASID.
         SLL   R1,2              GET INDEX INTO ASVT (MULTIPLY BY 4).
         AL    R1,ASVTADDR       GET ASVT ENTRY FOR THIS ASID.
         LA    R1,ASVTENTY-ASVT-4(,R1)
         MVC   UNITUSER,ENDED
         ICM   R15,15,0(R1)      IS THIS ADDRESS SPACE ACTIVE?
         BM    BUSYTEST          NO, THE JOB HAS ENDED.
         MVC   UNITUSER,STARTING
         ICM   R1,15,JBNI(R15)   LOAD PTR TO JOBNAME.
         BNZ   LOADUSER
         ICM   R1,15,JBNS(R15)   LOAD PTR TO JOBNAME.
         BZ    BUSYTEST          JOB HAS NO NAME.
LOADUSER MVC   UNITUSER,0(R1)    OTHERWISE, MOVE IN JOBNAME.
         CLI   UNITUSER,X'49'    IS IT UNPRINTABLE?
         BH    BUSYTEST          NO, BYPASS FURTHER JOBNAME PROCESSING.
         MVC   UNITUSER,UNKNOWN  JOB NAME NOT KNOWN.
BUSYTEST DS    0H
         CLI   SELJOBNM,C' '     LOOKING FOR CERTAIN JOB NAMES?
         BNH   DOBSYTST          NO, CONTINUE.
         TM    UCBTBYT3,X'A0'    TAPE OR DASD?
         BNZ   DOBSYTST          YES, CONTINUE.
         LH    R9,JPRFXLEN       GET LENGTH OF JOB NAME PREFIX.
         LA    R7,SELJOBNM       POINT TO DESIRED PREFIX.
         LA    R8,UNITUSER       POINT TO JOB NAME.
         STM   R7,R9,WORK+4      SAVE PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             COMPARE JOB NAME WITH PREFIX.
         BNE   NEXTUCB           IF NOT EQUAL THEN GO AWAY.
*                                IS THE DEVICE BUSY?
DOBSYTST TM    UCBFLA,UCBSTRT+UCBHALT+UCBCLEAR
         BZ    SKIPIOSB          NO, UCBIOQ IS NOT VALID.
         MVI   DEVENTRY+4,X'F8'  YES, HIGH INTENSITY.
         MVI   DEVENTRY+8,X'F2'  YES, SHOW IN RED.              (I3279)
*                                CLEAR OR HALT ACTIVE?
         TM    UCBFLA,UCBHALT+UCBCLEAR
         BNZ   SKIPIOSB          YES, SKIP SEEK ADDRESS DETAILS.
         MVI   DEVENTRY+8,X'F3'  NO, SHOW IN PINK.              (I3279)
         CLC   STATUS3,=C'REW'   IS THIS A REWINDING TAPE DRIVE?
         BE    SKIPIOSB          YES, DETAILS PROBABLY INVALID.
         TM    MODESW2,AUTH      APF AUTHORIZED?
         BO    IOSBAUTH          YES, USE KEY ZERO JUST IN CASE.
         MVI   WORK2,8           NO, FLAG STILL IN USER KEY.
         TM    UCBSTAT,X'08'     IS THE DEVICE ALLOCATED?
         BZ    IOSBOKAY          NO, CAN'T BE VTAM.
         TM    UCBTBYT3,X'50'    YES, ALLOCATED GRAPHIC OR COMM DEVICE?
         BZ    IOSBOKAY          NO, VTAM IS NOT THE ACCESS METHOD.
         B     SKIPIOSB          YES, AVOID S0C4-4 ABEND WITH VTAM.
IOSBAUTH DS    0H                USE KEY ZERO IF POSSIBLE TO BYPASS
         MODESET MF=(E,MDSET0)   FETCH PROTECTION.
         MVI   WORK2,0           REMEMBER THAT CURRENTLY IN KEY ZERO.
IOSBOKAY LR    R5,R3             GET UCB ADDRESS.
         AHI   R5,-4             GET IOQ HANDLING I/O FOR THIS UNIT.
         L     R5,0(,R5)         POINT TO THE IOQ.
         USING IOQ,R5
         L     R5,IOQIOSB        GET THE IOSB.
         DROP  R5                IOQ.
         USING IOSB,R5
         SLR   R1,R1             CLEAR WORK REGISTER.
         IC    R1,IOSCKEY        GET STORAGE PROTECT KEY OF THE
         SRL   R1,4                  CHANNEL PROGRAM.
         STC   R1,KEY
         TR    KEY,HEX           TRANSLATE TO HEXADECIMAL CHARACTER.
         CLI   IOSPROC,IOSATTN   ATTENTION FOR THIS UNIT?
         BNE   NOTATN            NO.
         MVC   STATUS3,=C'ATN'   YES.
         B     DASDTEST          CONTINUE.
NOTATN   CLI   IOSPROC,IOSADDR   DYNAMIC DEVICE RECONFIGURATION?
         BNE   NOTDDR            NO.
         MVC   STATUS3,=C'DDR'   YES.
         B     DASDTEST          CONTINUE.
NOTDDR   CLI   IOSPROC,IOSAWTO   LONG WAIT (ACTION WTO) FOR THIS UNIT?
         BNE   DASDTEST          NO, CONTINUE.
         MVC   STATUS2(7),=C'INT-REQ'  YES, INTERVENTION REQUIRED.
         TM    IOSFLB,IOSMSG     REALLY?
         BZ    DASDTEST          YES.
         MVC   STATUS2(7),=C'I/O-ERR'  NO, I/O ERROR.
DASDTEST CLI   UCBTBYT3,X'20'    DASD DEVICE?
         BNE   IOSBHEX           NO, FORGET MBBCCHHR DISPLAY.
         IC    R1,IOSSKR         GET RECORD NUMBER OF DYNAMIC SEEK.
         CVD   R1,WORK           DISPLAY THE RECORD NUMBER SEEKED
         MVC   RECORD,ED3                FOR THIS HEAD.
         ED    RECORD,WORK+6
         IC    R1,IOSSKH2        GET HEAD NUMBER OF DYNAMIC SEEK.
         CVD   R1,WORK           DISPLAY THE HEAD BEING USED
         OI    WORK+7,15                 BY THIS DISK.
         UNPK  HEAD,WORK+6(2)            (ONLY USE THE LOW ORDER BYTE.)
         ICM   R1,3,IOSSKCC      GET CYLINDER NUMBER OF DYNAMIC SEEK.
         CVD   R1,WORK           DISPLAY THE CYLINDER NUMBER
         MVC   WORK2(6),ED5              BEING SEEKED FOR THIS DISK.
         ED    WORK2(6),WORK+5
         MVC   CYLINDER,WORK2+1
IOSBHEX  CLI   IOSCOD,IOSNRMC    NORMAL COMPLETION CODE?
         BNE   DOIOSHEX          NO, OVERWRITE PREVIOUS DETAILS.
         CLI   HEAD,C' '         YES, ANY PREVIOUS DETAILS?
         BNE   FINDDRVR          YES, DON'T OVERWRITE.
DOIOSHEX MVC   CYLINDER+2(3),=C'CC='
         UNPK  CYLINDER+5(3),IOSCOD(2)
         TR    CYLINDER+5(2),HEX-C'0'
         MVI   HEAD+1,C' '       DISPLAY COMPLETION CODE.
         UNPK  HEAD+2(5),IOSTATUS(3)
         TR    HEAD+2(4),HEX-C'0'
         MVI   HEAD+6,C' '       DISPLAY DEVICE AND SUBCHANNEL STATUS.
FINDDRVR MVC   IODRIVER,DRVRUNKN ASSUME UNKNOWN DRIVER.
         SLR   R1,R1             CLEAR FOR INSERT.
         IC    R1,IOSDVRID       LOAD I/O DRIVER ID.
         SLL   R1,2              MULTIPLY BY FOUR.
         CLI   IOSDVRID,24       EXAMINE I/O DRIVER ID.
         BH    TRYERDRV
         LA    R1,DRVRTABL(R1)   POINT TO DRIVER NAME.
         B     SHOWDRVR          DISPLAY THE DRIVER NAME.
TRYERDRV CLI   IOSDVRID,128      ERROR CONDITION DRIVER?
         BL    FNDUNAME          NO.
         CLI   IOSDVRID,135      YES, KNOWN TYPE?
         BH    FNDUNAME          NO.
         LA    R1,ERDRVRTB-512(R1) POINT TO DRIVER NAME.
SHOWDRVR MVC   IODRIVER,0(R1)    DISPLAY THE DRIVER NAME.
FNDUNAME SLR   R1,R1             CLEAR WORK REGISTER.
         ICM   R1,3,IOSASID      GET ASID TO BE SCHEDULED AT I/O COMP.
         BZ    IOSBEXIT          NO ASID.
         SLL   R1,2              GET INDEX INTO ASVT (MULTIPLY BY 4).
         A     R1,ASVTADDR
         LA    R1,524(,R1)       GET ASVT ENTRY FOR THIS ASID.
         MVC   UNITUSER,ENDED
         ICM   R15,B'1111',0(R1) IS THIS ADDRESS SPACE ACTIVE?
         BM    IOSBEXIT          NO, THE JOB HAS ENDED.
         MVC   UNITUSER,STARTING
         ICM   R1,15,JBNI(R15)   LOAD PTR TO JOBNAME.
         BNZ   LOADUNAM
         ICM   R1,15,JBNS(R15)   LOAD PTR TO JOBNAME.
         BZ    IOSBEXIT          JOB HAS NO NAME.
LOADUNAM MVC   UNITUSER,0(R1)    OTHERWISE, MOVE IN JOBNAME.
         CLI   UNITUSER,X'49'    IS IT UNPRINTABLE?
         BH    IOSBEXIT          NO, BYPASS FURTHER JOBNAME PROCESSING.
         MVC   UNITUSER,UNKNOWN  JOB NAME NOT KNOWN.
IOSBEXIT DS    0H                IF IN KEY ZERO THEN GET OUT OF IT.
         CLI   WORK2,0           CURRENTLY IN KEY ZERO?
         BNE   SKIPIOSB          NO.
         MODESET MF=(E,MDSETN0)  YES, KEY ZERO NO LONGER REQUIRED.
         DROP  R5                IOSB.
SKIPIOSB DS    0H                END OF IOSB PROCESSING.
         CLI   SELJOBNM,C' '     LOOKING FOR CERTAIN JOB NAMES?
         BNH   FLAGSCHK          NO, CONTINUE.
         LH    R9,JPRFXLEN       GET LENGTH OF JOB NAME PREFIX.
         LA    R7,SELJOBNM       POINT TO DESIRED PREFIX.
         LA    R8,UNITUSER       POINT TO JOB NAME.
         STM   R7,R9,WORK+4      SAVE PARAMETER LIST.
         L     R7,PRFCHKAD       LOAD PREFCHEK ENTRY POINT.
         BASR  R1,R7             COMPARE JOB NAME WITH PREFIX.
         BNE   NEXTUCB           IF NOT EQUAL THEN GO AWAY.
FLAGSCHK CLI   UCBTBYT3,X'20'    DASD DEVICE?
         BNE   NOTSHARD          NO, FORGET VSAM, PAGING, SHARED TESTS.
         TM    UCBFL5,UCBAMV     HAS VSAM MATCHED VTOC TIME-STAMP?
         BNO   NOTVSAM           NO.
         MVI   VSAMFLAG,C'V'     YES.
NOTVSAM  TM    UCBSTAB,UCBPGFL   IS THIS A PAGING VOLUME?
         BNO   PGSELTST          NO.
         MVI   PAGEFLAG,C'P'     YES.
         B     PAGING
PGSELTST TM    DEVFLAGS,DPAGEV   SELECTING NON-PAGING VOLUMES?
         BO    NEXTUCB           NO.
PAGING   TM    UCBTBYT2,UCBRR    IS THIS A SHARED VOLUME?
         BNO   NOTSHARD          NO.
         MVI   SHRDFLAG,C'*'     YES.
NOTSHARD CLI   DEVENTRY+8,X'F1'  BLUE ENTRY?  (DEVICE NOT IN USE?)
         BE    INUSETST          YES.
         CLI   DEVENTRY+8,X'F5'  TURQUOISE ENTRY?  (DEVICE NOT IN USE?)
         BNE   DEVINUSE          NO.
INUSETST TM    DEVFLAGS,DINUSE   SUPPRESS IF NOT IN-USE?
         BO    NEXTUCB           YES.
DEVINUSE MVC   SECWORKA+32(33),DEVGRFMK   PRESERVE GRAPH.
         TR    VSAMFLAG(DVDL),XLATETBL    ENSURE NO UNPRINTABLES.
         MVC   DEVGRFMK(33),SECWORKA+32   RESTORE GRAPH.
         BAS   R14,DADDLINE      LINE NOW COMPLETE.
         DROP  R2                DEVLINE.
         CLI   UCBTBYT3,X'80'    TAPE DEVICE?
         BNE   NEXTUCB           NO, CONTINUE THE SEARCH.
         CLI   CLASMASK,X'80'    ONLY SELECTING TAPES?
         BE    XTRATAPE          YES, DISPLAY EXTRA TAPE DETAILS.
         CLI   SELJOBNM,C' '     LOOKING FOR CERTAIN JOB NAMES?
         BH    XTRATAPE          YES, DISPLAY EXTRA TAPE DETAILS.
         CLI   SELVOLID,C' '     LOOKING FOR CERTAIN VOLUMES?
         BH    XTRATAPE          YES, DISPLAY EXTRA TAPE DETAILS.
         CLI   SELUCBID,C' '     LOOKING FOR CERTAIN UNITS?
         BNH   NEXTUCB           NO, DON'T CLUTTER UP FULLISH SCREEN.
XTRATAPE LR    R1,R2             COPY CURRENT LINE ADDRESS.
         AHI   R1,-DVLL          POINT TO PREVIOUS LINE.
         MVC   0(LL-79,R2),0(R1) COPY ATTRIBUTE BYTES.
         MVI   0(R2),X'FF'       INDICATE NON-SCROLLABLE LINE.
         SLR   R5,R5
         SLR   R1,R1
         ICM   R5,B'0111',45(R3) POINT TO MAGNETIC TAPE UCB EXTENSION.
         BZ    NEXTUCB           HANDLE UCB ABOVE THE LINE.
         MVC   WORK(16),0(R5)    COPY MAGNETIC TAPE UCB EXTENSION.
         OC    WORK(16),WORK     ANY NON-ZERO DATA?
         BZ    NEXTUCB           NO, DON'T CREATE THE EXTRA LINE.
         TM    UCBTBYT4,X'80'    3480?
         BO    XTRACART          YES, CARTRIDGE DEVICE.
         TM    UCBTBYT1,X'01'    3400-9?  (3480 IN 3420 COMPAT MODE?)
         BO    XTRACART          YES, CARTRIDGE DEVICE.
XTRAMGTP IC    R1,4(,R5)         TEMPORARY READ ERROR COUNT.
         LA    R9,TAPELINE+18
         BAS   R14,XTRAED3
         IC    R1,5(,R5)         TEMPORARY WRITE ERROR COUNT.
         LA    R9,TAPELINE+25
         BAS   R14,XTRAED3
         IC    R1,8(,R5)         PERMANENT READ ERROR COUNT.
         LA    R9,TAPELINE+42
         BAS   R14,XTRAED3
         IC    R1,9(,R5)         PERMANENT WRITE ERROR COUNT.
         LA    R9,TAPELINE+49
         BAS   R14,XTRAED3
         IC    R1,10(,R5)        NUMBER OF NOISE BLOCKS ENCOUNTERED.
         LA    R9,TAPELINE+56
         BAS   R14,XTRAED3
         ICM   R1,B'0011',0(R5)  TAPE DRIVE SERIAL NUMBER.
         LA    R9,TAPELINE+9
         BAS   R14,XTRAED5
         ICM   R1,B'0011',6(R5)  START I/O OPERATION COUNT.
         LA    R9,TAPELINE+33
         BAS   R14,XTRAED5
         ICM   R1,B'0011',12(R5) NUMBER OF ERASE GAPS ENCOUNTERED.
         LA    R9,TAPELINE+64
         BAS   R14,XTRAED5
         ICM   R1,B'0011',14(R5) NUMBER OF CLEANER ACTIONS.
         LA    R9,TAPELINE+74
         BAS   R14,XTRAED5
         TM    UCBTBYT4,X'80'    3480?
         BO    CARTXTRA          YES, CARTRIDGE DEVICE.
         TM    UCBTBYT1,X'01'    3400-9?  (3480 IN 3420 COMPAT MODE?)
         BZ    DONEXTRA          NO, LINE NOW FORMATTED.
CARTXTRA LA    R5,DSCNDCE        POINT TO UCB DEVICE CLASS EXTENSION.
         TM    OSLEVEL,CVTUCBSV  UCB SERVICES AVAILABLE?
         BO    CRTEXTRA          YES, CONTINUE.
         ICM   R5,B'0111',21(R3) POINT TO UCB COMMON EXTENSION.
         ICM   R5,15,24(R5)      POINT TO UCB DEVICE CLASS EXTENSION.
         BZ    DONEXTRA          NO CLASS EXTENSION EXISTS.
CRTEXTRA CLI   7(R5),48          DFP FORMAT?
         BL    DONEXTRA          NO, DON'T CHECK 3480 STATISTICS.
         L     R9,48(,R5)        TAPE DRIVE SERIAL NUMBER.
         SLL   R9,4              ALLOW FOR SIGN.
         ST    R9,WORK+4
         OI    WORK+7,X'0F'
         UNPK  TAPELINE+9(5),WORK+5(3)
         TR    TAPELINE+9(5),HEX-C'0'
         ICM   R1,3,32(R5)       TEMPORARY READ ERROR COUNT.
         AH    R1,34(,R5)        TEMPORARY READ BACKWARD ERROR COUNT.
         BZ    CARTRDOK          SKIP EDIT IF ZERO.
         LA    R9,TAPELINE+18
         BAS   R14,XTRAED3
CARTRDOK ICM   R1,3,36(R5)       TEMPORARY WRITE ERROR COUNT.
         BZ    CARTWROK          SKIP EDIT IF ZERO.
         LA    R9,TAPELINE+25
         BAS   R14,XTRAED3
CARTWROK ICM   R1,3,28(R5)       NUMBER OF ERASE GAPS ENCOUNTERED.
         BZ    CARTEGOK          SKIP EDIT IF ZERO.
         LA    R9,TAPELINE+64
         BAS   R14,XTRAED5
CARTEGOK ICM   R1,3,30(R5)       NUMBER OF CLEANER ACTIONS.
         BZ    DONEXTRA          SKIP EDIT IF ZERO.
         LA    R9,TAPELINE+74
         BAS   R14,XTRAED5
DONEXTRA MVC   LL-79(79,R2),TAPELINE
         TR    LL-79(79,R2),XLATETBL      ENSURE NO UNPRINTABLES.
         MVI   LL(R2),C' '       SUPPLY TRAILING BLANKS.
         MVC   LL+1(DVDL-80,R2),LL(R2)
         BAS   R14,DADDLINE      LINE NOW COMPLETE.
         B     NEXTUCB           PROCESS NEXT DEVICE.
XTRACART TM    4(R5),B'11100000' IS A DISMOUNT BIT ON?
         BNM   XTRAMGTP          NO, SHOW STATISTICS.
         B     NEXTUCB           YES, DON'T CREATE EXTRA LINE.
XTRAED3  CVD   R1,WORK
         OI    WORK+7,X'0F'
         UNPK  0(3,R9),WORK+6(R2)
         BR    R14
XTRAED5  CVD   R1,WORK
         OI    WORK+7,X'0F'
         UNPK  0(5,R9),WORK+5(R3)
         BR    R14
         SPACE
DUSCNOT0 CHI   R15,4             END OF UCBS?
         BE    SHOWDEVS          YES, DISPLAY ACCUMULATED INFORMATION.
         STM   R15,R0,WORK       NO, SAVE RETURN AND REASON CODES.
         CHI   R15,12            I/O CONFIGURATION CHANGE?
         BNE   DUSCFAIL          NO, ROLL OVER AND DIE.
         L     R4,CREIOTAD       YES.
         BASR  R4,R14            GET A NEW I/O DEVICE TABLE.
DUSCFAIL MVC   0(LL-79,R2),HIGHX
         MVI   0(R2),X'FF'       FIXED FLAG - NON-SCROLLABLE LINE.
         MVI   2(R2),X'F2'       REVERSE FOR ERROR.             (I3279)
         MVI   8(R2),X'F7'       RED FOR ERROR.                 (I3279)
         MVC   28(23,R2),=C'UCBSCAN RETC=XX REAS=XX'
         UNPK  43(3,R2),WORK+3(2)
         TR    43(2,R2),HEX-C'0'
         MVI   45(R2),C' '
         UNPK  51(3,R2),WORK+7(2)
         TR    51(2,R2),HEX-C'0'
         MVI   53(R2),C' '
         BAS   R14,DADDLINE      LINE NOW COMPLETE.
         USING DEVLINE,R2
SHOWDEVS MVI   VSAMFLAG,C' '     PUT TRAILING BLANKS ON SUMMARY LINE.
         MVC   PAGEFLAG(DVDL-1),VSAMFLAG
         DROP  R2                DEVLINE.
         BAS   R1,SYSUMMRY       PRODUCE & SHOW SYSTEM SUMMARY LINE.
         USING WNDWMNGR,R15
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         LA    R1,1
         A     R1,WINLINES       INCREMENT LINE COUNTER.
         ST    R1,WINLINES
         MVC   0(2,R2),FFFF+2    SET FINAL LINE FLAG FOR WINDOW DATA.
         DROP  R15               WNDWMNGR.
PUTSCRND LA    R0,LL             MAKE REGISTER 0 POSITIVE FOR OUTPUT.
GETSCRND L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         BASR  R14,R15           CALL WINDOW MANAGER.
         DS    0H
         TM    MODESW2,XFLG      SUBROUTINE EXIT REQUIRED?
         BZ    NOTEXITD          NO, PROCESS SUBCOMMAND.
         MVI   TGETFLG,TGETFLGA  RESTORE TGET TYPE FOR OTHER ROUTINES.
         BR    R9                RETURN TO MAINLINE VIA SCREENIO.
NOTEXITD CHI   R15,4             ANY INPUT RECEIVED FROM TERMINAL YET?
         BNE   GETCMDEV          YES, PROCESS IT.
         LH    R9,SCANCNTR       GET DEVICE SCAN COUNT.
         LA    R9,1(,R9)         ADD ONE.
         STH   R9,SCANCNTR       SAVE INCREMENTED COUNTER.
         L     R5,DEVTBL         POINT TO FIRST DEVICE TABLE ENTRY.
         USING IOENTRY,R5
NEXTDEV  LA    R5,IOENTLEN(,R5)  GET DEVICE TABLE ENTRY.
         ICM   R3,15,IOUCBADR    POINT TO THE UCB BASE.
         BZ    EOUCBTBL          NO MORE UCB ENTRIES.
         CLI   UCBID,UCBSTND     STANDARD UCB?
         BNE   DVBADUCB          NO, NEED TO RESYNC WITH REALITY.
         CLC   UCBDEVNO,UCBCHAN  RIGHT DEVICE NUMBER?
         BNE   NEXTDEV           NO, PROBABLY DDR SWAP.
         TM    UCBFLA,X'0E'      IS THE DEVICE BUSY?
         BZ    CHKRESVS          NO, SO CAN'T BE ANY QUEUING EITHER.
         TM    UCBUSYCT,X'80'    CMB STATISTICS FOR THIS DEVICE?
         BO    CHKQUEUE          YES, DON'T USE SAMPLING FOR BUSY CALC.
         LH    R9,UCBUSYCT       GET BUSY COUNT SO FAR.
         LA    R9,1(,R9)         ADD ONE.
         STH   R9,UCBUSYCT       SAVE INCREMENTED COUNTER.
CHKQUEUE LR    R9,R3             POINT TO UCB.
         AHI   R9,-4             POINT TO UCBIOQ.
         ICM   R9,15,0(R9)       ANY IOQ HERE?
         BZ    CHKRESVS          NO, CHECK FOR DASD H/W RESERVES.
         TM    UCBFLA,UCBDEFER   MUST NEW I/O BE DEFERRED?
         BO    FNDQUEUE          YES, CHALK THIS ONE UP FOR QUEUING.
         ICM   R9,15,4(R9)       ANY IOQ QUEUED?
         BZ    CHKRESVS          NO, CHECK FOR DASD H/W RESERVES.
FNDQUEUE LH    R9,DEVQUEUD       GET QUEUED COUNT SO FAR.
         LA    R9,1(,R9)         ADD ONE.
         STH   R9,DEVQUEUD       SAVE INCREMENTED COUNTER.
CHKRESVS CLI   UCBTBYT3,X'20'    DASD DEVICE?
         BNE   CHECK200          NO, CHECK FOR 200 RESET.
         CLI   UCBSQC,0          ANY RESERVES CURRENTLY ISSUED?
         BE    CHECK200          NO, CHECK FOR 200 RESET.
         LH    R9,DEVRSRVD       GET RESERVED COUNT SO FAR.
         LA    R9,1(,R9)         ADD ONE.
         STH   R9,DEVRSRVD       SAVE INCREMENTED COUNTER.
CHECK200 CLI   SCANCNTR+1,200    TWO HUNDREDTH SCAN?
         BL    NEXTDEV           NO, CHECK NEXT DEVICE.
         CLI   UCBUSYCT+1,199    UNIT BUSY 199 OUT OF 200?
         BNE   RESET200          NO, PERFORM RESET.
         MVI   UCBUSYCT+1,200    YES, MAKE 100% POSSIBLE IN THE FUTURE.
RESET200 LH    R9,UCBUSYCT       GET BUSYCOUNT SO FAR.
         SRL   R9,2              DIVIDE BY 4.
         STH   R9,UCBUSYCT       SAVE SCALED-DOWN COUNTER.
         LH    R9,DEVQUEUD       GET QUEUED COUNT SO FAR.
         SRL   R9,2              DIVIDE BY 4.
         STH   R9,DEVQUEUD       SAVE SCALED-DOWN COUNTER.
         LH    R9,DEVRSRVD       GET RESERVED COUNT SO FAR.
         SRL   R9,2              DIVIDE BY 4.
         STH   R9,DEVRSRVD       SAVE SCALED-DOWN COUNTER.
         B     NEXTDEV           CHECK NEXT DEVICE.
         DROP  R5,R3             IOENTRY, UCBOB.
EOUCBTBL CLI   SCANCNTR+1,200    END OF TWO HUNDREDTH SCAN?
         BL    NOADJUST          NO, DON'T NORMALIZE COUNTERS YET.
         MVI   SCANCNTR+1,50     YES, ADJUST TO FIFTY SCANS.
NOADJUST CLI   DOWNCNTR,C' '     IS AUTOMATIC SCREEN REFRESH ACTIVE?
         BH    REDODEVS          YES, GO LOAD ANOTHER SCREENFUL.
         STIMER WAIT,BINTVL=DEVDELAY  NO, WAIT FOR A BIT.
         SLR   R0,R0             DON'T WRITE ANYTHING TO THE SCREEN.
         B     GETSCRND          CALL SCREEN HANDLER FOR ASYNC COMMAND.
DVBADUCB L     R5,DEVTBL         POINT TO THE DEVICE TABLE.
         SLR   R0,R0
         LA    R5,IOENTLEN(,R5)  POINT TO THE FIRST ENTRY.
         USING IOENTRY,R5
         ST    R0,IOUCBADR       ZERO THE UCB ADDRESS TO STOP SCANS.
         DROP  R5                IOENTRY.
         B     NOADJUST          RESUME DISPLAY CHECK.
GETCMDEV CLI   DOWNCNTR,C' '     ARE WE IN AUTO REFRESH MODE?
         BH    REDODEVS          YES, CONTINUE WITH DISPLAYS.
         CLI   INBUFF,5          PF5/17?
         BE    UNITEXAM          YES, PREPARE FOR OPTION "U".
DZOOMCHK CLI   INBUFF+2,0        CURSOR IN LEFT-MOST COLUMN?
         BE    DASDCHK           YES, DO NOT PERFORM ZOOM-IN.
         CLI   INBUFF+1,0        CURSOR ON HEADING LINE?
         BNE   UNITEXAM          NO, CHECK FOR ZOOM-IN REQUEST.
DASDCHK  CLI   INCHARS,C'D'      CHECK IF DASD TOGGLE REQUESTED.
         BNE   TAPECHK           NO, TRY TAPE.
         XI    CLASMASK,X'20'    TOGGLE DASD SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
TAPECHK  CLI   INCHARS,C'T'      CHECK IF TAPE TOGGLE REQUESTED.
         BNE   GRAFICHK          NO, TRY GRAPHIC DISPLAY.
         XI    CLASMASK,X'80'    TOGGLE TAPE SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
GRAFICHK CLI   INCHARS,C'G'      CHECK IF GRAPHIC TOGGLE REQUESTED.
         BNE   UNITRCHK          NO, TRY UNIT RECORD.
         XI    CLASMASK,X'10'    TOGGLE GRAPHIC DISPLAY SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
UNITRCHK CLI   INCHARS,C'R'      CHECK IF UNIT RECORD REQUESTED.
         BNE   COMMCHK           NO, TRY COMMUNICATION.
         XI    CLASMASK,X'08'    TOGGLE UNIT RECORD SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
COMMCHK  CLI   INCHARS,C'C'      CHECK IF COMMUNICATION REQUESTED.
         BNE   MISCHECK          NO, TRY MISCELLANEOUS SELECTION.
         XI    CLASMASK,X'40'    TOGGLE COMMUNICATION SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
MISCHECK CLI   INCHARS,C'M'      CHECK IF MISCELLANEOUS REQUESTED.
         BNE   INUSECHK          NO, TRY NOT IN USE SUPPRESSION.
         XI    CLASMASK,X'06'    TOGGLE CHAR-RDR ET AL SELECTION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
INUSECHK CLI   INCHARS,C'I'      CHECK IF IN-USE-ONLY REQUESTED.
         BNE   DBLNKCHK          NO, TRY CONTINUE.
         XI    DEVFLAGS,DINUSE   TOGGLE NOT-IN-USE SUPPRESSION.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
DBLNKCHK CLI   INCHARS,C' '      CONTINUE REQUESTED?
         BNE   DOFFLCHK          NO, CONTINUE CHECKS.
         B     REDODEVS          RESTART SCAN.
DOFFLCHK CLI   INCHARS,C'O'      OFFLINE SELECTION TOGGLE?
         BNE   PGVOLCHK          NO, TRY PAGING VOLUME CHECK.
         XI    DEVFLAGS,DOFFLN   TOGGLE OFFLINE SELECTION TEST.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
PGVOLCHK CLI   INCHARS,C'P'      PAGING VOLUME ONLY SELECTION TOGGLE?
         BNE   JOBCHECK          NO, TRY JOB NAME PREFIX CHECK.
         XI    DEVFLAGS,DPAGEV   TOGGLE PAGING-ONLY SELECTION TEST.
         B     DEVINITD          CHECK FOR FURTHER SUBCOMMANDS.
JOBCHECK CLI   INCHARS,C'J'      CHECK FOR SPECIFIC JOB NAME PREFIX.
         BNE   VOLCHECK          NO, TRY VOLUME SERIAL PREFIX CHECK.
         XC    SELJOBNM,SELJOBNM ERASE PREVIOUS JOBNAME.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOJOBPIO          YES, DO IT.
         CLI   INCHARS+1,C' '    JOB PREFIX SUPPLIED?
         BNH   DGETJNAM          NO.
         MVC   SELJOBNM,INCHARS+1   LOAD PRE-PROMPT SUPPLIED MASK.
         LA    R15,SELJOBNM+1    POINT TO FIRST BYTE TO CHECK.
         LA    R0,7              MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
DJOBLOOP CLI   0(R15),C' '       END OF JOB NAME PREFIX FOUND?
         BNH   NOJOBPIO          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,DJOBLOOP
         B     NOJOBPIO          EIGHT CHARACTER MASK SUPPLIED.
DGETJNAM MVI   TGETFLG,TGETFLGA  PERFORM AN ASIS WAIT TGET FOR PROMPT.
         NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
DGETASNM LA    R1,JPRFXMSG
         LA    R0,JOBSELEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    DGETASNM          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVI   TGETFLG,TGETFLGN  RESTORE ASIS NOWAIT TGETS FOR DEVMON.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         MVC   SELJOBNM,INCHARS  EXTRACT SPECIFIED PREFIX.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
NOJOBPIO STH   R1,JPRFXLEN       STORE PREFIX LENGTH.
         B     REDODEVS          RESTART UCB SCAN.
VOLCHECK CLI   INCHARS,C'V'      CHECK FOR VOLUME SERIAL NUMBER PREFIX.
         BNE   UCBCHECK          IF NOT, TRY FOR UCB ADDRESS PREFIX.
         XC    SELVOLID,SELVOLID ERASE PREVIOUS VOLID.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOVOLPFX          YES, DO IT.
         CLI   INCHARS+1,C' '    LEADING VOLSER CHARACTER SUPPLIED?
         BH    DVOLSERP          YES, DETERMINE HOW MANY.
         MVI   TGETFLG,TGETFLGA  PERFORM AN ASIS WAIT TGET FOR PROMPT.
         NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
DGETVLSR LA    R1,VPRFXMSG
         LA    R0,SELVOLEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    DGETVLSR          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVI   TGETFLG,TGETFLGN  RESTORE ASIS NOWAIT TGETS FOR DEVMON.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         MVC   SELVOLID,INCHARS  EXTRACT SPECIFIED PREFIX.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
NOVOLPFX STH   R1,VPRFXLEN       STORE PREFIX LENGTH.
         B     REDODEVS          RESTART DEVICE SCAN.
DVOLSERP MVC   SELVOLID,INCHARS+1   LOAD PRE-PROMPT SUPPLIED MASK.
         LA    R15,SELVOLID+1    POINT TO FIRST BYTE TO CHECK.
         LA    R0,5              MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
DVOLLOOP CLI   0(R15),C' '       END OF VOLUME SERIAL PREFIX FOUND?
         BNH   NOVOLPFX          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,DVOLLOOP
         B     NOVOLPFX          SIX CHARACTER MASK SUPPLIED.
UCBCHECK CLI   INCHARS,C'U'      UCB ADDRESS PREFIX REQUEST?
         BNE   GIVEHLPD          NO.
         XC    SELUCBID,SELUCBID ERASE PREVIOUS UCB NAME.
         SLR   R1,R1
         CLI   INCHARS+1,C'/'    ERASE PREVIOUS PREFIX REQUEST?
         BE    NOUCBPFX          YES, DO IT.
         CLI   INCHARS+1,C' '    LEADING DEVNUM CHARACTER SUPPLIED?
         BH    DUNITADP          YES, DETERMINE HOW MANY.
         MVI   TGETFLG,TGETFLGA  PERFORM AN ASIS WAIT TGET FOR PROMPT.
         NI    MODESW2,255-CLNF  CLEAN-UP NOT REQUIRED IF EXIT HERE.
DGETUCBN LA    R1,UPRFXMSG
         LA    R0,SELUCLEN       FULL SCREEN TPUT THE PROMPT MESSAGE.
         BAS   R14,SCREENIO      ISSUE PROMPT AND GET PREFIX.
         CLI   INBUFF+4,0        INPUT FROM THE FIRST SCREEN LINE?
         BE    DGETUCBN          YES, UNEXPECTED SO REDRIVE THE PROMPT.
         MVI   TGETFLG,TGETFLGN  RESTORE ASIS NOWAIT TGETS FOR DEVMON.
         OI    MODESW2,CLNF      FLAG CLEAN-UP REQUIRED UPON EXIT.
         MVC   SELUCBID,INCHARS  EXTRACT SPECIFIED PREFIX.
         LH    R1,TGETLEN        GET LENGTH OF INPUT STREAM.
         AHI   R1,-6             GET LENGTH OF TEXT.
NOUCBPFX STH   R1,UPRFXLEN       STORE PREFIX LENGTH.
         B     REDODEVS          RESTART DEVICE SCAN.
DUNITADP MVC   SELUCBID,INCHARS+1   LOAD PRE-PROMPT SUPPLIED MASK.
         LA    R15,SELUCBID+1    POINT TO FIRST BYTE TO CHECK.
         LA    R0,3              MAXIMUM BYTES TO CHECK.
         LA    R1,1              PREFIX LENGTH DETERMINED SO FAR.
DUCBLOOP CLI   0(R15),C' '       END OF DEVICE NUMBER PREFIX FOUND?
         BNH   NOUCBPFX          YES, SAVE THE PREFIX LENGTH.
         LA    R15,1(,R15)       NO, POINT TO NEXT BYTE.
         LA    R1,1(,R1)         INCREMENT PREFIX LENGTH.
         BCT   R0,DUCBLOOP
         B     NOUCBPFX          FOUR CHARACTER MASK SUPPLIED.
GIVEHLPD MVC   INBUFF(8),SBLST   REQUEST SUBCOMMAND LIST FROM TUTORIAL.
         MVI   TGETLEN+1,8       SUPPLY LENGTH OF INPUT TO PARSE.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         B     GETSCRND          GO DISPLAY HELP PANEL.
         SPACE
VPRFXLEN DC    H'0'              LENGTH FOR VOLUME SERIAL PREFIX.
UPRFXLEN DC    H'0'              LENGTH FOR UCB NAME PREFIX.
         SPACE
UNITEXAM DS    0H                INVOKE OPTION "U" FOR SELECTED UNIT.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         SLR   R1,R1             CLEAR FOR INSERT.
         IC    R1,INBUFF+1       GET RELATIVE LINE NUMBER OF CURSOR.
         CH    R1,WINHDGCT       CURSOR ON A HEADING LINE?
         BNH   DUZOOMIT          YES, DO NOT SPECIFY ANY DEVICE NUMBER.
         BCTR  R1,0
         BCTR  R1,0
         A     R1,WINTOP         CONVERT TO WINDOW LINE NUMBER.
         C     R1,WINLINES       CURSOR AFTER LAST LINE?
         BH    DUZOOMIT          YES, DO NOT SUPPLY A DEVICE NUMBER.
         MH    R1,WINLNLEN       NO, GET AN INDEX INTO WINDOW AREA.
         A     R1,WINBUFF        POINT TO DETAIL LINE CONTENTS.
         CLI   0(R1),X'FF'       CURSOR ON DEVICE LINE?
         BE    DUZOOMIT          NO, DO NOT SUPPLY A UCB NAME.
         DROP  R15               WNDWMNGR.
         USING DEVLINE,R1
         L     R15,UUCBNMAD      POINT TO OPTION "U" UCB NAME AREA.
         MVI   0(R15),X'DD'      INDICATE TRANSFER FROM OPTION "D".
         MVC   THEUNIT-THEUCBAD(4,15),DEVNAME  INDICATE UCB.
         CLI   DEVNAME,C' '      THREE-DIGIT DEVICE NUMBER?
         BNE   DINVOKEU          NO, FOUR DIGITS LOADED.
         MVI   THEUNIT-THEUCBAD(15),C'0'  YES, LOAD LEADING ZERO.
         DROP  R1                DEVLINE.
DINVOKEU MVC   INBUFF(L'INBUFF+L'INCHARS),BLANKS  CLEAR INPUT BUFFER.
         MVI   INBUFF,X'7C'      SIMULATE PFK 12.
         MVI   INCHARS,C'U'      SIMULATE OPTION "U" REQUEST.
         LNR   R0,R11            MAKE REGISTER 0 NEGATIVE FOR PARSE.
         MVI   TGETFLG,TGETFLGA  RESTORE TGET TYPE FOR OTHER ROUTINES.
         NI    MODESW2,255-CLNF  CLEAN-UP NO LONGER REQUIRED.
         B     GETSCRND          PERFORM INTER-OPTION JUMP.
DUZOOMIT CLI   INBUFF,5          OPTION "U" REQUEST ANYWAY?
         BE    DINVOKEU          YES, DO NOT SPECIFY ANY UCB NAME.
         B     DASDCHK           NO, SCAN FOR LOCAL SUBCOMMAND.
         SPACE
DEVMEGS  CVD   R0,WORK
         MVC   0(9,R15),=XL9'40202020206B202120'
         ED    0(9,R15),WORK+4
         BR    R14
         SPACE
DADDLINE LA    R2,DVLL(,R2)      ADJUST BUFFER POINTER.
         L     R15,WINMANAD      POINT TO THE WINDOW MANAGER.
         USING WNDWMNGR,R15
         LA    R1,1
         A     R1,WINLINES       INCREMENT LINE COUNTER.
         ST    R1,WINLINES
         MVC   0(2,R2),WINFULL   PREPARE FOR FULL WINDOW.
         C     R2,WINEND         SEE IF THE BUFFER IS FULL YET.
         BNL   PUTSCRND          IF YES, THEN GO PUT THIS SCREEN.
         BR    R14
         DROP  R15               WNDWMNGR.
         TITLE 'DEVICE MONITOR - VARIABLES AND CONSTANTS'
UUCBNMAD DC    A(THEUCBAD)       ADDRESS OF OPTION "U"S UCB NAME.
DEVTYPN  DC    F'0'              UCB TYPE OF THIS UCB.
DEVTYPO  DC    F'0'              UCB TYPE OF PREVIOUS UCB.
DEVTYPC  DC    CL8' '            UNIT NAME OF UCB IN EBCDIC.
DEVDELAY DC    F'20'             DEVICE SAMPLE CYCLE WAIT DELAY.
DSCNWORK DC    XL100'00'         UCBSCAN WORKAREA.
DSCNPRFX DC    XL48'00'          UCBSCAN UCB PREFIX AREA.
DSCNUCB  DC    XL48'00'          UCBSCAN UCB BASE AREA.
DSCNCMXT DC    XL64'00'          UCBSCAN UCB COMMON EXTENSION AREA.
DSCNDCE  DC    XL48'00'          UCBSCAN DEVICE CLASS EXTENSION AREA.
DSCNDEVN DC    CL4'DEV#'         UCBSCAN UCB DEVICE NUMBER AREA.
DSCNDCEL DC    AL2(L'DSCNDCE)    UCBSCAN AREA LENGTH PARAMETER.
SCANCNTR DC    H'0'              BUSY UNIT SCAN COUNTER.
KEEPCART DC    C' RK?D???'       RETAIN/KEEP/DISMOUNT.
CLASMASK DC    X'20'             DEVICE CLASS SELECTION MASK.
DEVFLAGS DC    X'00'
DOFFLN   EQU   X'80'             SELECT OFFLINE DEVICES.
DINUSE   EQU   X'40'             SELECT ONLY IN-USE DEVICES.
DPAGEV   EQU   X'20'             RESTRICT DASD TO PAGING VOLUMES.
DASDMODL DS    0X                0E - 3380-A/B.
         DC    C'D  '            1E - 3380-D.  1F-20.
         DC    C'J '             21 - 3380-J.  22.
         DC    C'K'              23 - 3380-K.
         DC    C'3 '             24 - 3390-3.  25.
         DC    C'1'              26 - 3390-1.
         DC    C'2      '        27 - 3390-2.  28-2D.
         DC    C'E   '           2E - 3380-E.  2F-31.
         DC    C'9'              32 - 3390-9.
HEADINGD DC    C'   VOLUME DVNO UNIT-STATUS PTH USE OPN QUE RSV ---RESP+
               ONSE--1+5--TIME(MS)--3+0>VTOC DEVTYPE IORATE LAST-USR  C+
               YL HD REC K DRVR SRVTM RSPTM BUSY CU CACHE DFW DUP STOGR+
               OUP+STATUS TOTAL-MB  FREE-MB LARGE-MB'
TAPELINE DC    CL79'DRIVE-NO=##### TR=### TW=### SIO=##### PR=### PW=##+
               # NB=### ERG=##### CLN=#####'
VPRFXMSG DC    X'C11140403C40400011C5C2&LOW'
         DC    C'ENTER VOLUME SERIAL PREFIX OR COMPLETE VOL'
         DC    C'UME SERIAL NUMBER'
         DC    X'&MED',C'===>',X'1BC94E132841F4'
SELVOLID DC    CL6' '
         DC    X'2841001BF8C2114BD2&LOW'
         DC    C'(USE',X'&HIGH',C'V/',X'&LOW',C'FROM DETAIL SCREEN '
         DC    C'TO CANCEL VOLUME SERIAL NUMBER PREFIX SELECTION)'
         DC    X'11D1E3'
         DC    C'(USE',X'&HIGH',C'*',X'&LOW'
         DC    C'FOR NON-SPECIFIC PLACE-HOLDER CHARACTERS)'
SELVOLEN EQU   *-VPRFXMSG
UPRFXMSG DC    X'C11140403C40400011C5C2&LOW'
         DC    C'ENTER UCB NAME PREFIX OR COMPLETE UCB NAME'
         DC    X'&MED',C'===>',X'1BC94E132841F4'
SELUCBID DC    CL4' '
         DC    X'2841001BF8C2114BD2&LOW'
         DC    C'(USE',X'&HIGH',C'U/',X'&LOW',C'FROM DETAIL SCREEN '
         DC    C'TO CANCEL UCB NAME PREFIX SELECTION)'
         DC    X'11D1E3'
         DC    C'(USE',X'&HIGH',C'*',X'&LOW'
         DC    C'FOR NON-SPECIFIC PLACE-HOLDER CHARACTERS)'
SELUCLEN EQU   *-UPRFXMSG
         UCBSCAN PLISTVER=MAX,MF=(L,DSCN)     UCBSCAN PARAMETER LIST.
SMSVSTAT DC    C'NOTCON'  0   NOT CONNECTED.
         DC    C'ENABLE'  1   ENABLED.
         DC    C'QUIALL'  2   QUIESCED/ALL.
         DC    C'QUINEW'  3   QUIESCED/NEW.
         DC    C'DISALL'  4   DISABLED/ALL.
         DC    C'DISNEW'  5   DISABLED/NEW.
         DC    C'??????'  6
         DC    C' ???? '  7
DRVRTABL DC    C'IOS ' X'00'  RESERVED FOR INPUT/OUTPUT SUPERVISOR.
         DC    C'MISC' X'01'  MISCELLANEOUS IDENTIFIER.
         DC    C'EXCP' X'02'  EXECUTE CHANNEL PROGRAM PROCESSOR.
         DC    C'VSAM' X'03'  VIRTUAL STORAGE ACCESS METHOD.
         DC    C'VTAM' X'04'  VIRTUAL TELECOMMUNICATIONS ACCESS METHOD.
         DC    C'TCAM' X'05'  TELECOMMUNICATIONS ACCESS METHOD.
         DC    C'OLTE' X'06'  ONLINE TEST EXECUTIVE PROGRAM.
         DC    C'PCIF' X'07'  PROGRAM CONTROLLED INTERRUPT FETCH.
         DC    C'JES3' X'08'  JOB ENTRY SUBSYSTEM 3.
         DC    C'MSC ' X'09'  MSC/SS1.
         DC    C'IOPG' X'0A'  IECVIOPM PURGE.
         DC    C'VPSS' X'0B'  VECTOR PROCESSING SUBSYSTEM.
         DC    C'CRYP' X'0C'  CRYPTOGRAPHIC.
DRVRUNKN DC    C'????' X'0D'
         DC    C'ASM ' X'0E'  AUXILIARY STORAGE MANAGER.
         DC    C'MSG ' X'0F'  MESSAGE DISPLAY SERVICE.
         DC    C'ASGN' X'10'  ASSIGN/UNASSIGN SERVICE.
         DC    C'DYNP' X'11'  DYNAMIC PATHING.
         DC    C'DAVV' X'12'  DIRECT ACCESS VOLUME VERIFICATION.
         DC    C'DCS ' X'13'  DEVICE CONTROL SERVICE.
         DC    C'AOM ' X'14'  ASYNCHRONOUS OPERATION MANAGER.
         DC    C'SMS ' X'15'  DFSMS.
         DC    C'XCF ' X'16'  XCF CHANNEL-TO-CHANNEL ADAPTER.
         DC    C'CDR ' X'17'  IOS USE.
         DC    C'SLF ' X'18'  IOSVSLFD.
ERDRVRTB DC    C'HALT' X'80'  SVC 33 IOHALT.
         DC    C'CLDV' X'81'  CLEAR DEVICE RECOVERY.
         DC    C'SBCH' X'82'  SUBCHANNEL RECOVERY.
         DC    C'PURG' X'83'  SVC 16 PURGE.
         DC    C'URSV' X'84'  UNCONDITIONAL RESERVE (UR) RECOVERY.
         DC    C'MIH ' X'85'  MISSING INTERRUPT HANDLER (MIH).
         DC    C'PRVN' X'86'  I/O PREVENTION.
         DC    C'RRSV' X'87'  RE-RESERVE.
         SPACE
         LTORG
         SPACE
         DS    0H
         TITLE 'DEVICE MONITOR - SELECTION CRITERIA DISPLAY'
         USING WHATDEVS,R15
WHATDEVS DS    0H
         MVC   0(LL-79,R2),LOWX  SUPPLY 3270 ATTRIBUTES.
         MVI   0(R2),X'FF'       FIXED FLAG - NON-SCROLLABLE LINE.
         LA    R2,LL-79(,R2)     POINT PAST ATTRIBUTES.
         MVI   0(R2),C' '        BLANK A LINE.
         MVC   1(DVDL-1,R2),0(R2)
         MVC   0(5,R2),=C'SELN:' INDICATE SELECTION CRITERIA.
         ICM   R14,8,CLASMASK    LOAD DEVICE CLASS SELECTION MASK.
         LA    R0,6              LOAD NUMBER OF DEVICE CLASSES.
         LA    R1,6(,R2)         POINT TO FIRST DISPLAY SLOT.
         LA    R3,CLASNAME       POINT TO DEVICE CLASS NAME TABLE.
WHATDVLP LTR   R14,R14           DEVICE CLASS BEING SELECTED?
         BNM   WHATCLAS          NO.
         MVC   0(4,R1),0(R3)     YES, SHOW DEVICE CLASS NAME.
WHATCLAS SLL   R14,1             PROMOTE NEXT DEVICE CLASS MASK BIT.
         LA    R1,5(,R1)         POINT TO NEXT DISPLAY SLOT.
         LA    R3,4(,R3)         POINT TO NEXT DEVICE CLASS NAME.
         BCT   R0,WHATDVLP       PROCESS NEXT DEVICE CLASS.
         TM    DEVFLAGS,DOFFLN   OFFLINE DEVICES BEING SELECTED?
         BZ    WHATOFFL          NO.
         MVC   36(5,R2),=C'OFFLN'
WHATOFFL TM    DEVFLAGS,DINUSE   SELECT ONLY IN-USE DEVICES?
         BZ    WHATUSE           NO.
         MVC   42(6,R2),=C'IN-USE'
WHATUSE  TM    DEVFLAGS,DPAGEV   ONLY PAGING VOLUME DASD?
         BZ    WHATPAGE          NO.
         MVC   49(3,R2),=C'PAG'
WHATPAGE MVC   53(26,R2),=CL26'J=******** V=****** U=****'
         CLI   SELJOBNM,C' '     USING JOB NAME PREFIX SELECTION?
         BNH   WHATJBOK          NO.
         LH    R1,JPRFXLEN       GET PREFIX LENGTH.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,WHATJBLD       LOAD THE PREFIX INTO THE DISPLAY LINE.
WHATJBOK CLI   SELUCBID,C' '     USING UCB NAME PREFIX SELECTION?
         BNH   WHATUCOK          NO.
         LH    R1,UPRFXLEN       GET PREFIX LENGTH.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,WHATUCLD       LOAD THE PREFIX INTO THE DISPLAY LINE.
WHATUCOK CLI   SELVOLID,C' '     USING VOLUME SERIAL PREFIX SELECTION?
         BNH   WHATVOOK          NO.
         LH    R1,VPRFXLEN       GET PREFIX LENGTH.
         BCTR  R1,0              DECREMENT FOR EXECUTE.
         EX    R1,WHATVOLD       LOAD THE PREFIX INTO THE DISPLAY LINE.
WHATVOOK LA    R2,DVDL(,R2)      POINT TO NEXT DISPLAY LINE.
         MVC   HEADINGD+31(3),=C'UCS'
         MVC   HEADINGD+79(4),=C'FCB '
         TM    CLASMASK,X'80'    TAPES BEING SELECTED?
         BZ    NOTAPES           NO.
         MVC   HEADINGD+79(4),=C'DENS'
         MVC   HEADINGD+109(10),=C'FSC LB FSN'
NOTAPES  TM    CLASMASK,X'20'    DISKS BEING SELECTED?
         BZ    NODISKS           NO.
         MVC   HEADINGD+31(3),=C'USE'
         MVC   HEADINGD+79(4),=C'VTOC'
         MVC   HEADINGD+109(10),=C'CYL HD REC'
NODISKS  MVC   0(LL-79,R2),HIGHX LOAD HEADING ATTRIBUTES.
         MVI   2(R2),X'F4'       USE UNDERSCORE FOR HEADING.    (I3279)
         MVI   5(R2),X'C4'       SUPPLY UNDERLINE ALSO.         (F9526)
         MVI   8(R2),X'F7'       SHOW HEADING IN WHITE.         (I3279)
         MVC   LL-79(DVDL,R2),HEADINGD     LOAD DISPLAYABLE
         TR    LL-79(DVDL,R2),XLATETBL          HEADING.
         LA    R2,DVLL(,R2)      POINT PAST HEADING.
         B     DEVHDGOK          RETURN TO SELECT DEVICES.
         SPACE
WHATJBLD MVC   55(0,R2),SELJOBNM                <<< EXECUTED >>>
WHATUCLD MVC   75(0,R2),SELUCBID                <<< EXECUTED >>>
WHATVOLD MVC   66(0,R2),SELVOLID                <<< EXECUTED >>>
         SPACE
         DROP  R11,R6,R15        DEVICES, WHATDEVS.
         SPACE 2
CLASNAME DC    C'TAPECOMMDASDGRAFURECMISC'
         LTORG
         SPACE
         DS    0H
