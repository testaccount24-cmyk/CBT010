IMDSSTRD TITLE ' READ AND FORMAT IMON DISK SPACE SNAP SHOT FILE '
*
*         WRITTEN 18TH APRIL 2001 BY GREG PRICE
*
IMDSSTRD CSECT
         USING IMDSSTRD,R15
         B     $START
         DROP  R15                 IMDSSTRD.
         DC    AL1(17),C'IMDSSTRD &SYSDATC'
$START   STM   R14,R12,12(R13)
         LR    R11,R15
         USING IMDSSTRD,R11
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(,R13)
         LR    R13,R9
         LA    R10,INREC
         OPEN  (DSSTIN,INPUT)
         OPEN  (DSSTOUT,OUTPUT)
         PUT   DSSTOUT,OUTREC
GETLOOP  GET   DSSTIN,INREC
         CLC   BASEID,INREC+24
         BNE   VOLREC
         USING DSST,R10
         ZAP   WORK,=P'1900000'
         AP    WORK,DSSTDATE
         MVC   OUTDATE,=X'40212020204B202020'
         ED    OUTDATE,WORK+4
         UNPK  OUTTIME(3),DSSTTIME(2)
         UNPK  OUTTIME+3(3),DSSTTIME+1(2)
         MVI   OUTTIME+2,C':'
         MVI   OUTTIME+5,C','
         B     GETLOOP
         DROP  R10
         USING DSSTE,R10
VOLREC   MVC   OUTUNIT,DSSTEUCB
         MVC   OUTVOL,DSSTEVOL
         MVC   OUTSG,DSSTEGRP
         OC    OUTSG,=CL8' '
         SLR   R0,R0
         ICM   R0,3,DSSTETSZ
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTSZ,WORK
         L     R0,DSSTECYL
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTCYL,WORK
         L     R0,DSSTETRK
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTTRK,WORK
         L     R0,DSSTEEXT
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTEXT,WORK
         L     R0,DSSTEMXC
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTMXC,WORK
         L     R0,DSSTEMXT
         CVD   R0,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTMXT,WORK
         PUT   DSSTOUT,OUTREC
         B     GETLOOP
ENDOFDAT CLOSE (DSSTIN)
         CLOSE (DSSTOUT)
         L     R13,SAVE+4
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
         TITLE ' WORKING STORAGE '
WORK     DC    D'0'
SAVE     DC    18F'0'
BASEID   EQU   *,24
         DC    XL20'00',C'DSST'
INREC    DC    CL48' '
OUTREC   DC    CL80' '
         ORG   OUTREC
OUTDATE  DC    C'  DATE   '
         DC    C','
OUTTIME  DC    C'TIME '
         DC    C','
OUTUNIT  DC    C'UNIT'
         DC    C','
OUTVOL   DC    C'VOLUME'
         DC    C','
OUTSG    DC    C'STOGROUP'
         DC    C','
OUTSZ    DC    C'TRKSZ'
         DC    C','
OUTCYL   DC    C'FREEC'
         DC    C','
OUTTRK   DC    C'FREET'
         DC    C','
OUTEXT   DC    C'XTNTS'
         DC    C','
OUTMXC   DC    C'LARGC'
         DC    C','
OUTMXT   DC    C'LARGT'
         ORG
         LTORG
DSSTIN   DCB   DSORG=PS,MACRF=GM,RECFM=FB,LRECL=48,DDNAME=DSSTIN,      +
               EODAD=ENDOFDAT
DSSTOUT  DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,DDNAME=DSSTOUT
         COPY  IMSPDSST
         YREGS
         END   IMDSSTRD
